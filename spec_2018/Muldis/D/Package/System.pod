=pod

=encoding utf8

=head1 NAME

Muldis::D::Package::System - Muldis D primary data types and operators

=head1 VERSION

This document is Muldis::D::Package::System version 0.300.0.

=head1 SYNOPSIS

 `Muldis_Content_Predicate
 MCP version https://muldis.com 0.300.0 MCP
 MCP script text Unicode 2.1 UTF-8 MCP
 MCP syntax Muldis_D_Plain_Text https://muldis.com 0.300.0 MCP
 MCP model Muldis_Data_Language https://muldis.com 0.300.0 MCP
 Muldis_Content_Predicate`
 (\Package : (
    identity : (
        package_base_name : [\My_App],
        authority : "http://mycorp.com",
        version_number : "0",
    ),
    foundation : (
        authority : "https://muldis.com",
        version_number : "0.300.0",
    ),
    uses : (
        MD : (
            package_base_name : [\System],
            authority : "https://muldis.com",
            version_number : "0.300.0",
        ),
    ),
    entry : \$package::main,
    floating : {\$package, \$used::MD, \$used::MD::Unicode_Aliases},
    materials : (
        `TODO: Put example routines etc here, one is a procedure named "main".`
    ),
 ))

=head1 DESCRIPTION

This document defines the Muldis D package
C<System https://muldis.com 0.300.0> which provides the common core
system-defined data types and operators that regular users of the language
would employ directly in their applications and schemas.

See also L<Muldis::D::Standard_Library> to read about the B<Muldis D
Standard Library> specification, of which this document is a part.

=head1 PACKAGE

 `Muldis_Content_Predicate
 MCP version https://muldis.com 0.300.0 MCP
 MCP script text Unicode 2.1 UTF-8 MCP
 MCP syntax Muldis_D_Plain_Text https://muldis.com 0.300.0 MCP
 MCP model Muldis_Data_Language https://muldis.com 0.300.0 MCP
 Muldis_Content_Predicate`
 (\Package : (
    identity : (
        package_base_name : [\System],
        authority : "https://muldis.com",
        version_number : "0.300.0",
    ),
    foundation : (
        authority : "https://muldis.com",
        version_number : "0.300.0",
    ),
    floating : {\$package},
    materials : (
        ...
    ),
 ))

=head1 MAXIMAL AND MINIMAL DATA TYPES

=head2 Any

        Any : (\Function : (
            is_type_definer : True,
            evaluates : True,
            default : False,
        )),

The selection type definer C<Any> represents the infinite
I<universal type>, which is the maximal data type of the entire Muldis D
type system and consists of all values which can possibly exist.  It also
represents the infinite Muldis D Foundation type C<foundation::Any>.  Its
default value is C<False>.  Other programming languages may name their
corresponding types I<Object> or I<Universal>.

=head2 None

        None : (\Function : (
            is_type_definer : True,
            evaluates : False,
        )),

The selection type definer C<None> represents the finite I<empty type>,
which is the minimal data type of the entire Muldis D type system and
consists of exactly zero values.  It can not have any default value.

=head2 same =

        same : (\Function : (
            returns : \$Boolean,
            matches : (\$Any, \$Any),
            is_commutative : True,
            evaluates : (evaluates args --> \foundation::Any_same()),
        )),

        '=' : (\Alias : ( of : \$same, )),

The function C<same> aka C<=> results in C<True> iff its 2 arguments C<0>
and C<1> are exactly the same value, and C<False> otherwise.  Other
programming languages may name their corresponding operators I<==> or
I<===> or I<eq>.

Note that C<same> is guaranteed to result in C<False> when exactly one of
its 2 arguments is an C<External> value but beyond that its behaviour when
both of its arguments are C<External> values is implementation defined, in
that implementations are expected to make it completely deterministic
according to appropriate rules of value distinctness for the external
environment in question; for example, if the external entity referenced by
an C<External> value is considered a mutable container, then 2 C<External>
should only be considered I<same> if they both point to the same container,
and not if two distinct containers have the same content.

=head2 not_same != ≠

        not_same : (\Function : (
            negates : \$same,
            is_commutative : True,
        )),

        '!=' : (\Alias : ( of : \$not_same, )),

        Unicode_Aliases::'≠' : (\Alias : ( of : \$not_same, )),

The function C<not_same> aka C<!=> aka C<≠> results in C<False> iff its 2
arguments C<0> and C<1> are exactly the same value, and C<True> otherwise.
Other programming languages may name their corresponding operators
I<< <> >> or I<!===> or or I<~=> or I<^=> or I<ne> or I</=> or I<=/=>
or I<=\=>.

=head2 is_a

        is_a : (\Function : (
            returns : \$Boolean,
            matches : (\$Any, \$Signature),
            evaluates : (evaluates (args:.\0,) --> Signature_to_Function_Call_But_0::(args:.\1)),
        )),

The function C<is_a> results in C<True> iff its C<0> argument is a
member of the type specified by its C<1> argument, and C<False> otherwise.
Note that the idiomatic syntax for simply testing if a given value C<v> is
a member of a type named C<T> is C<T v> or C<T::(v)> or C<evaluates \T::() <-- (v,)>
and no generic testing operator is used for the purpose.  And so, the prime
operator name C<is_a> is freed up for its current higher-level use, such
that the type specifier it takes has more of a template format suitable in
particular for concisely defining common cases of structural types, and in
particular routine input and output signatures.

=head2 not_is_a

        not_is_a : (\Function : (
            negates : \$is_a,
        )),

The function C<not_is_a> results in C<False> iff its C<0> argument is a
member of the type specified by its C<1> argument, and C<True> otherwise.

=head1 ORDERABLE DATA TYPES

=head2 Orderable

        Orderable::'' : (\Function : (
            is_type_definer : True,
            is_generalization : True,
        )),

The interface type definer C<Orderable> is semifinite.  An C<Orderable> value has
all of the traditional comparison operators defined for it such that values
of its type I<T> can be deterministically sorted by Muldis D into a
canonical total order.  But I<T> otherwise does not necessarily have
conceptually a total order in the normal sense or that order is different
than what the provided comparison operators give you.  An C<Orderable> type
is a type for which one can take all of its values and place them on a line
such that each value is definitively considered I<before> all of the values
one one side and I<after> all of the values on the other side.  Other
programming languages may name their corresponding types I<IComparable> or
I<Ord> or I<ordered> or I<ordinal>.

The default value of C<Orderable> is the C<Integer> value C<0>.  The
minimum and maximum values of C<Orderable> are C<\!Before_All_Others>
and C<\!After_All_Others>, respectively; these 2 C<Excuse> values are
canonically considered to be before and after, respectively, I<every> other
value of the Muldis D type system, regardless of whether those values are
members a type for which an C<Orderable>-composing type definer exists.  The
primary reason for having these values C<\!Before_All_Others> and
C<\!After_All_Others> is so Muldis D has an easy consistent way to
define an C<Interval> that is partially or completely unbounded, and to use
as I<two-sided identity element> values for chained order-comparisons.

C<Orderable> is composed, directly or indirectly, by:
C<Before_All_Others>, C<After_All_Others>, C<Bicessable>,
C<Boolean>, C<Integral>, C<Integer>, C<Fractional>, C<Fraction>,
C<Stringy>, C<Bits>, C<Blob>, C<Textual>, C<Text>, C<Positional>, C<Array>,
C<Tuple_Array>.

=head2 in_order

        in_order::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Orderable, \$Orderable),
        )),

The virtual function C<in_order> results in C<True> iff its 2 arguments are
considered to already be I<in order> as given to the function, meaning that
either both arguments are exactly the same value or otherwise that the C<0>
argument value is definitively considered I<before> the C<1> argument
value; if C<0> is considered I<after> C<1> then C<in_order> results in
C<False>.  The primary reason for C<in_order> is to underlie all
order-determination or value sorting operations in an easy consistent way;
an C<Orderable>-composing type definer only has to implement C<in_order> and
then values of its type can be compared, sorted on, and have intervals
defined in terms of them.

Other programming languages may instead typically use a three-way
comparison operator for this role, where its possible result values are
I<before>, I<same>, and I<after>, and those 3 are typically represented by
either the integers {-1,0,1} or a special 3-valued enumeration type.  Said
operators may be named I<< <=> >> or I<cmp> or I<compare> or I<CompareTo>
or I<memcmp> or I<strcmp>.  But Muldis D uses a C<Boolean> result instead
partly to keep its core type system simpler (it would have gone the
enumeration route) and partly because the logic for doing sorting or
comparisons or validation is typically much simpler with this foundation.

=head2 in_order (Before_All_Others, After_All_Others)

        in_order::Before_All_Others_L : (\Function : (
            returns : \$Boolean,
            matches : (\$Before_All_Others, \$Orderable),
            implements : \$folder::'',
            evaluates : (True),
        )),

        in_order::Before_All_Others_R : (\Function : (
            returns : \$Boolean,
            matches : (\$Orderable, \$Before_All_Others),
            implements : \$folder::'',
            evaluates : (args:.\0 = args:.\1),
        )),

        in_order::After_All_Others_L : (\Function : (
            returns : \$Boolean,
            matches : (\$After_All_Others, \$Orderable),
            implements : \$folder::'',
            evaluates : (args:.\0 = args:.\1),
        )),

        in_order::After_All_Others_R : (\Function : (
            returns : \$Boolean,
            matches : (\$Orderable, \$After_All_Others),
            implements : \$folder::'',
            evaluates : (True),
        )),

The 4 functions {C<in_order::Before_All_Others_L>,
C<in_order::Before_All_Others_R>, C<in_order::After_All_Others_L>,
C<in_order::After_All_Others_R>} implement the C<Orderable> virtual
function C<in_order> for the composing types C<Before_All_Others>
and C<After_All_Others>, specifically for comparing either value
with any C<Orderable>.

=head2 before <

        before : (\Function : (
            commutes : \$after,
        )),

        '<' : (\Alias : ( of : \$before, )),

The function C<before> aka C<< < >> results in C<True> iff its C<0>
argument is I<before> its C<1> argument; otherwise it results in C<False>.
Other programming languages may name this operator I<lt>.

=head2 after >

        after : (\Function : (
            negates : \$before_or_same,
        )),

        '>' : (\Alias : ( of : \$after, )),

The function C<after> aka C<< > >> results in C<True> iff its C<0>
argument is I<after> its C<1> argument; otherwise it results in C<False>.
Other programming languages may name this operator I<gt>.

=head2 before_or_same <= ≤

        before_or_same : (\Alias : ( of : \$in_order, )),

        '<=' : (\Alias : ( of : \$before_or_same, )),

        Unicode_Aliases::'≤' : (\Alias : ( of : \$before_or_same, )),

The function C<before_or_same> aka C<< <= >> aka C<≤> results in C<True>
iff its C<0> argument is I<before> its C<1> argument or they are the same
value; otherwise it results in C<False>.  Other programming languages may
name this operator I<le>.

=head2 after_or_same >= ≥

        after_or_same : (\Function : (
            commutes : \$before_or_same,
        )),

        '>=' : (\Alias : ( of : \$after_or_same, )),

        Unicode_Aliases::'≥' : (\Alias : ( of : \$after_or_same, )),

The function C<after_or_same> aka C<< >= >> aka C<≥> results in C<True>
iff its C<0> argument is I<after> its C<1> argument or they are the same
value; otherwise it results in C<False>.  Other programming languages may
name this operator I<ge>.

=head2 min

        min : (\Function : (
            returns : \$Orderable,
            matches : (\$Orderable, \$Orderable),
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            identity : \!After_All_Others,
            evaluates : (if args:.\0 in_order args:.\1 then args:.\0 else args:.\1),
        )),

The function C<min> results in whichever of its 2 arguments is first when
the 2 are sorted I<in order>.  This function is designed to be chained for
any number of values in order to pick the one that sorts
I<before> all of the others.

=head2 max

        max : (\Function : (
            returns : \$Orderable,
            matches : (\$Orderable, \$Orderable),
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            identity : \!Before_All_Others,
            evaluates : (if args:.\0 in_order args:.\1 then args:.\1 else args:.\0),
        )),

The function C<max> results in whichever of its 2 arguments is last when
the 2 are sorted I<in order>.  This function is designed to be chained for
any number of values in order to pick the one that sorts
I<after> all of the others.

=head2 minmax

        minmax : (\Function : (
            returns : (\$Orderable, \$Orderable),
            matches : (\$Orderable, \$Orderable),
            is_commutative : True,
            evaluates : (if args:.\0 in_order args:.\1 then args else (args:.\1, args:.\0)),
        )),

The function C<minmax> results in a binary C<Tuple> containing its 2
arguments sorted I<in order>; the function's result is the same as its
source when the arguments are already in order, and the reverse of that
otherwise, meaning the values of C<0> and C<1> are swapped.

=head1 SUCCESSABLE DATA TYPES

=head2 Successable

        Successable : (\Function : (
            is_type_definer : True,
            is_generalization : True,
        )),

The interface type definer C<Successable> is semifinite.  A C<Successable> value
is a member of a conceptually noncontiguous totally ordered type; it has a
definitive I<successor> value of that type, at least where the given value
isn't the last value.

The primary reason for C<Successable> is to provide an easy consistent and
terse API for a generator of arbitrary sequences of values of any type.  In
this context, a C<Successable> value defines a complete self-contained
I<state> for a sequence generator, which is everything the generator needs
to know to both emit a I<current> value, which we call the I<asset>, as
well as determine all subsequent values of the sequence without any further
input.  To keep the fundamental general case API simple, there is just the
a monadic function to derive the next state from the current one, and a
monadic function to extract the asset from the current state, so actually
reading a sequence of values requires 2 function calls per value in the
general case.  For some trivial cases of C<Successable>, the I<state> and
I<asset> are one and the same, so just 1 function call per value is needed.
Keep in mind that asset values may repeat in a sequence, so it is not them
but rather the state values that have the total order property.  Other
programming languages may name their corresponding types I<sequence> or
I<iterator> or I<enumerator>.

C<Successable> is a less rigorous analogy to C<Bicessable>, where the
latter also requires the ability to produce the I<predecessor> value of the
given value, as well as the ability to determine if 2 arbitrary values are
in order.  While conceptually a C<Successable> has those features, formally
it is not required to because for some types it may be onerous or
unnecessary for its mandate to support those features; for example,
producing a successor state may disgard information otherwise needed to
recall any of its predecessors.

The default and minimum and maximum values of C<Successable> are the same
as those of C<Orderable>.  C<Successable> is composed, directly or
indirectly, by: C<Bicessable>, C<Boolean>, C<Integral>, C<Integer>.

C<Successable> is intended to be a generalized tool for performing I<list
comprehension> or I<set comprehension>.  The typically idiomatic and more
efficient way to do many kinds of such I<comprehensions> is to use the
features of various C<Homogeneous> types to map an existing list or set to
another using generic member mapping and filtering functions, such as a
list of even integers less than a hundred.  With those cases, the
map/filter approach can permit processing members in any order or in
parallel, and avoiding unnecessary intermediate values.  In contrast, the
primary intended uses of C<Successable> is when either you want to produce
or process a potentially infinite-sized list (lazily) or especially produce
a sequence with uneven step sizes, such as an arbitrary number of
Fibonacci.  This is for cases where it may be necessary to calculate all
the intermediate values in order to arrive at a desired nth one, and doing
them out of sequence or in parallel may not be an option.

=head2 asset

        asset::'' : (\Function : (
            virtual : True,
            returns : \$Any,
            matches : (\$Successable,),
        )),

The virtual function C<asset> results in the I<asset> of its C<0> argument,
which for trivial cases may simply be that same argument.  Other
programming languages may name their corresponding operators I<Current>.

=head2 succ

        succ : (\Function : (
            returns : {\$Successable, \$After_All_Others},
            matches : (\$Successable,),
            evaluates : (args:.\0 nth_succ 1),
        )),

The function C<succ> results in the I<successor> value of its C<0>
argument, or in C<\!After_All_Others> if there is none.  Other
programming languages may name their corresponding operators I<next> or
I<MoveNext>.

=head2 nth_succ

        nth_succ::'' : (\Function : (
            virtual : True,
            returns : {\$Successable, \$After_All_Others},
            matches : (\$Successable, \$Integer_NN),
        )),

The virtual function C<nth_succ> results in the Nth I<successor> value of
its C<0> argument, where N is its C<1> argument, or in
C<\!After_All_Others> if there is none.

=head1 BICESSABLE DATA TYPES

=head2 Bicessable

        Bicessable : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Orderable, \$Successable},
            provides_default_for : {\$Orderable, \$Successable},
        )),

The interface type definer C<Bicessable> is semifinite.  A C<Bicessable> value
is an C<Orderable> value for which, using the same canonical total order
for its type, there exists definitive I<predecessor> and I<successor>
values, at least where the given value isn't the first or last value on the
line respectively.  Similarly, one can take any two values of a
C<Bicessable> type and produce an ordered list of all of that type's values
which are on the line between those two values.  A primary quality of a
type that is C<Orderable> but not C<Bicessable> is that you can take any
two values of that type and then find a third value of that type which lies
between the first two on the line; by definition for a C<Bicessable> type,
there is no third value between one of its values and that value's
predecessor or successor value.  Other programming languages may name their
corresponding types I<ordinal> or categorically as I<enum>.  Note that
while a generic rational numeric type may qualify as an ordinal type by
some definitions of I<ordinal>, since it is possible to count all the
rationals if arranged a particular way, these types would not qualify as
C<Bicessable> here when that ordering is not the same as the one used for
the same type's C<Orderable> comparisons.  The default and minimum and
maximum values of C<Bicessable> are the same as those of C<Orderable>.
C<Bicessable> is composed, directly or indirectly, by: C<Boolean>,
C<Integral>, C<Integer>.

For some C<Bicessable> types, there is the concept of a I<quantum> or
I<step size>, where every consecutive pair of values on that type's value
line are conceptually spaced apart at equal distances; this distance would
be the quantum, and all steps along the value line are at exact multiples
of that quantum.  However, C<Bicessable> types in general don't need to be
like this, and there can be different amounts of conceivable distance
between consecutive values; a C<Bicessable> type is just required to know
where all the values are.  For example, C<Integer> has a quantum while a
type consisting just of prime integers does not.

Note that while mathematics formally defines I<predecessor> and
I<successor> for non-negative integers only, and some other programming
languages extend this concept to real numbers with the meaning I<minus one>
and I<plus one> respectively, Muldis D only formally associates these terms
with the quantum of I<one> for types specifically representing integers;
for C<Bicessable> types in general, the terms just mean prior or next
values and should not be conceptualized as mathematical operations.

=head2 pred

        pred : (\Function : (
            returns : {\$Bicessable, \$Before_All_Others},
            matches : (\$Bicessable,),
            evaluates : (args:.\0 nth_pred 1),
        )),

The function C<pred> results in the I<predecessor> value of its C<0>
argument, or in C<\!Before_All_Others> if there is none.  Other
programming languages may name their corresponding operators I<prior> or
I<previous>.

=head2 nth_pred

        nth_pred::'' : (\Function : (
            virtual : True,
            returns : {\$Bicessable, \$Before_All_Others},
            matches : (\$Bicessable, \$Integer_NN),
        )),

The virtual function C<nth_pred> results in the Nth I<predecessor> value of
its C<0> argument, where N is its C<1> argument, or in
C<\!Before_All_Others> if there is none.

=head1 BOOLABLE DATA TYPES

=head2 Boolable

        Boolable : (\Function : (
            is_type_definer : True,
            is_generalization : True,
        )),

The interface type definer C<Boolable> is semifinite.  A C<Boolable> value has a
canonical way of being cast to a C<Boolean> value in a context-free manner,
as the answer to the non-specific question "Is that so?" on the value taken
in isolation, whatever that would conceivably mean for the value's type.
The idiomatic predicate being asked has to do with whether or not something
exists; for composing numeric types it is asking whether the number is
nonzero; for composing collection types it is asking whether the collection
has any members.  The primary reason for C<Boolable> is to provide an easy
consistent and terse way to ask a common predicate question such as this.
The default value of C<Boolable> is C<False>.  Other programming languages
often have the concept of particular values from a wide variety of types as
being conceptually either I<false> or I<true>, and C<Boolable> is the
formalization of that concept for Muldis D, allowing program code to be
written in a similar style but with more type safety as any treatment of a
value as a C<Boolean> must be made explicit.

C<Boolable> is composed, directly or indirectly, by: C<Boolean>,
C<Numerical>, C<Integral>, C<Integer>, C<Fractional>, C<Fraction>,
C<Emptyable>, C<Stringy>, C<Bits>, C<Blob>, C<Textual>, C<Text>,
C<Homogeneous>, C<Unionable>, C<Discrete>, C<Positional>, C<Array>,
C<Set>, C<Bag>, C<Relational>, C<Tuple_Array>, C<Relation>,
C<Tuple_Bag>, C<Intervalish>, C<Interval>, C<Unionable_Intervalish>,
C<Interval_Set>, C<Interval_Bag>.

While conceivably C<Boolable> could also be composed by C<Attributive>, and
hence C<Tuple>, it isn't because that would set up a semantic conflict for
C<Relation> and C<Tuple_Bag> which are collections across 2 dimensions, and
it was decided for those latter types that C<Boolable> would apply to them
explicitly in their C<Homogeneous> dimension (by way of C<Emptyable>) and
not in their C<Attributive> dimension.  As such, the C<Attributive> functions
C<has_any_attrs> and C<is_nullary> are provided as that dimension's direct
analogies to the C<Homogeneous> dimension's C<Boolable>-implementing
C<has_any_members> (C<to_Boolean>/C<so>) and C<is_empty> (C<not_so>) functions.

=head2 to_Boolean so ?

        to_Boolean::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Boolable,),
        )),

        so  : (\Alias : ( of : \$to_Boolean, )),
        '?' : (\Alias : ( of : \$to_Boolean, )),

The virtual function C<to_Boolean> aka C<so> aka C<?> results in C<True>
typically when its C<0> argument is a nonzero number or a nonempty
collection; otherwise it results in C<False>.

=head2 not_so !?

        not_so : (\Function : (
            negates : \$to_Boolean,
        )),

        '!?' : (\Alias : ( of : \$not_so, )),

The function C<not_so> aka C<!?> results in C<True> typically when
its C<0> argument is a number zero or an empty collection; otherwise
it results in C<False>.

=head1 BOOLEAN DATA TYPES

=head2 Boolean

        Boolean : (\Function : (
            is_type_definer : True,
            composes : {\$Bicessable, \$Boolable},
            provides_default_for : {\$Boolable},
            evaluates : \foundation::Boolean(),
            default : False,
        )),

The selection type definer C<Boolean> represents the finite Muldis
D Foundation type C<foundation::Boolean>.  A C<Boolean> value is a general purpose
2-valued logic boolean or I<truth value>, or specifically it is one of the
2 values C<False> and C<True>.  Its default value is C<False>.  C<Boolean>
is both C<Orderable> and C<Bicessable>; its minimum value is C<False> and
its maximum value is C<True>.  Other programming languages frequently don't
have a dedicated boolean type but rather consider values of other types,
typically integer types, to be I<false> or I<true>.

=head2 False ⊥

        False : (\Function : (
            is_type_definer : True,
            constant : False,
        )),

        Unicode_Aliases::'⊥' : (\Alias : ( of : \$False, )),

The singleton type definer C<False> aka C<⊥> represents the boolean logical truth value
I<false> aka I<contradiction>.  Other programming languages frequently use
the integer 0 to represent I<false>.

=head2 True ⊤

        True : (\Function : (
            is_type_definer : True,
            constant : True,
        )),

        Unicode_Aliases::'⊤' : (\Alias : ( of : \$True, )),

The singleton type definer C<True> aka C<⊤> represents the boolean logical truth value
I<true> aka I<tautology>.  Other programming languages frequently use the
integer 1 to represent I<true>.

=head2 in_order (Boolean)

        in_order::Boolean : (\Function : (
            returns : \$Boolean,
            matches : (\$Boolean, \$Boolean),
            implements : \$folder::'',
            evaluates : (!args:.\0 or args:.\1),
        )),

The function C<in_order::Boolean> implements the C<Orderable> virtual
function C<in_order> for the composing type C<Boolean>.

=head2 asset (Boolean)

        asset::Boolean : (\Function : (
            returns : \$Boolean,
            matches : (\$Boolean,),
            implements : \$folder::'',
            evaluates : (args:.\0),
        )),

The function C<asset::Boolean> simply results in its C<0> argument.
This function implements the C<Successable> virtual function C<asset> for
the composing type C<Boolean>.

=head2 nth_pred (Boolean)

        nth_pred::Boolean : (\Function : (
            returns : {\$False, \$Before_All_Others},
            matches : (\$Boolean, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (if args:.\1 = 0 then args:.\0 else if args:.\1 = 1 and args:.\0 then False else \!Before_All_Others),
        )),

The function C<nth_pred::Boolean> implements the C<Bicessable> virtual
function C<nth_pred> for the composing type C<Boolean>.

=head2 nth_succ (Boolean)

        nth_succ::Boolean : (\Function : (
            returns : {\$True, \$After_All_Others},
            matches : (\$Boolean, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (if args:.\1 = 0 then args:.\0 else if args:.\1 = 1 and !args:.\0 then True else \!After_All_Others),
        )),

The function C<nth_succ::Boolean> implements the C<Successable> virtual
function C<nth_succ> for the composing type C<Boolean>.

=head2 to_Boolean (Boolean)

        to_Boolean::Boolean : (\Function : (
            returns : \$Boolean,
            matches : (\$Boolean,),
            implements : \$folder::'',
            evaluates : (args:.\0),
        )),

The function C<to_Boolean::Boolean> performs a logical I<proposition>; it
simply results in its C<0> argument.  This function implements the
C<Boolable> virtual function C<to_Boolean> aka C<so> aka C<?> for the
composing type C<Boolean>.

=head2 not ! ¬

        not : (\Function : (
            negates : \$to_Boolean::Boolean,
        )),

        '!' : (\Alias : ( of : \$not, )),

        Unicode_Aliases::'¬' : (\Alias : ( of : \$not, )),

The function C<not> aka C<!> aka C<¬> performs a logical I<negation> or
I<logical complement>; it results in C<True> iff its C<0> argument is
C<False> and vice-versa.  Other programming languages may name their
corresponding operators I<~> or I<^> or I<N>.

=head2 and ∧

        and : (\Function : (
            returns : \$Boolean,
            matches : (\$Boolean, \$Boolean),
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            identity : True,
            evaluates : (args:.\0 and_then args:.\1),
        )),

        Unicode_Aliases::'∧' : (\Alias : ( of : \$and, )),

The function C<and> aka C<∧> performs a logical I<conjunction>; it results
in C<True> iff its 2 arguments C<0> and C<1> are both C<True>, and C<False>
otherwise.  Other programming languages may name their corresponding
operators I<&> or I<&&> or I<K>.

=head2 nand not_and ⊼ ↑

        nand : (\Function : (
            negates : \$and,
            is_commutative : True,
        )),

        not_and : (\Alias : ( of : \$nand, )),

        Unicode_Aliases::'⊼' : (\Alias : ( of : \$nand, )),
        Unicode_Aliases::'↑' : (\Alias : ( of : \$nand, )),

The function C<nand> aka C<not_and> aka C<⊼> aka C<↑> performs a logical
I<alternative denial>; it results in C<False> iff its 2 arguments C<0> and
C<1> are both C<True>, and C<True> otherwise.

=head2 or ∨

        or : (\Function : (
            returns : \$Boolean,
            matches : (\$Boolean, \$Boolean),
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            identity : False,
            evaluates : (args:.\0 or_else args:.\1),
        )),

        Unicode_Aliases::'∨' : (\Alias : ( of : \$or, )),

The function C<or> aka C<∨> performs a logical I<disjunction>; it results
in C<True> iff at least one of its 2 arguments C<0> and C<1> is C<True>,
and C<False> otherwise.  Other programming languages may name their
corresponding operators I<|> or I<||> or I<A>.

=head2 nor not_or ⊽ ↓

        nor : (\Function : (
            negates : \$or,
            is_commutative : True,
        )),

        not_or : (\Alias : ( of : \$nor, )),

        Unicode_Aliases::'⊽' : (\Alias : ( of : \$nor, )),
        Unicode_Aliases::'↓' : (\Alias : ( of : \$nor, )),

The function C<nor> aka C<not_or> aka C<⊽> aka C<↓> performs a logical
I<joint denial>; it results in C<False> iff at least one of its 2 arguments
C<0> and C<1> is C<True>, and C<True> otherwise.

=head2 xnor iff ↔

        xnor : (\Function : (
            returns : \$Boolean,
            matches : (\$Boolean, \$Boolean),
            is_associative : True,
            is_commutative : True,
            identity : True,
            evaluates : (args:.\0 = args:.\1),
        )),

        iff : (\Alias : ( of : \$xnor, )),

        Unicode_Aliases::'↔' : (\Alias : ( of : \$xnor, )),

The function C<xnor> aka C<iff> aka C<↔> performs a logical
I<biconditional> or I<material equivalence> or I<even parity>; it results
in C<True> iff its 2 arguments C<0> and C<1> are exactly the same value,
and C<False> otherwise.  This function is effectively a C<Boolean>-specific
alias of the function C<same> aka C<=>; it behaves identically to C<same>
when given the same arguments.  Other programming languages may
name their corresponding operators I<E>.

=head2 xor ⊻ ↮

        xor : (\Function : (
            negates : \$xnor,
            is_associative : True,
            is_commutative : True,
            identity : False,
        )),

        Unicode_Aliases::'⊻' : (\Alias : ( of : \$xor, )),
        Unicode_Aliases::'↮' : (\Alias : ( of : \$xor, )),

The function C<xor> aka C<⊻> aka C<↮> performs a logical I<exclusive
disjunction> or I<odd parity>; it results in C<False> iff its 2 arguments
C<0> and C<1> are exactly the same value, and C<True> otherwise.  This
function is effectively a C<Boolean>-specific alias of the function
C<not_same> aka C<!=> aka C<≠>; it behaves identically to C<not_same>
when given the same arguments.  Other programming languages may name their
corresponding operators I<^>.

=head2 imp implies →

        imp : (\Function : (
            returns : \$Boolean,
            matches : (\$Boolean, \$Boolean),
            evaluates : (if args:.\0 then args:.\1 else True),
        )),

        implies : (\Alias : ( of : \$imp, )),

        Unicode_Aliases::'→' : (\Alias : ( of : \$imp, )),

The function C<imp> aka C<implies> aka C<→> performs a logical I<material
implication>; it results in C<False> when its C<0> argument is C<True> and
its C<1> argument is C<False>, and C<True> otherwise.

=head2 nimp not_implies ↛

        nimp : (\Function : (
            negates : \$imp,
        )),

        not_implies : (\Alias : ( of : \$nimp, )),

        Unicode_Aliases::'↛' : (\Alias : ( of : \$nimp, )),

The function C<nimp> aka C<not_implies> aka C<↛> performs a logical
I<material nonimplication>; it results in C<True> when its C<0> argument is
C<True> and its C<1> argument is C<False>, and C<False> otherwise.

=head2 if ←

        if : (\Function : (
            commutes : \$imp,
        )),

        Unicode_Aliases::'←' : (\Alias : ( of : \$if, )),

The function C<if> aka C<←> performs a logical I<converse implication> or
I<reverse material implication>; it results in C<False> when its C<0>
argument is C<False> and its C<1> argument is C<True>, and C<True>
otherwise.

=head2 nif not_if ↚

        nif : (\Function : (
            commutes : \$nimp,
        )),

        not_if : (\Alias : ( of : \$nif, )),

        Unicode_Aliases::'↚' : (\Alias : ( of : \$nif, )),

The function C<nif> aka C<not_if> aka C<↚> performs a logical I<converse
nonimplication>; it results in C<True> when its C<0> argument is C<False>
and its C<1> argument is C<True>, and C<False> otherwise.

=head1 ROUNDING INSTRUCTION DATA TYPES

=head2 Round_Meth

        Round_Meth : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Round_Meth_Attr_Name::(),
                ),
            )),
            default : (\$material : (\To_Zero,)),
        )),

The selection type definer C<Round_Meth> is finite.  When a value of some
C<Orderable> type needs to be mapped into a proper subtype that doesn't
contain that value, such as when mapping an arbitrary number to one with
less precision, some rounding method is applied to determine which value of
the subtype is to be mapped to while most accurately reflecting the
original value.  The C<Round_Meth> type enumerates the rounding methods
that Muldis D operators can typically apply.

With C<Down> (aka I<floor>), C<Up> (aka I<ceiling>), C<To_Zero> (aka
I<truncate>), and C<To_Inf>, the original value will always be mapped to
the single nearest value that is lower than it, or higher than it, or
towards "zero" from it, or towards the nearer infinity from it,
respectively.  With C<Half_Down>, C<Half_Up>, C<Half_To_Zero>,
C<Half_To_Inf>, C<Half_Even> (aka I<unbiased rounding>, I<convergent
rounding>, I<statistician's rounding>, or I<bankers' rounding>), and
C<Half_Odd> the original value will be mapped to the single target value
that it is nearest to, if there is one; otherwise, if it is exactly
half-way between 2 adjacent target values, then C<Half_Down> will round
towards negative infinity, C<Half_Up> will round towards positive infinity,
C<Half_To_Zero> will round towards "zero", C<Half_To_Inf> will round
towards the nearer infinity, while C<Half_Even> and C<Half_Odd> will round
towards the nearest "even" or "odd" target respectively.

The default value of C<Round_Meth> is C<To_Zero>, which is the simplest.
Other programming languages may name their corresponding types
I<RoundingMode> (Java) or I<MidpointRounding> (C#).

=head2 Round_Meth_Attr_Name

        Round_Meth_Attr_Name : (\Function : (
            is_type_definer : True,
            evaluates : [\Attr_Name::(), \'⊆$'::( 1:
                \$(Down,Up,To_Zero,To_Inf
                ,Half_Down,Half_Up,Half_To_Zero,Half_To_Inf
                ,Half_Even,Half_Odd)
            )],
            default : \To_Zero,
        )),

The selection type definer C<Round_Meth_Attr_Name> represents the finite type
consisting just of the C<Attr_Name> values that are valid Article attribute
assets of C<Round_Meth> values.

=head2 RM

        RM : (\Function : (
            returns : \$Round_Meth,
            matches : (\$Round_Meth_Attr_Name,),
            evaluates : ((\$Round_Meth : (args:.\0,))),
        )),

The function C<RM> results in the C<Round_Meth> value selected in
terms of the C<Attr_Name> of its C<0> argument.

=head1 NUMERICAL DATA TYPES

=head2 Numerical

        Numerical : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Boolable},
        )),

The interface type definer C<Numerical> is semifinite.  A C<Numerical> value
either is a simple number of some kind or is something that can act as a
simple number.  A I<simple number> means, typically speaking, any rational
real number, those numbers that can be derived simply by multiplying or
dividing integers.  All operators defined by the C<System> package
for C<Numerical> are expect to be closed over the real rational numbers,
and consist mainly of addition, subtraction, multiplication, and division
of any rationals, plus exponentiation of any rationals to integer powers
only.  Idiomatically a C<Numerical> is a pure number which does not
represent any kind of thing in particular, neither cardinal nor ordinal nor
nominal; however some types which do represent such a particular kind of
thing may choose to compose C<Numerical> because it makes sense to provide
its operators.  The default value of C<Numerical> is the C<Integer> value
C<0>.  A C<Numerical> in the general case is not C<Orderable>, but often a
type that is numeric is also orderable.  Other programming languages may
name their corresponding types I<Numeric>.

C<Numerical> is composed, directly or indirectly, by: C<Integral>,
C<Integer>, C<Fractional>, C<Fraction>, C<Quantitative>, C<Quantity>.
It is also composed by a lot of additional type definers defined by other
Muldis D packages such as L<System::Math|Muldis::D::Package::System::Math>;
these include types for irrational or algebraic or complex numbers or
quaternions or rational types with a fixed precision or scale or
floating-point types and so on.

=head2 to_Boolean (Numerical)

        to_Boolean::Numerical : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Numerical,),
            implements : \$folder::'',
        )),

The virtual function C<to_Boolean::Numerical> results in C<True> when its
C<0> argument is a nonzero number; otherwise it results in C<False>.  This
function implements the C<Boolable> virtual function C<to_Boolean> aka
C<so> aka C<?> for the composing type C<Numerical>.

=head2 is_zero

        is_zero : (\Function : (
            negates : \$to_Boolean::Numerical,
        )),

The function C<is_zero> results in C<True> when its C<0> argument is a
number zero; otherwise it results in C<False>.

=head2 zero

        zero::'' : (\Function : (
            virtual : True,
            returns : \$Numerical,
            matches : (\$Numerical,),
        )),

The virtual function C<zero> results in the number zero of its C<0>
argument's numerical type, assuming that every type composing C<Numerical>
has one.

=head2 opposite

        opposite::'' : (\Function : (
            virtual : True,
            returns : \$Numerical,
            matches : (\$Numerical,),
        )),

        additive_inverse : (\Alias : ( of : \$opposite, )),

The virtual function C<opposite> aka C<additive_inverse> aka unary C<-> aka
unary C<−> results in the numeric I<opposite> or I<negation> or I<additive
inverse> or I<unary minus> of its C<0> argument, and is a shorthand for
either multiplying that argument by negative one or subtracting it from
zero.  By definition, the sum of a number and its opposite is zero.

=head2 reciprocal

        reciprocal::'' : (\Function : (
            virtual : True,
            returns : {\$Numerical, \$Div_By_Zero},
            matches : (\$Numerical,),
        )),

        multiplicative_inverse : (\Alias : ( of : \$reciprocal, )),

The virtual function C<reciprocal> aka C<multiplicative_inverse> results in
the numeric I<reciprocal> or I<multiplicative inverse> of its nonzero C<0>
argument, and is a shorthand for dividing one by that argument.  By
definition, the product of a number and its reciprocal is one.  The result
is always C<Fractional> for both C<Integral> and C<Fractional> arguments.
The result is only I<defined> when the argument is a nonzero number; it is
C<\!Div_By_Zero> otherwise.

=head2 modulus abs

        modulus::'' : (\Function : (
            virtual : True,
            returns : \$Numerical,
            matches : (\$Numerical,),
        )),

        abs : (\Alias : ( of : \$modulus, )),

The virtual function C<modulus> aka C<abs> results in the numeric
I<modulus> or I<absolute value> of its C<0> argument, which is the
(non-negative) distance between that argument and zero.  Note that typical
mathematical notion writes this operator in circumfix like I<|n|>.

=head2 plus +

        plus::'' : (\Function : (
            virtual : True,
            returns : \$Numerical,
            matches : (\$Numerical, \$Numerical),
            is_associative : True,
            is_commutative : True,
            repeater : \$times,
        )),

        '+' : (\Alias : ( of : \$plus, )),

The virtual function C<plus> aka C<+> results in the numeric I<sum> from
performing I<addition> of its 2 I<summand> arguments C<0> (I<augend>) and
C<1> (I<addend>).  This operation has a I<two-sided identity element> value of a number zero.

=head2 minus

        minus::'' : (\Function : (
            virtual : True,
            returns : \$Numerical,
            matches : (\$Numerical, \$Numerical),
        )),

The virtual function C<minus> aka binary C<-> aka binary C<−> results in
the numeric I<difference> from performing I<subtraction> of its 2 arguments
C<0> (I<minuend>) and C<1> (I<subtrahend>).  This operation has a I<right
identity element> value of a number zero.

=head2 - −

        '-' : (\Function : (
            returns : \$Numerical,
            matches : {(\$Numerical), (\$Numerical, \$Numerical)},
            evaluates : (evaluates args --> (if degree::(args) = 1 then \opposite::() else \minus::())),
        )),

        Unicode_Aliases::'−' : (\Alias : ( of : '-' )),

The function C<-> aka C<−> is a proxy for either of the virtual functions
unary C<opposite> and binary C<minus>, depending on how many arguments it
was invoked with.

=head2 modulus_minus abs_minus |-| |−|

        modulus_minus : (\Function : (
            returns : \$Numerical,
            matches : (\$Numerical, \$Numerical),
            is_commutative : True,
            evaluates : (modulus args:.\0 - args:.\1),
        )),

        abs_minus : (\Alias : ( of : \$modulus_minus, )),
        '|-|'     : (\Alias : ( of : \$modulus_minus, )),

        Unicode_Aliases::'|−|' : (\Alias : ( of : \$modulus_minus, )),

The function C<modulus_minus> aka C<abs_minus> aka C<|-|> aka C<|−|>
results in the numeric I<absolute difference> of its 2 arguments C<0> and
C<1>, which is the (non-negative) distance between those arguments.

=head2 times * ×

        times::'' : (\Function : (
            virtual : True,
            returns : \$Numerical,
            matches : (\$Numerical, \$Numerical),
            is_associative : True,
            is_commutative : True,
            repeater : \$integral_nn_power,
        )),

        '*' : (\Alias : ( of : \$times, )),

        Unicode_Aliases::'×' : (\Alias : ( of : \$times, )),

The virtual function C<times> aka C<*> aka C<×> results in the numeric
I<product> from performing I<multiplication> of its 2 I<factor> arguments
C<0> (I<multiplier>) and C<1> (I<multiplicand>).  This operation has a
I<two-sided identity element> value of a number positive one.

=head2 multiple_of

        multiple_of::'' : (\Function : (
            virtual : True,
            returns : {\$Boolean, \$Div_By_Zero},
            matches : (\$Numerical, \$Numerical),
        )),

The virtual function C<multiple_of> results in C<True> iff its C<0>
argument is an even multiple of its C<1> argument (that is, the former is
evenly divisible by the latter), and C<False> otherwise.  The result is
only I<defined> when the C<1> argument is a nonzero number; it is
C<\!Div_By_Zero> otherwise.  Other programming languages may name their
corresponding operators I<%%>.

=head2 nearest_multiple_of round

        nearest_multiple_of : (\Function : (
            returns : {\$Numerical, \$Div_By_Zero},
            matches : (\$Numerical, \$Numerical, \$Round_Meth),
            evaluates : (if is_zero args:.\1 then \!Div_By_Zero
                else guard args:.\1 * (args:.\0 div args:.\1)),
        )),

        round : (\Alias : ( of : \$nearest_multiple_of, )),

The function C<nearest_multiple_of> aka C<round> results in the same or
nearest number to its C<0> argument that is an even multiple of its C<1>
argument (that is, the result is evenly divisible by the latter), where the
nearest is determined by the rounding method specified by the C<2>
argument.  For the common case of rounding to the nearest integer, use a
C<1> argument of positive one.  The result is C<Integral> for C<Integral>
arguments and is C<Fractional> for C<Fractional> arguments.  The result is
only I<defined> when the C<1> argument is a nonzero number; it is
C<\!Div_By_Zero> otherwise.  Other programming languages may name their
corresponding operators I<truncate> or I<int> or I<floor> or I<ceil> or
other things, some of which would always round to a multiple of one.

=head2 fractional_divided_by / ÷ ∕

        fractional_divided_by::'' : (\Function : (
            virtual : True,
            returns : {\$Numerical, \$Div_By_Zero},
            matches : (\$Numerical, \$Numerical),
        )),

        '/' : (\Alias : ( of : \$fractional_divided_by, )),

        Unicode_Aliases::'÷' : (\Alias : ( of : \$fractional_divided_by, )),
        Unicode_Aliases::'∕' : (\Alias : ( of : \$fractional_divided_by, )),

The virtual function C<fractional_divided_by> aka C</> aka C<÷> aka C<∕>
results in the typically-fractional numeric I<quotient> from performing
I<division> of its 2 arguments C<0> (I<dividend> or I<numerator>) and C<1>
(I<divisor> or I<denominator>) using the semantics of real number division.
The result is always C<Fractional> for both C<Integral> and C<Fractional>
arguments; as such, C<fractional_divided_by> is the idiomatic way to select
any C<Fraction> values in terms of C<Integer> values.  The result is only
I<defined> when the C<1> argument is a nonzero number; it is C<\!Div_By_Zero>
otherwise.  This operation has a I<right identity element> value of a
number positive one.

=head2 integral_divided_by div

        integral_divided_by::'' : (\Function : (
            virtual : True,
            returns : {\$Numerical, \$Div_By_Zero},
            matches : (\$Numerical, \$Numerical, \$Round_Meth),
        )),

        div : (\Alias : ( of : \$integral_divided_by, )),

The virtual function C<integral_divided_by> aka C<div> results in the
integral numeric I<quotient> from performing I<division> of its 2 arguments
C<0> (I<dividend> or I<numerator>) and C<1> (I<divisor> or I<denominator>)
using the semantics of real number division, whereupon the real number
result is rounded to the same or nearest integral number, where the nearest
is determined by the rounding method specified by the C<2> argument.  The
result is C<Integral> for C<Integral> arguments and is C<Fractional> with a
I<denominator> of one for C<Fractional> arguments.  The result is only
I<defined> when the C<1> argument is a nonzero number; it is C<\!Div_By_Zero>
otherwise.  This operation has a I<right identity element> value of a
number positive one.

=head2 modulo mod

        modulo : (\Function : (
            returns : {\$Numerical, \$Div_By_Zero},
            matches : (\$Numerical, \$Numerical, \$Round_Meth),
            evaluates : (if is_zero args:.\1 then \!Div_By_Zero
                else guard args:.\0 - (args:.\0 nearest_multiple_of args:.\1)),
        )),

        mod : (\Alias : ( of : \$modulo, )),

The function C<modulo> aka C<mod> results in the possibly-fractional
numeric I<remainder> from performing same I<division> operation as
C<integral_divided_by> does with all of the same arguments; to be specific,
C<modulo> preserves the identity C<x mod y = x - y * (x div y)>.  The
result is C<Integral> for C<Integral> arguments and is C<Fractional> for
C<Fractional> arguments.  The result is only I<defined> when the C<1>
argument is a nonzero number; it is C<\!Div_By_Zero> otherwise.  Other
programming languages may name their corresponding operators I<%> or I<//>
or I<\\> or I<div> or I<rem> or I<remainder> or various other things.

=head2 divided_by_and_modulo

        divided_by_and_modulo::'' : (\Function : (
            returns : ({\$Numerical, \$Div_By_Zero}, {\$Numerical, \$Div_By_Zero}),
            matches : (\$Numerical, \$Numerical, \$Round_Meth),
            evaluates : ((args:.\0 div args:.\1, args:.\0 mod args:.\1)),
        )),

The function C<divided_by_and_modulo> results in a binary tuple whose C<0>
and C<1> attributes have the exact same values that C<integral_divided_by>
and C<modulo> would result in, respectively, when each is given all of the
same arguments.  This function is a shorthand for invoking the other two.

=head2 integral_power **

        integral_power::'' : (\Function : (
            virtual : True,
            returns : {\$Numerical, \$Zero_To_The_Zero},
            matches : (\$Numerical, \$Integral),
        )),

        '**' : (\Alias : ( of : \$integral_power, )),

The virtual function C<integral_power> aka C<**> results in a
typically-fractional number from performing I<exponentiation> of its 2
arguments C<0> (I<base>) and C<1> (I<exponent> or I<power>).  The result is
always C<Fractional> for both an C<Integral> and a C<Fractional> C<0>
argument.  The result is only I<defined> when at least one of the arguments
C<0> and C<1> is a nonzero number; it is C<\!Zero_To_The_Zero> otherwise.  Other
programming languages may name their corresponding operators I<exp> or I<^>.

=head2 integral_nn_power power

        integral_nn_power::'' : (\Function : (
            virtual : True,
            returns : {\$Numerical, \$Zero_To_The_Zero},
            matches : (\$Numerical, \$Integral_NN),
        )),

        power : (\Alias : ( of : \$integral_nn_power, )),

The virtual function C<integral_nn_power> aka C<power> results in a
possibly-fractional number from performing I<exponentiation> of its 2
arguments C<0> (I<base>) and C<1> (I<exponent> or I<power>).  The result is
C<Integral> for an C<Integral> C<0> argument and is C<Fractional> for a
C<Fractional> C<0> argument.  The result is only I<defined> when at least
one of the arguments C<0> and C<1> is a nonzero number; it is C<\!Zero_To_The_Zero>
otherwise.

=head1 INTEGRAL DATA TYPES

=head2 Integral

        Integral::'' : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Bicessable, \$Numerical},
            provides_default_for : {\$Bicessable, \$Numerical},
        )),

The interface type definer C<Integral> is semifinite.  An C<Integral> value
either is an exact integral number of some kind or is something that can
act as such.  Idiomatically an C<Integral> is a pure integer which does not
represent any kind of thing in particular, neither cardinal nor ordinal nor
nominal; however some types which do represent such a particular kind of
thing may choose to compose C<Integral> because it makes sense to provide
its operators.  The default value of C<Integral> is the C<Integer> value
C<0>.  C<Integral> is both C<Orderable> and C<Bicessable>.  For each type
composing C<Integral>, a value closer to negative infinity is ordered
before a value closer to positive infinity, and the definition of
I<predecessor> and I<successor> is exactly equal to subtracting or adding
an integer positive-one respectively, while other C<Bicessable> don't
generally mean that.  In the general case, C<Integral> has no minimum or
maximum value, but often a type that is C<Integral> will have them.
C<Integral> is composed by C<Integer>.

=head2 Integral_NN

        Integral_NN : (\Function : (
            is_type_definer : True,
            evaluates : (Integral args:.\0 and_then guard
                args:.\0 >= zero::(args:.\0)),
        )),

The selection type definer C<Integral_NN> represents the infinite type
consisting just of the C<Integral> values that are non-negative.  Its
default and minmum value is C<0>; it has no maximum value.

=head2 Integral_P

        Integral_P : (\Function : (
            is_type_definer : True,
            evaluates : (Integral_NN::(args:.\0) and_then guard
                args:.\0 > zero::(args:.\0)),
            default : (succ::(Integral::())),
        )),

The selection type definer C<Integral_P> represents the infinite type
consisting just of the C<Integral_NN> values that are positive.  Its
default and minmum value is C<1>; it has no maximum value.

=head2 --

        '--' : (\Function : (
            returns : {\$Integral, \$Before_All_Others},
            matches : (\$Integral,),
            evaluates : (pred args:.\0),
        )),

The function C<--> results in the I<predecessor> value of its C<0>
argument, or in C<\!Before_All_Others> if there is none.  It is an integral numeric
specific alias for the C<Bicessable> virtual function C<pred>.  Other
programming languages may name their corresponding operators I<decrement>.

=head2 ++

        '++' : (\Function : (
            returns : {\$Integral, \$After_All_Others},
            matches : (\$Integral,),
            evaluates : (succ args:.\0),
        )),

The function C<++> results in the I<successor> value of its C<0> argument,
or in C<\!After_All_Others> if there is none.  It is an integral numeric specific
alias for the C<Successable> virtual function C<succ>.  Other programming
languages may name their corresponding operators I<increment>.

=head2 to_Integer

        to_Integer::'' : (\Function : (
            virtual : True,
            returns : \$Integer,
            matches : (\$Integral,),
        )),

The virtual function C<to_Integer> results in the C<Integer> value that
represents the same integer value as its C<0> argument.  The purpose of
C<to_Integer> is to canonicalize C<Integral> values so they can be compared
abstractly as integers, or so that it is easier to do exact math with
integers without running afoul of possible range limits of fixed-size
C<Integral> types, just dealing with the latter for storage.

=head2 factorial

        factorial::'' : (\Function : (
            virtual : True,
            returns : \$Integral_P,
            matches : (\$Integral_NN,),
        )),

The virtual function C<factorial> results in the integral numeric
I<factorial> of its C<0> argument, and is a shorthand for the product of
every integer between 1 and that argument; the factorial of zero is defined
to result in positive one.  Note that typical mathematical notion writes
this operator in postfix like I<n!>.

=head2 gcd greatest_common_divisor

        gcd : (\Function : (
            returns : \$Integral_P,
            matches : (\$Integral_NN, \$Integral_NN),
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            evaluates : ((if is_zero args:.\1 then args:.\0 else guard material::(args:.\1, mod::(args:.\0, args:.\1, RM::(\To_Zero))))
                note "Calculate using the Euclidean algorithm."),
        )),

        greatest_common_divisor : (\Alias : ( of : \$gcd, )),

The function C<gcd> aka C<greatest_common_divisor> results in the integral
numeric I<greatest common divisor> of its 2 arguments C<0> and C<1>, which
is the largest integer that will divide both arguments evenly.
I<TODO: Look into generalizing it to take negative integer arguments too.>

=head2 lcm least_common_multiple

        lcm : (\Function : (
            returns : \$Integral_NN,
            matches : (\$Integral_NN, \$Integral_NN),
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            evaluates : (if is_zero args:.\0 or is_zero args:.\1 then zero args:.\0
                else guard div::(args:.\0 * args:.\1, args:.\0 gcd args:.\1, RM::(\To_Zero))),
        )),

        least_common_multiple : (\Alias : ( of : \$lcm, )),

The function C<lcm> aka C<least_common_multiple> results in the integral
numeric I<least common multiple> of its 2 arguments C<0> and C<1>, which
is the smallest integer that is an even multiple of both arguments.
I<TODO: Look into generalizing it to take negative integer arguments too.>

=head2 coprime

        coprime : (\Function : (
            returns : \$Boolean,
            matches : (\$Integral, \$Integral),
            evaluates : ((abs::(args:.\0) gcd abs::(args:.\1)) = succ::(zero args:.\0)),
        )),

The function C<coprime> results in C<True> iff its 2 arguments C<0> and
C<1> are coprime (their I<greatest common divisor> is 1), and C<False>
otherwise.

=head1 INTEGER DATA TYPES

=head2 Integer

        Integer::'' : (\Function : (
            is_type_definer : True,
            composes : {\$Integral},
            provides_default_for : {\$Integral},
            evaluates : \foundation::Integer(),
            default : 0,
        )),

The selection type definer C<Integer> represents
the infinite Muldis D Foundation type C<foundation::Integer>.  An C<Integer>
value is a general purpose exact integral number of any magnitude, which
explicitly does not represent any kind of thing in particular, neither
cardinal nor ordinal nor nominal.  Its default value is C<0>.  C<Integer>
is both C<Orderable> and C<Bicessable>; it has no minimum or maximum value.
Other programming languages may name their corresponding types I<BigInt>
or I<Bignum> or I<BigInteger>.

=head2 Integer_NN

        Integer_NN : (\Function : (
            is_type_definer : True,
            evaluates : [\Integer::(), \'>='::( 1: 0 )],
        )),

The selection type definer C<Integer_NN> represents the infinite type
consisting just of the C<Integer> values that are non-negative.  Its
default and minmum value is C<0>; it has no maximum value.

=head2 Integer_P

        Integer_P : (\Function : (
            is_type_definer : True,
            evaluates : [\Integer_NN::(), \'>'::( 1: 0 )],
            default : 1,
        )),

The selection type definer C<Integer_P> represents the infinite type consisting
just of the C<Integer_NN> values that are positive.  Its default and minmum
value is C<1>; it has no maximum value.

=head2 in_order (Integer)

        in_order::Integer : (\Function : (
            returns : \$Boolean,
            matches : (\$Integer, \$Integer),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Integer_in_order()),
        )),

The function C<in_order::Integer> implements the C<Orderable> virtual
function C<in_order> for the composing type C<Integer>.

=head2 asset (Integer)

        asset::Integer : (\Function : (
            returns : \$Integer,
            matches : (\$Integer,),
            implements : \$folder::'',
            evaluates : (args:.\0),
        )),

The function C<asset::Integer> simply results in its C<0> argument.
This function implements the C<Successable> virtual function C<asset> for
the composing type C<Integer>.

=head2 nth_pred (Integer)

        nth_pred::Integer : (\Function : (
            returns : \$Integer,
            matches : (\$Integer, \$Integer_NN),
            implements : \$folder::Integral,
            evaluates : (args:.\0 - args:.\1),
        )),

The function C<nth_pred::Integer> implements the C<Bicessable>
virtual function C<nth_pred> for the composing type C<Integer>.

=head2 nth_succ (Integer)

        nth_succ::Integer : (\Function : (
            returns : \$Integer,
            matches : (\$Integer, \$Integer_NN),
            implements : \$folder::Integral,
            repeater : \$plus::Integer,
            evaluates : (args:.\0 + args:.\1),
        )),

The function C<nth_succ::Integer> implements the C<Successable>
virtual function C<nth_succ> for the composing type C<Integer>.

=head2 to_Boolean (Integer)

        to_Boolean::Integer : (\Function : (
            returns : \$Boolean,
            matches : (\$Integer,),
            implements : \$folder::'',
            evaluates : (args:.\0 != 0),
        )),

The function C<to_Boolean::Integer> results in C<True> iff its C<0>
argument is not C<0>, and in C<False> if it is C<0>.  This function
implements the C<Boolable> virtual function C<to_Boolean> aka C<so> aka
C<?> for the composing type C<Integer>.

=head2 zero (Integer)

        zero::Integer : (\Function : (
            returns : \$Integer,
            matches : (\$Integer,),
            implements : \$folder::'',
            evaluates : (0),
        )),

The function C<zero::Integer> simply results in C<0>.  This function
implements the C<Numerical> virtual function C<zero> for the composing type
C<Integer>.

=head2 opposite (Integer)

        opposite::Integer : (\Function : (
            returns : \$Integer,
            matches : (\$Integer,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Integer_opposite()),
        )),

The function C<opposite::Integer> implements the C<Numerical> virtual
function C<opposite> aka C<additive_inverse> aka unary C<-> aka unary C<−>
for the composing type C<Integer>.

=head2 reciprocal (Integer)

        reciprocal::Integer : (\Function : (
            returns : {\$Fraction, \$Div_By_Zero},
            matches : (\$Integer,),
            implements : \$folder::'',
            evaluates : (1 / args:.\0),
        )),

The function C<reciprocal::Integer> implements the C<Numerical> virtual
function C<reciprocal> aka C<multiplicative_inverse> for the composing type
C<Integer>.

=head2 modulus (Integer)

        modulus::Integer : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Integer,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Integer_modulus()),
        )),

The function C<modulus::Integer> implements the C<Numerical>
virtual function C<modulus> aka C<abs> for the composing type C<Integer>.

=head2 plus (Integer)

        plus::Integer : (\Function : (
            returns : \$Integer,
            matches : (\$Integer, \$Integer),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            identity : 0,
            repeater : \$times::Integer,
            evaluates : (evaluates args --> \foundation::Integer_plus()),
        )),

The function C<plus::Integer> implements the C<Numerical>
virtual function C<plus> aka C<+> for the composing type C<Integer>.

=head2 minus (Integer)

        minus::Integer : (\Function : (
            returns : \$Integer,
            matches : (\$Integer, \$Integer),
            implements : \$folder::'',
            right_identity : 0,
            evaluates : (evaluates args --> \foundation::Integer_minus()),
        )),

The function C<minus::Integer> implements the C<Numerical> virtual function
C<minus> aka binary C<-> aka binary C<−> for the composing type C<Integer>.

=head2 times (Integer)

        times::Integer : (\Function : (
            returns : \$Integer,
            matches : (\$Integer, \$Integer),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            identity : 1,
            repeater : \$integral_nn_power::Integer,
            evaluates : (evaluates args --> \foundation::Integer_times()),
        )),

The function C<times::Integer> implements the C<Numerical> virtual function
C<times> aka C<*> aka C<×> for the composing type C<Integer>.

=head2 multiple_of (Integer)

        multiple_of::Integer : (\Function : (
            returns : {\$Boolean, \$Div_By_Zero},
            matches : (\$Integer, \$Integer),
            implements : \$folder::'',
            evaluates : (if args:.\1 = 0 then \!Div_By_Zero
                else guard evaluates args --> \foundation::Integer_multiple_of()),
        )),

The function C<multiple_of::Integer> implements the C<Numerical>
virtual function C<multiple_of> for the composing type C<Integer>.

=head2 fractional_divided_by (Integer)

        fractional_divided_by::Integer : (\Function : (
            returns : {\$Fraction, \$Div_By_Zero},
            matches : (\$Integer, \$Integer),
            implements : \$folder::'',
            right_identity : 1,
            evaluates : (
                n ::= args:.\0;
                d ::= args:.\1;

                returns if d = 0 then \!Div_By_Zero else guard q;

                q ::= (\Fraction : (
                    numerator   : div::((if d > 0 then n else -n), gcd, RM::(\To_Zero)),
                    denominator : div::((if d > 0 then d else -d), gcd, RM::(\To_Zero)),
                ));

                gcd ::= gcd::(abs::(n), abs::(d));
            ),
        )),

The function C<fractional_divided_by::Integer> implements the C<Numerical>
virtual function C<fractional_divided_by> aka C</> aka C<÷> aka C<∕> for
the composing type C<Integer>.

=head2 integral_divided_by (Integer)

        integral_divided_by::Integer : (\Function : (
            returns : {\$Integer, \$Div_By_Zero},
            matches : (\$Integer, \$Integer, \$Round_Meth),
            implements : \$folder::'',
            right_identity : 1,
            evaluates : (
                dividend   ::= args:.\0;
                divisor    ::= args:.\1;
                round_meth ::= args:.\2;

                returns if divisor = 0 then \!Div_By_Zero else guard e1;

                e1 note "This is the case where we are dividing by a non-zero.";

                e1 ::=   if dividend = 0       then 0
                    else if divisor  = 1       then dividend
                    else if dividend = divisor then 1
                    else if divisor  = -1      then -dividend
                    else                            e2
                ;

                e2 note "This is the case where the divisor and dividend do not"
                    " equal each other and neither of them is a zero or a one.";

                e2 ::= (
                    real_q_is_neg ::= dividend < 0 xor divisor < 0;
                    rtz_quotient  ::= foundation::Integer_divided_by_rtz(dividend, divisor);
                    rtz_remainder ::= dividend - (divisor * rtz_quotient);

                    returns if rtz_remainder = 0 then rtz_quotient else e3;
                );

                e3 note "This is the case where the divisor does not divide the"
                    " dividend evenly and the real number division result would"
                    " have a fractional part, so we decide how to round that.";

                e3 ::= (
                    rti_quotient ::= rtz_quotient + (if real_q_is_neg then -1 else 1);
                    rdn_quotient ::= rtz_quotient + (if real_q_is_neg then -1 else 0);
                    rup_quotient ::= rtz_quotient + (if real_q_is_neg then  0 else 1);

                    returns given round_meth
                        when RM::(\Down)    then rdn_quotient
                        when RM::(\Up)      then rup_quotient
                        when RM::(\To_Zero) then rtz_quotient
                        when RM::(\To_Inf)  then rti_quotient
                        default
                                 if (2 * abs::(rtz_remainder)) < abs::(divisor) then rtz_quotient
                            else if (2 * abs::(rtz_remainder)) > abs::(divisor) then rti_quotient
                            else e4
                    ;
                );

                e4 note "This is the case where real division remainder is"
                    " exactly one-half so we decide how to round that.";

                e4 ::= (
                    q ::= foundation::Integer_divided_by_rtz(abs::(rtz_quotient),2)
                    r ::= abs::(rtz_quotient) - (2 * q);
                    rtz_quotient_is_even ::= r = 0;

                    returns given round_meth
                        when RM::(\Half_Down)    then rdn_quotient
                        when RM::(\Half_Up)      then rup_quotient
                        when RM::(\Half_To_Zero) then rtz_quotient
                        when RM::(\Half_To_Inf)  then rti_quotient
                        when RM::(\Half_Even)    then
                            (if rtz_quotient_is_even then rtz_quotient else rti_quotient)
                        when RM::(\Half_Odd)     then
                            (if rtz_quotient_is_even then rti_quotient else rtz_quotient)
                        default fail  `oops, an unhandled case`
                    ;
                );
            ),
        )),

The function C<integral_divided_by::Integer> implements the C<Numerical>
virtual function C<integral_divided_by> aka C<div> for the composing type
C<Integer>.

=head2 integral_power (Integer)

        integral_power::Integer : (\Function : (
            returns : {\$Fraction, \$Zero_To_The_Zero},
            matches : (\$Integer, \$Integer),
            implements : \$folder::'',
            evaluates : (if args:.\0 = 0 and args:.\1 = 0 then \!Zero_To_The_Zero
                else guard args:.\0 / 1 ** args:.\1),
        )),

The function C<integral_power::Integer> implements the C<Numerical> virtual
function C<integral_power> aka C<**> for the composing type C<Integer>.

=head2 integral_nn_power (Integer)

        integral_nn_power::Integer : (\Function : (
            returns : {\$Integer, \$Zero_To_The_Zero},
            matches : (\$Integer, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (if args:.\0 = 0 and args:.\1 = 0 then \!Zero_To_The_Zero
                else guard evaluates args --> \foundation::Integer_nn_power()),
        )),

The function C<integral_nn_power::Integer> implements the C<Numerical>
virtual function C<integral_nn_power> aka C<power> for the composing type
C<Integer>.

=head2 to_Integer (Integer)

        to_Integer::Integer : (\Function : (
            returns : \$Integer,
            matches : (\$Integer,),
            implements : \$folder::'',
            evaluates : (args:.\0),
        )),

The function C<to_Integer::Integer> simply results in its C<0> argument.
This function implements the C<Integral> virtual function C<to_Integer> for
the composing type C<Integer>.

=head2 factorial (Integer)

        factorial::Integer : (\Function : (
            returns : \$Integer_P,
            matches : (\$Integer_NN,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Integer_factorial()),
        )),

The function C<factorial::Integer> implements the C<Integral> virtual
function C<factorial> for the composing type C<Integer>.

=head1 FRACTIONAL DATA TYPES

=head2 Fractional

        Fractional::'' : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Orderable, \$Numerical},
        )),

The interface type definer C<Fractional> is semifinite.  A C<Fractional> value
either is a rational exact numeric of some kind, expressible as a coprime
I<numerator> / I<denominator> pair of C<Integral> whose I<denominator> is
positive, or is something that can act as such.  Idiomatically a
C<Fractional> is a pure rational number which does not represent any kind
of thing in particular, neither cardinal nor ordinal nor nominal; however
some types which do represent such a particular kind of thing may choose to
compose C<Fractional> because it makes sense to provide its operators.  The
default value of C<Fractional> is the C<Fraction> value C<0.0>.
C<Fractional> is C<Orderable>; for each type composing C<Fractional>, a
value closer to negative infinity is ordered before a value closer to
positive infinity.  In the general case it is not C<Bicessable> nor does it
have a minimum or maximum value, but sometimes a type that is C<Fractional>
will have either of those.  C<Fractional> is composed by C<Fraction>.

=head2 Fractional_NN

        Fractional_NN : (\Function : (
            is_type_definer : True,
            evaluates : (Fractional args:.\0 and_then guard
                args:.\0 >= zero::(args:.\0)),
        )),

The selection type definer C<Fractional_NN> represents the infinite type
consisting just of the C<Fractional> values that are non-negative.  Its
default and minmum value is C<0.0>; it has no maximum value.

=head2 to_Fraction

        to_Fraction::'' : (\Function : (
            virtual : True,
            returns : \$Fraction,
            matches : (\$Fractional,),
        )),

The virtual function C<to_Fraction> results in the C<Fraction> value that
represents the same rational value as its C<0> argument.  The purpose of
C<to_Fraction> is to canonicalize C<Fractional> values so they can be
compared abstractly as rationals, or so that it is easier to do exact math
with rationals without running afoul of possible range limits of fixed-size
C<Fractional> types, just dealing with the latter for storage.

=head2 numerator

        numerator::'' : (\Function : (
            virtual : True,
            returns : \$Integral,
            matches : (\$Fractional,),
        )),

The virtual function C<numerator> results in the I<numerator> of its
C<0> argument, when the latter is expressed as a coprime I<numerator> /
I<denominator> pair of C<Integral> whose I<denominator> is positive.

=head2 denominator

        denominator::'' : (\Function : (
            virtual : True,
            returns : \$Integral_P,
            matches : (\$Fractional,),
        )),

The virtual function C<denominator> results in the I<denominator> of its
C<0> argument, when the latter is expressed as a coprime I<numerator> /
I<denominator> pair of C<Integral> whose I<denominator> is positive.

=head1 FRACTION DATA TYPES

=head2 Fraction

        Fraction::'' : (\Function : (
            is_type_definer : True,
            composes : {\$Fractional},
            provides_default_for : {\$Fractional},
            evaluates : (\$Signature::Article_Match : (
                label : \Fraction,
                attrs : [
                    (
                        numerator : \Integer::(),
                        denominator : \Integer_P::(),
                    ),
                    \(args:.\0:.\numerator coprime args:.\0:.\denominator),
                ],
            )),
            default : 0.0,
        )),

The selection type definer C<Fraction> is infinite.  A
C<Fraction> value is a general purpose exact rational number of any
magnitude and precision, expressible as a coprime
I<numerator> / I<denominator> pair of C<Integer> whose I<denominator> is
positive, which explicitly does not represent any kind of thing in
particular, neither cardinal nor ordinal nor nominal.  Its default value is
C<0.0>.  C<Fraction> is C<Orderable>; it has no minimum or maximum value.
Other programming languages may name their corresponding types I<BigRat>
or I<Rational>.

=head2 Fraction_NN

        Fraction_NN : (\Function : (
            is_type_definer : True,
            evaluates : [\Fraction::(), \'>='::( 1: 0.0 )],
        )),

The selection type definer C<Fraction_NN> represents the infinite type
consisting just of the C<Fraction> values that are non-negative.  Its
default and minmum value is C<0.0>; it has no maximum value.

=head2 in_order (Fraction)

        in_order::Fraction : (\Function : (
            returns : \$Boolean,
            matches : (\$Fraction, \$Fraction),
            implements : \$folder::'',
            evaluates : (
                if (denominator args:.\0) = (denominator args:.\1)
                    then in_order::(numerator args:.\0, numerator args:.\1)
                else
                  (
                    common_d ::= lcm::(denominator args:.\0, denominator args:.\1);
                    returns in_order::(
                        (numerator args:.\0) * div::(common_d, denominator args:.\0, RM::(\To_Zero)),
                        (numerator args:.\1) * div::(common_d, denominator args:.\1, RM::(\To_Zero)),
                    );
                  )
            ),
        )),

The function C<in_order::Fraction> implements the C<Orderable> virtual
function C<in_order> for the composing type C<Fraction>.

=head2 to_Boolean (Fraction)

        to_Boolean::Fraction : (\Function : (
            returns : \$Boolean,
            matches : (\$Fraction,),
            implements : \$folder::'',
            evaluates : (args:.\0 != 0.0),
        )),

The function C<to_Boolean::Fraction> results in C<True> iff its C<0>
argument is not C<0.0>, and in C<False> if it is C<0.0>.  This function
implements the C<Boolable> virtual function C<to_Boolean> aka C<so> aka
C<?> for the composing type C<Fraction>.

=head2 zero (Fraction)

        zero::Fraction : (\Function : (
            returns : \$Fraction,
            matches : (\$Fraction,),
            implements : \$folder::'',
            evaluates : (0.0),
        )),

The function C<zero::Fraction> simply results in C<0.0>.  This function
implements the C<Numerical> virtual function C<zero> for the composing type
C<Fraction>.

=head2 opposite (Fraction)

        opposite::Fraction : (\Function : (
            returns : \$Fraction,
            matches : (\$Fraction,),
            implements : \$folder::'',
            evaluates : (-(numerator args:.\0) / (denominator args:.\0)),
        )),

The function C<opposite::Fraction> implements the C<Numerical> virtual
function C<opposite> aka C<additive_inverse> aka unary C<-> aka unary C<−>
for the composing type C<Fraction>.

=head2 reciprocal (Fraction)

        reciprocal::Fraction : (\Function : (
            returns : {\$Fraction, \$Div_By_Zero},
            matches : (\$Fraction,),
            implements : \$folder::'',
            evaluates : (if args:.\0 = 0.0 then \!Div_By_Zero
                else guard (denominator args:.\0) / (numerator args:.\0)),
        )),

The function C<reciprocal::Fraction> implements the C<Numerical> virtual
function C<reciprocal> aka C<multiplicative_inverse> for the composing type
C<Fraction>.

=head2 modulus (Fraction)

        modulus::Fraction : (\Function : (
            returns : \$Fraction_NN,
            matches : (\$Fraction,),
            implements : \$folder::'',
            evaluates : (abs::(numerator args:.\0) / (denominator args:.\0)),
        )),

The function C<modulus::Fraction> implements the C<Numerical>
virtual function C<modulus> aka C<abs> for the composing type C<Fraction>.

=head2 plus (Fraction)

        plus::Fraction : (\Function : (
            returns : \$Fraction,
            matches : (\$Fraction, \$Fraction),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            identity : 0.0,
            repeater : \$times::Fraction_Integer,
            evaluates : (
                if (denominator args:.\0) = (denominator args:.\1)
                    then (numerator args:.\0) + (numerator args:.\1) / (denominator args:.\0)
                else
                  (
                    common_d ::= lcm::(denominator args:.\0, denominator args:.\1);
                    returns ((numerator args:.\0) * div::(common_d, denominator args:.\0, RM::(\To_Zero)))
                        + ((numerator args:.\1) * div::(common_d, denominator args:.\1, RM::(\To_Zero)))
                        / common_d;
                  )
            ),
        )),

The function C<plus::Fraction> implements the C<Numerical>
virtual function C<plus> aka C<+> for the composing type C<Fraction>.

=head2 minus (Fraction)

        minus::Fraction : (\Function : (
            returns : \$Fraction,
            matches : (\$Fraction, \$Fraction),
            implements : \$folder::'',
            right_identity : 0.0,
            evaluates : (args:.\0 + -args:.\1),
        )),

The function C<minus::Fraction> implements the C<Numerical> virtual function
C<minus> aka binary C<-> aka binary C<−> for the composing type C<Fraction>.

=head2 times (Fraction)

        times::Fraction : (\Function : (
            returns : \$Fraction,
            matches : (\$Fraction, \$Fraction),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            identity : 1.0,
            repeater : \$integral_nn_power::Fraction,
            evaluates : (((numerator args:.\0) * (numerator args:.\1))
                / ((denominator args:.\0) * (denominator args:.\1))),
        )),

The function C<times::Fraction> implements the C<Numerical> virtual function
C<times> aka C<*> aka C<×> for the composing type C<Fraction>.

=head2 times (Fraction, Integer)

        times::Fraction_Integer : (\Function : (
            returns : \$Fraction,
            matches : (\$Fraction, \$Integer),
            implements : \$folder::'',
            evaluates : (((numerator args:.\0) * args:.\1) / (denominator args:.\0)),
        )),

The function C<times::Fraction_Integer> implements the C<Numerical> virtual
function C<times> aka C<*> aka C<×> for the composing type C<Fraction>,
specifically for multiplying one by an C<Integer>.

=head2 multiple_of (Fraction)

        multiple_of::Fraction : (\Function : (
            returns : {\$Boolean, \$Div_By_Zero},
            matches : (\$Fraction, \$Fraction),
            implements : \$folder::'',
            evaluates : (if args:.\1 = 0.0 then \!Div_By_Zero
                else guard (args:.\0 mod args:.\1) = 0.0),
        )),

The function C<multiple_of::Fraction> implements the C<Numerical>
virtual function C<multiple_of> for the composing type C<Fraction>.

=head2 fractional_divided_by (Fraction)

        fractional_divided_by::Fraction : (\Function : (
            returns : {\$Fraction, \$Div_By_Zero},
            matches : (\$Fraction, \$Fraction),
            implements : \$folder::'',
            right_identity : 1.0,
            evaluates : (if args:.\1 = 0.0 then \!Div_By_Zero
                else guard args:.\0 * reciprocal::(args:.\1)),
        )),

The function C<fractional_divided_by::Fraction> implements the C<Numerical>
virtual function C<fractional_divided_by> aka C</> aka C<÷> aka C<∕> for
the composing type C<Fraction>.

=head2 integral_divided_by (Fraction)

        integral_divided_by::Fraction : (\Function : (
            returns : {\$Fraction, \$Div_By_Zero},
            matches : (\$Fraction, \$Fraction, \$Round_Meth),
            implements : \$folder::'',
            right_identity : 1.0,
            evaluates : (
                d ::= lcm::(denominator args:.\0, denominator args:.\1);
                n0 ::= (numerator args:.\0) * div::(d, denominator args:.\0, RM::(\To_Zero));
                n1 ::= (numerator args:.\1) * div::(d, denominator args:.\1, RM::(\To_Zero));
                returns if args:.\1 = 0.0 then \!Div_By_Zero
                    else guard div::(n0 * d, n1 * d, args:.\2) / 1;
            ),
        )),

The function C<integral_divided_by::Fraction> implements the C<Numerical>
virtual function C<integral_divided_by> aka C<div> for the composing type
C<Fraction>.

=head2 integral_power (Fraction)

        integral_power::Fraction : (\Function : (
            returns : {\$Fraction, \$Zero_To_The_Zero},
            matches : (\$Fraction, \$Integer),
            implements : \$folder::'',
            evaluates : (evaluates \integral_nn_power::()
                <-- (if args:.\1 >= 0 then args else (reciprocal::(args:.\0), -args:.\1))),
        )),

The function C<integral_power::Fraction> implements the C<Numerical> virtual
function C<integral_power> aka C<**> for the composing type C<Fraction>.

=head2 integral_nn_power (Fraction)

        integral_nn_power::Fraction : (\Function : (
            returns : {\$Fraction, \$Zero_To_The_Zero},
            matches : (\$Fraction, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (if args:.\0 = 0.0 and args:.\1 = 0 then \!Zero_To_The_Zero
                else guard ((numerator args:.\0) ** args:.\1) / ((denominator args:.\0) ** args:.\1)),
        )),

The function C<integral_nn_power::Fraction> implements the C<Numerical>
virtual function C<integral_nn_power> aka C<power> for the composing type
C<Fraction>.

=head2 to_Fraction (Fraction)

        to_Fraction::Fraction : (\Function : (
            returns : \$Fraction,
            matches : (\$Fraction,),
            implements : \$folder::'',
            evaluates : (args:.\0),
        )),

The function C<to_Fraction::Fraction> simply results in its C<0> argument.
This function implements the C<Fractional> virtual function C<to_Fraction>
for the composing type C<Fraction>.

=head2 numerator (Fraction)

        numerator::Fraction : (\Function : (
            returns : \$Integer,
            matches : (\$Fraction,),
            evaluates : (args:.\0:>.\numerator),
        )),

The function C<numerator::Fraction> implements the C<Fractional> virtual
function C<numerator> for the composing type C<Fraction>.

=head2 denominator (Fraction)

        denominator::Fraction : (\Function : (
            returns : \$Integer_P,
            matches : (\$Fraction,),
            evaluates : (args:.\0:>.\denominator),
        )),

The function C<denominator::Fraction> implements the C<Fractional> virtual
function C<denominator> for the composing type C<Fraction>.

=head1 EMPTYABLE DATA TYPES

=head2 Emptyable

        Emptyable : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Boolable},
        )),

The interface type definer C<Emptyable> is semifinite.  An C<Emptyable> value is
an aggregate C<Boolable> value that can have either zero or more than zero
components.  The primary reason for C<Emptyable> is to provide easy
consistent and terse ways to ask if an aggregate has any values, or to ask
for the value with no members of the same type as a given aggregate value.
The default value of C<Emptyable> is the C<Bits> value with zero members.

C<Emptyable> is composed, directly or indirectly, by: C<Stringy>, C<Bits>,
C<Blob>, C<Textual>, C<Text>, C<Homogeneous>, C<Unionable>, C<Discrete>,
C<Positional>, C<Array>, C<Set>, C<Bag>, C<Relational>,
C<Tuple_Array>, C<Relation>, C<Tuple_Bag>, C<Intervalish>, C<Interval>,
C<Unionable_Intervalish>, C<Interval_Set>, C<Interval_Bag>.

=head2 to_Boolean (Emptyable) has_any_members

        to_Boolean::Emptyable : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Emptyable,),
            implements : \$folder::'',
        )),

        has_any_members : (\Alias : ( of : \$to_Boolean::Emptyable, )),

The virtual function C<to_Boolean::Emptyable> aka C<has_any_members>
results in C<True> iff its C<0> argument has any members, and in C<False>
iff it has no members.  This function implements the C<Boolable> virtual
function C<to_Boolean> aka C<so> aka C<?> for the composing type
C<Emptyable>.

=head2 is_empty ∅?

        is_empty : (\Function : (
            negates : \$to_Boolean::Emptyable,
        )),

        Unicode_Aliases::'∅?' : (\Alias : ( of : \$is_empty, )),

The function C<is_empty> aka C<∅?> results in C<True> iff its C<0> argument
has no members, and in C<False> iff it has any members.
Other programming languages may name their corresponding operators I<empty?>.

=head2 empty ∅

        empty::'' : (\Function : (
            virtual : True,
            returns : {\$Emptyable, \$No_Empty_Value},
            matches : (\$Emptyable,),
        )),

        Unicode_Aliases::'∅' : (\Alias : ( of : \$empty, )),

The virtual function C<empty> aka C<∅> results in the value of its C<0>
argument's collection type that has zero members.  For many types like
C<Text> or C<Set>, this is a constant value, but for types like C<Relation>
or C<Tuple_Bag>, there is a distinct result for each distinct I<heading>.
Other programming languages may name their corresponding operators I<clear>.

=head1 STRINGY DATA TYPES

=head2 Stringy

        Stringy : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Orderable, \$Emptyable},
            provides_default_for : {\$Emptyable},
        )),

The interface type definer C<Stringy> is semifinite.  A C<Stringy> value is a
homogeneous ordered aggregate of other values such that there may not
necessarily be any single best interpretation of where each component value
begins or ends, and as such the only generic interpretation of a C<Stringy>
value is that it is a sequence of smaller C<Stringy> values of the same
type.  Idiomatically a C<Stringy> type has opaque values that each
represent something specific, such as a sequence of bits or of octets or of
characters of some repertoire; that being said, by way of C<Positional>,
some C<Stringy> types are in fact generic collections whose elements don't
represent something specific.  The general case of a C<Stringy> type has
operators for catenating or splitting C<Stringy> values, but it has no
generic concept of counting or addressing individual members of the
aggregate, with the lone special case exception that a count of zero can be
distinguished from a count of more than zero.  Addressing or counting
individual members can only be done properly in a composing type-specific
way, and often a single composing type may provide multiple ways, such as
character string types offering both code point and grapheme
representations.  The default value of C<Stringy> is the C<Bits> value with
zero members.  C<Stringy> expects every one of its composing types to be
orderable (but that some C<Positional> are only conditionally so), and
idiomatically that is done by some kind of pairwise comparison of members.

C<Stringy> is composed, directly or indirectly, by: C<Bits>, C<Blob>,
C<Textual>, C<Text>, C<Positional>, C<Array>, C<Tuple_Array>.

=head2 substring_of

        substring_of::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Stringy, \$Stringy),
        )),

The virtual function C<substring_of> results in C<True> iff the sequence of
members of its C<0> argument is a substring of the sequence of members of
its C<1> argument; otherwise it results in C<False>.  Other programming
languages may name their corresponding operators I<in>.

=head2 superstring_of

        superstring_of : (\Function : (
            commutes : \$substring_of,
        )),

The function C<superstring_of> results in C<True> iff the sequence of
members of its C<0> argument is a superstring of the sequence of members of
its C<1> argument; otherwise it results in C<False>.  Other programming
languages may name their corresponding operators I<contains> or
I<include?>; some of them instead provide more generalized pattern
searching operators such as I<like> or I<~~> or I<=~>; some of them also
provide operators that result in an ordinal position or nonmatch indicator
rather than a boolean.

=head2 proper_substring_or_superstring

        proper_substring_or_superstring : (\Function : (
            returns : \$Boolean,
            matches : (\$Stringy, \$Stringy),
            is_commutative : True,
            evaluates : (args:.\0 != args:.\1 and (args:.\0 substring_or_superstring args:.\1)),
        )),

The function C<proper_substring_or_superstring> results in C<True> iff the
sequence of members of one of its 2 arguments C<0> and C<1> is a proper
substring of the sequence of members of its other argument; otherwise it
results in C<False>.

=head2 substring_or_superstring

        substring_or_superstring : (\Function : (
            returns : \$Boolean,
            matches : (\$Stringy, \$Stringy),
            is_commutative : True,
            evaluates : ((args:.\0 substring_of args:.\1) or (args:.\0 superstring_of args:.\1)),
        )),

The function C<substring_or_superstring> results in C<True> iff the
sequence of members of one of its 2 arguments C<0> and C<1> is a substring
of the sequence of members of its other argument; otherwise it results in
C<False>.

=head2 overlaps_string

        overlaps_string::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Stringy, \$Stringy),
            is_commutative : True,
        )),

The virtual function C<overlaps_string> results in C<True> iff, given I<X>
as the sequence of members of its argument C<0> and I<Y> as the sequence of
members of its argument C<1>, when I<X> and I<Y> are overlapped to the
greatest possible extent such that every corresponding member pair has 2 of
the same value, the overlap of I<X> and I<Y> has at least 1 member, and
each of I<X> and I<Y> has at least 1 member that is not overlapped;
otherwise it results in C<False>.

=head2 disjoint_string

        disjoint_string::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Stringy, \$Stringy),
            is_commutative : True,
        )),

The virtual function C<disjoint_string> results in C<True> iff the sequence
of members of its C<0> argument can not be overlapped with the sequence of
members of its C<1> argument by at least 1 member such that every
corresponding member pair has 2 of the same value; otherwise it results in
C<False>.

=head2 catenate ~

        catenate::'' : (\Function : (
            virtual : True,
            returns : \$Stringy,
            matches : (\$Stringy, \$Stringy),
            is_associative : True,
            repeater : \$replicate,
        )),

        '~' : (\Alias : ( of : \$catenate, )),

The virtual function C<catenate> aka C<~> results in the catenation of its
2 arguments C<0> and C<1> such that the result starts with the members of
C<0> and ends with the members of C<1>, the members from both in the same
order as in their respective arguments.  This operation has a I<two-sided identity element>
value of a collection with zero members.  Other programming languages may
name their corresponding operators I<concat> or I<||> or I<+> or I<.> or
I<strcat> or I<join>; some of them also have string interpolation syntax
which logically does the same thing without an explicit operator.

=head2 replicate ~#

        replicate::'' : (\Function : (
            virtual : True,
            returns : \$Stringy,
            matches : (\$Stringy, \$Integer_NN),
        )),

        '~#' : (\Alias : ( of : \$replicate, )),

The virtual function C<replicate> aka C<~#> results in the catenation of N
instances of its C<0> argument where N is defined by its C<1> argument.  If
the C<1> argument is zero then the result is the value of the C<0>
argument's collection type that has zero members.  Other programming
languages may name their corresponding operators I<x>.

=head1 BITS DATA TYPES

=head2 Bits

        Bits : (\Function : (
            is_type_definer : True,
            composes : {\$Stringy},
            provides_default_for : {\$Stringy},
            evaluates : (\$Signature::Article_Match : (
                label : \Bits,
                attrs : (
                    bits : \Array::Bits(),
                ),
            )),
            default : \~?"",
        )),

The selection type definer C<Bits> is infinite.  A C<Bits> value is an
arbitrarily-long sequence of I<bits> where each bit is represented by
an C<Integer> in the range 0..1.  The default value of C<Bits> is
C<\~?""> (the empty bit string).  C<Bits> is C<Orderable>; its minimum
value is the same C<\~?""> as its default value; it has no maximum value;
its ordering algorithm corresponds directly to that of C<Array>, pairwise
as integer sequences.  Other programming languages may name their
corresponding types I<bit> or I<bit varying>.

=head2 Array::Bits

        Array::Bits : (\Function : (
            is_type_definer : True,
            evaluates : [\Array::(), \all::( 1: \in::( 1: 0..1 ) )],
        )),

The selection type definer C<Array::Bits> represents the infinite type
consisting just of the C<Array> values for which every one of their member
values is an integer in the range 0..1 inclusive.

=head2 in_order (Bits)

        in_order::Bits : (\Function : (
            returns : \$Boolean,
            matches : (\$Bits, \$Bits),
            implements : \$folder::'',
            evaluates : ((Bits_to_Array_Bits args:.\0) in_order (Bits_to_Array_Bits args:.\1)),
        )),

The function C<in_order::Bits> implements the C<Orderable> virtual
function C<in_order> for the composing type C<Bits>.

=head2 to_Boolean (Bits)

        to_Boolean::Bits : (\Function : (
            returns : \$Boolean,
            matches : (\$Bits,),
            implements : \$folder::'',
            evaluates : (args:.\0 != \~?""),
        )),

The function C<to_Boolean::Bits> results in C<True> iff its C<0> argument
is not C<\~?"">, and in C<False> if it is C<\~?"">.  This function
implements the C<Boolable> virtual function C<to_Boolean> aka C<so> aka
C<?> for the composing type C<Bits>.

=head2 empty (Bits)

        empty::Bits : (\Function : (
            returns : \$Bits,
            matches : (\$Bits,),
            implements : \$folder::'',
            evaluates : (\~?""),
        )),

The function C<empty::Bits> simply results in C<\~?"">.  This function
implements the C<Emptyable> virtual function C<empty> for the composing
type C<Bits>.

=head2 substring_of (Bits)

        substring_of::Bits : (\Function : (
            returns : \$Boolean,
            matches : (\$Bits, \$Bits),
            implements : \$folder::'',
            evaluates : ((Bits_to_Array_Bits args:.\0) substring_of (Bits_to_Array_Bits args:.\1)),
        )),

The function C<substring_of::Bits> implements the C<Stringy> virtual
function C<substring_of> for the composing type C<Bits>.

=head2 overlaps_string (Bits)

        overlaps_string::Bits : (\Function : (
            returns : \$Boolean,
            matches : (\$Bits, \$Bits),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : ((Bits_to_Array_Bits args:.\0)
                overlaps_string (Bits_to_Array_Bits args:.\1)),
        )),

The function C<overlaps_string::Bits> implements the C<Stringy> virtual
function C<overlaps_string> for the composing type C<Bits>.

=head2 disjoint_string (Bits)

        disjoint_string::Bits : (\Function : (
            returns : \$Boolean,
            matches : (\$Bits, \$Bits),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : ((Bits_to_Array_Bits args:.\0)
                disjoint_string (Bits_to_Array_Bits args:.\1)),
        )),

The function C<disjoint_string::Bits> implements the C<Stringy> virtual
function C<disjoint_string> for the composing type C<Bits>.

=head2 catenate (Bits)

        catenate::Bits : (\Function : (
            returns : \$Bits,
            matches : (\$Bits, \$Bits),
            implements : \$folder::'',
            is_associative : True,
            identity : \~?"",
            repeater : \$replicate::Bits,
            evaluates : (Bits_from_Array_Bits::((Bits_to_Array_Bits args:.\0)
                ~ (Bits_to_Array_Bits args:.\1))),
        )),

The function C<catenate::Bits> implements the C<Stringy> virtual function
C<catenate> aka C<~> for the composing type C<Bits>.

=head2 replicate (Bits)

        replicate::Bits : (\Function : (
            returns : \$Bits,
            matches : (\$Bits, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (Bits_from_Array_Bits::((Bits_to_Array_Bits args:.\0) ~# args:.\1)),
        )),

The function C<replicate::Bits> implements the C<Stringy> virtual function
C<replicate> aka C<~#> for the composing type C<Bits>.

=head2 Bits_from_Array_Bits

        Bits_from_Array_Bits : (\Function : (
            returns : \$Bits,
            matches : (\$Array::Bits,),
            evaluates : ((\Bits : (bits : args:.\0,))),
        )),

The function C<Bits_from_Array_Bits> results in the C<Bits> value selected
in terms of the integer sequence of its C<0> argument.

=head2 Bits_to_Array_Bits

        Bits_to_Array_Bits : (\Function : (
            returns : \$Array::Bits,
            matches : (\$Bits,),
            evaluates : (args:.\0:>.\bits),
        )),

The function C<Bits_to_Array_Bits> results in an integer sequence defining
the bits of its C<Bits>-typed C<0> argument.

=head1 BLOB DATA TYPES

=head2 Blob

        Blob : (\Function : (
            is_type_definer : True,
            composes : {\$Stringy},
            evaluates : (\$Signature::Article_Match : (
                label : \Blob,
                attrs : (
                    octets : \Array::Octets(),
                ),
            )),
            default : \~+"",
        )),

The selection type definer C<Blob> is infinite.  A C<Blob> value is an
arbitrarily-long sequence of I<octets> where each octet is represented by
an C<Integer> in the range 0..255.  The default value of C<Blob> is
C<\~+""> (the empty octet string).  C<Blob> is C<Orderable>; its minimum
value is the same C<\~+""> as its default value; it has no maximum value;
its ordering algorithm corresponds directly to that of C<Array>, pairwise
as integer sequences.  Other programming languages may name their
corresponding types I<Buf> or I<byte[]> or I<bytea>.

=head2 Array::Octets

        Array::Octets : (\Function : (
            is_type_definer : True,
            evaluates : [\Array::(), \all::( 1: \in::( 1: 0..255 ) )],
        )),

The selection type definer C<Array::Octets> represents the infinite type
consisting just of the C<Array> values for which every one of their member
values is an integer in the range 0..255 inclusive.

=head2 in_order (Blob)

        in_order::Blob : (\Function : (
            returns : \$Boolean,
            matches : (\$Blob, \$Blob),
            implements : \$folder::'',
            evaluates : ((Blob_to_Octets args:.\0) in_order (Blob_to_Octets args:.\1)),
        )),

The function C<in_order::Blob> implements the C<Orderable> virtual
function C<in_order> for the composing type C<Blob>.

=head2 to_Boolean (Blob)

        to_Boolean::Blob : (\Function : (
            returns : \$Boolean,
            matches : (\$Blob,),
            implements : \$folder::'',
            evaluates : (args:.\0 != \~+""),
        )),

The function C<to_Boolean::Blob> results in C<True> iff its C<0> argument
is not C<\~+"">, and in C<False> if it is C<\~+"">.  This function
implements the C<Boolable> virtual function C<to_Boolean> aka C<so> aka
C<?> for the composing type C<Blob>.

=head2 empty (Blob)

        empty::Blob : (\Function : (
            returns : \$Blob,
            matches : (\$Blob,),
            implements : \$folder::'',
            evaluates : (\~+""),
        )),

The function C<empty::Blob> simply results in C<\~+"">.  This function
implements the C<Emptyable> virtual function C<empty> for the composing
type C<Blob>.

=head2 substring_of (Blob)

        substring_of::Blob : (\Function : (
            returns : \$Boolean,
            matches : (\$Blob, \$Blob),
            implements : \$folder::'',
            evaluates : ((Blob_to_Octets args:.\0) substring_of (Blob_to_Octets args:.\1)),
        )),

The function C<substring_of::Blob> implements the C<Stringy> virtual
function C<substring_of> for the composing type C<Blob>.

=head2 overlaps_string (Blob)

        overlaps_string::Blob : (\Function : (
            returns : \$Boolean,
            matches : (\$Blob, \$Blob),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : ((Blob_to_Octets args:.\0) overlaps_string (Blob_to_Octets args:.\1)),
        )),

The function C<overlaps_string::Blob> implements the C<Stringy> virtual
function C<overlaps_string> for the composing type C<Blob>.

=head2 disjoint_string (Blob)

        disjoint_string::Blob : (\Function : (
            returns : \$Boolean,
            matches : (\$Blob, \$Blob),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : ((Blob_to_Octets args:.\0) disjoint_string (Blob_to_Octets args:.\1)),
        )),

The function C<disjoint_string::Blob> implements the C<Stringy> virtual
function C<disjoint_string> for the composing type C<Blob>.

=head2 catenate (Blob)

        catenate::Blob : (\Function : (
            returns : \$Blob,
            matches : (\$Blob, \$Blob),
            implements : \$folder::'',
            is_associative : True,
            identity : \~+"",
            repeater : \$replicate::Blob,
            evaluates : (Blob_from_Octets::((Blob_to_Octets args:.\0) ~ (Blob_to_Octets args:.\1))),
        )),

The function C<catenate::Blob> implements the C<Stringy> virtual function
C<catenate> aka C<~> for the composing type C<Blob>.

=head2 replicate (Blob)

        replicate::Blob : (\Function : (
            returns : \$Blob,
            matches : (\$Blob, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (Blob_from_Octets::((Blob_to_Octets args:.\0) ~# args:.\1)),
        )),

The function C<replicate::Blob> implements the C<Stringy> virtual function
C<replicate> aka C<~#> for the composing type C<Blob>.

=head2 Blob_from_Octets

        Blob_from_Octets : (\Function : (
            returns : \$Blob,
            matches : (\$Array::Octets,),
            evaluates : ((\Blob : (octets : args:.\0,))),
        )),

The function C<Blob_from_Octets> results in the C<Blob> value selected in
terms of the integer sequence of its C<0> argument.

=head2 Blob_to_Octets

        Blob_to_Octets : (\Function : (
            returns : \$Array::Octets,
            matches : (\$Blob,),
            evaluates : (args:.\0:>.\octets),
        )),

The function C<Blob_to_Octets> results in an integer sequence defining the
octets of its C<Blob>-typed C<0> argument.

=head1 TEXTUAL DATA TYPES

=head2 Textual

        Textual : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Stringy},
        )),

The interface type definer C<Textual> is semifinite.  A C<Textual> value is a
C<Stringy> value which is explicitly a sequence of characters of some
repertoire, typically Unicode or a subset thereof such as ASCII.  Note that
the C<System> package has no concept of a I<single character> value in the
abstract sense that some programming languages do; the closest analogy is a
C<Stringy> value that just contains either one code point or grapheme or
similar concept; this is something in common with the Raku language.

C<Textual> is composed by C<Text>, which implements C<Orderable> using the
simple culture-agnostic method of ordering code points numerically.
Idiomatically each culture-specific text collation method will have its own
distinct C<Textual>-composing type that implements C<Orderable> in its own
way, so the latter's operators will just work like users expect.

The C<System> package excludes the majority of useful operators specific to
working with character strings; see instead other Muldis D packages such as
L<System::Text|Muldis::D::Package::System::Text> for these things.  Such tasks
include like case folding, pattern matching, whitespace trimming, Unicode
normalization, encoding to and decoding from most binary formats, and so on.

Muldis D is designed expressly to avoid mandatory external dependencies of
large complexity, such as most of the details of Unicode, in contrast with
a lot of the more modern languages of its time.  The Muldis D Foundation
and C<System> package are strictly limited in their knowledge of Unicode;
they know that a code point of the Unicode repertoire is just in the integer
set C<{0..0xD7FF,0xE000..0x10FFFF}>,
and that the leading subset C<0..127> is also 7-bit
ASCII, and they know how to read and write the fairly simple and stable
C<UTF-8> binary encoding for Unicode text, which is a proper superset of
7-bit ASCII encoding and is CPU endian-agnostic.  In contrast, anything to
do with knowing what abstract characters exist, and their various
properties (upper or lowercase, combining or not, etc), anything to do with
normalization or folding or pattern matching, and anything to do with other
binary encodings or character repertoires especially endian-specific, this
is all expressly I<not> part of the language core.  A
Muldis D implementation can choose whether or not to support them, allowing
for a lower barrier to entry.  Unicode in particular requires a vast
knowledge base to work properly with that is regularly updated, and Muldis
D has a principle that it is better to have multiple specialized components
that do their jobs well, such as handle Unicode intricacies, while the core
language can focus on other core competencies that don't involve complex
externally-defined moving targets.  The C<System> package loosely just
considers a character string to be a sequence of generic integers and
doesn't ascribe very many distinct semantics to particular ones, while
non-C<System> code is empowered to do that instead.

=head2 to_Text

        to_Text::'' : (\Function : (
            virtual : True,
            returns : \$Text,
            matches : (\$Textual,),
        )),

The virtual function C<to_Text> results in the C<Text> value that
represents the same character string value as its C<0> argument.  The
purpose of C<to_Text> is to canonicalize C<Textual> values so they can be
compared or worked with as character strings in a manner agnostic to things
like national collations or fixed-size types.

I<TODO: Add an Excuse for when the source type has non-Unicode characters.>

=head1 TEXT DATA TYPES

=head2 Text Text::Unicode

        Text::'' : (\Function : (
            is_type_definer : True,
            composes : {\$Textual},
            provides_default_for : {\$Textual},
            evaluates : (\$Signature::Article_Match : (
                label : \Text,
                attrs : (
                    unicode_codes : \Array::Unicode_Codes(),
                ),
            )),
            default : "",
        )),

        Text::Unicode : (\Alias : ( of : \$Text, )),

The selection type definer C<Text> is infinite.  A C<Text> value is
characterized by an arbitrarily-long sequence of Unicode 12.1 standard
I<character code points>, where each distinct code point corresponds to a
distinct integer in the set C<{0..0xD7FF,0xE000..0x10FFFF}>.  Each
character is taken from a finite repertoire having 0x10F7FF members, but
C<Text> imposes no limit on the length of each character sequence.  C<Text>
has its own canonical representation in terms of an C<Array> value named
C<Unicode_Codes>.  A C<Text> value is isomorphic to an C<Attr_Name> value.
The default value of C<Text>
is C<""> (the empty character string).  C<Text> is C<Orderable>; its
minimum value is the same C<""> as its default value; it has no maximum
value; its ordering algorithm corresponds directly to that of C<Array>,
pairwise as integer sequences.  Other programming languages may name their
corresponding types I<Str> or I<string> or I<varchar> or I<char>.

There are many defined character sets in the computing world that map
agreed upon sets of symbols to integers.  For those character repertoires
that are a subset of Unicode, such as 7-bit ASCII or ISO Latin 1 or EBCDIC,
the C<Text> type should map with character strings using them in a simple
and well-defined way, although the integers used to represent the same
logical characters may be different.  But for other character sets that are
not a subset of Unicode, such as ISO/IEC 2022 or Mojikyo or HKSCS, a
C<Text> value can not directly represent all possible character strings
that they can, and so other C<Textual>-composing types should be used
instead for such character strings.

=head2 Array::Unicode_Codes

        Array::Unicode_Codes : (\Function : (
            is_type_definer : True,
            evaluates : [\Array::(),
                \all::( 1: \in::( 1: ?..{0..0xD7FF,0xE000..0x10FFFF} ) )],
        )),

The selection type definer C<Array::Unicode_Codes> represents the infinite type
consisting just of the C<Array> values for which every one of their member
values is an integer in the range {0..0xD7FF,0xE000..0x10FFFF} inclusive.

=head2 Text::ASCII

        Text::ASCII : (\Function : (
            is_type_definer : True,
            evaluates : (Text::Unicode::(args:.\0) and_then guard
                Array::ASCII_Chars::(Text_from_Unicode_Codes args:.\0)),
        )),

The selection type definer C<Text::ASCII> represents the infinite type
consisting just of the C<Text> values for which every one of their member
characters is a member of the 128-character repertoire of 7-bit ASCII.
This C<Text> subtype has its own canonical representation in terms of an
C<Array> value named C<ASCII_Chars> where each member code point matches
the standard ASCII codes for the same symbols.

=head2 Array::ASCII_Chars

        Array::ASCII_Chars : (\Function : (
            is_type_definer : True,
            evaluates : [\Array::Unicode_Codes(), \all::( 1: \in::( 1: 0..127 ) )],
        )),

The selection type definer C<Array::ASCII_Chars> represents the infinite type
consisting just of the C<Array> values for which every one of their member
values is an integer in the range 0..127 inclusive.

=head2 in_order (Text)

        in_order::Text : (\Function : (
            returns : \$Boolean,
            matches : (\$Text, \$Text),
            implements : \$folder::'',
            evaluates : ((Text_to_Unicode_Codes args:.\0) in_order (Text_to_Unicode_Codes args:.\1)),
        )),

The function C<in_order::Text> implements the C<Orderable> virtual
function C<in_order> for the composing type C<Text>.

=head2 to_Boolean (Text)

        to_Boolean::Text : (\Function : (
            returns : \$Boolean,
            matches : (\$Text,),
            implements : \$folder::'',
            evaluates : (args:.\0 != ""),
        )),

The function C<to_Boolean::Text> results in C<True> iff its C<0> argument
is not C<"">, and in C<False> if it is C<"">.  This function implements the
C<Boolable> virtual function C<to_Boolean> aka C<so> aka C<?> for the
composing type C<Text>.

=head2 empty (Text)

        empty::Text : (\Function : (
            returns : \$Text,
            matches : (\$Text,),
            implements : \$folder::'',
            evaluates : (""),
        )),

The function C<empty::Text> simply results in C<"">.  This function
implements the C<Emptyable> virtual function C<empty> for the composing
type C<Text>.

=head2 substring_of (Text)

        substring_of::Text : (\Function : (
            returns : \$Boolean,
            matches : (\$Text, \$Text),
            implements : \$folder::'',
            evaluates : ((Text_to_Unicode_Codes args:.\0)
                substring_of (Text_to_Unicode_Codes args:.\1)),
        )),

The function C<substring_of::Text> implements the C<Stringy> virtual
function C<substring_of> for the composing type C<Text>.

=head2 overlaps_string (Text)

        overlaps_string::Text : (\Function : (
            returns : \$Boolean,
            matches : (\$Text, \$Text),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : ((Text_to_Unicode_Codes args:.\0)
                overlaps_string (Text_to_Unicode_Codes args:.\1)),
        )),

The function C<overlaps_string::Text> implements the C<Stringy> virtual
function C<overlaps_string> for the composing type C<Text>.

=head2 disjoint_string (Text)

        disjoint_string::Text : (\Function : (
            returns : \$Boolean,
            matches : (\$Text, \$Text),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : ((Text_to_Unicode_Codes args:.\0)
                disjoint_string (Text_to_Unicode_Codes args:.\1)),
        )),

The function C<disjoint_string::Text> implements the C<Stringy> virtual
function C<disjoint_string> for the composing type C<Text>.

=head2 catenate (Text)

        catenate::Text : (\Function : (
            returns : \$Text,
            matches : (\$Text, \$Text),
            implements : \$folder::'',
            is_associative : True,
            identity : "",
            repeater : \$replicate::Text,
            evaluates : (Text_from_Unicode_Codes::((Text_to_Unicode_Codes args:.\0)
                ~ (Text_to_Unicode_Codes args:.\1))),
        )),

The function C<catenate::Text> implements the C<Stringy> virtual function
C<catenate> aka C<~> for the composing type C<Text>.

=head2 replicate (Text)

        replicate::Text : (\Function : (
            returns : \$Text,
            matches : (\$Text, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (Text_from_Unicode_Codes::((Text_to_Unicode_Codes args:.\0) ~# args:.\1)),
        )),

The function C<replicate::Text> implements the C<Stringy> virtual function
C<replicate> aka C<~#> for the composing type C<Text>.

=head2 to_Text (Text)

        to_Text::Text : (\Function : (
            returns : \$Text,
            matches : (\$Text,),
            implements : \$folder::'',
            evaluates : (args:.\0),
        )),

The function C<to_Text::Text> simply results in its C<0> argument.
This function implements the C<Textual> virtual function C<to_Text>
for the composing type C<Text>.

=head2 Text_from_Unicode_Codes

        Text_from_Unicode_Codes : (\Function : (
            returns : \$Text,
            matches : (\$Array::Unicode_Codes,),
            evaluates : ((\Text : (unicode_codes : args:.\0,))),
        )),

The function C<Text_from_Unicode_Codes> results in the C<Text> value selected
in terms of an integer sequence in the standard Unicode code point
mapping of its C<0> argument.

=head2 Text_to_Unicode_Codes

        Text_to_Unicode_Codes : (\Function : (
            returns : \$Array::Unicode_Codes,
            matches : (\$Text,),
            evaluates : (args:.\0:>.\unicode_codes),
        )),

The function C<Text_to_Unicode_Codes> results in an integer sequence in the
standard Unicode code point mapping that corresponds to its
C<Text>-typed C<0> argument.

=head2 Text_from_ASCII_Chars

        Text_from_ASCII_Chars : (\Function : (
            returns : \$Text::ASCII,
            matches : (\$Array::ASCII_Chars,),
            evaluates : (Text_from_Unicode_Codes args:.\0),
        )),

The function C<Text_from_ASCII_Chars> results in the C<Text> value selected
in terms of an integer sequence in the standard 7-bit ASCII character
mapping of its C<0> argument.

=head2 Text_to_ASCII_Chars

        Text_to_ASCII_Chars : (\Function : (
            returns : \$Array::ASCII_Chars,
            matches : (\$Text::ASCII,),
            evaluates : (Text_to_Unicode_Codes args:.\0),
        )),

The function C<Text_to_ASCII_Chars> results in an integer sequence in the
standard 7-bit ASCII character mapping that corresponds to its
C<Text>-typed C<0> argument.

=head2 Blob_is_UTF_8

        Blob_is_UTF_8 : (\Function : (
            returns : \$Boolean,
            matches : (\$Blob,),
            evaluates : (...),
        )),

I<TODO.  See also https://tools.ietf.org/html/rfc3629 for the UTF-8 definition.>

I<TODO.  Note that while the UTF-8 encoding scheme can represent all Unicode
code points in the range 0..0x1FFFFF with 4 octets (and all 0..0x7FFFFFFF
with 6 octets), the UTF-8 standard further restricts the range to
{0..0xD7FF,0xE000..0x10FFFF} to match the constraints of the limitations of UTF-16.>

I<TODO.  Note that we don't define a Blob::UTF_8 type as
it is superfluous with simply trying to decode one and see if it succeeded.>

=head2 Text_from_UTF_8_Blob

        Text_from_UTF_8_Blob : (\Function : (
            returns : {\$Text::Unicode, \$Unicode::..., ...},
            matches : (\$Blob,),
            evaluates : (...),
        )),

I<TODO.  As a code/implementation comment, say the parallel design is
benefitting from the self-syncrhonizing nature that is a key feature of UTF-8.>

I<TODO.  Note, the multiple Excuse options are used to indicate the
different reasons why the Blob is not considered valid UTF-8, including
that it doesn't use the fewest bytes possible for a character, or it
represents code points greater than 0x10FFFF or it represents illegal
code points in the 0xD800..0xDFFF range of UTF-16 surrogates, or it has the
wrong number of continuation bytes following an ASCII char or starting byte
etc.  If a Blob contains multiple errors, the returned Excuse is for the
error closest to the start of the Blob; that is, chained anticoalesce() is
used. TODO, perhaps declare a union type collecting the Unicode errors like
we have with rounding methods, or we actually may have multiple Unicode sets.>

I<TODO.  See also http://www.cl.cam.ac.uk/~mgk25/ucs/examples/UTF-8-test.txt
and http://cpansearch.perl.org/src/RJBS/perl-5.24.0/t/op/utf8decode.t for
some decoder edge case testing.>

=head2 Text_from_UTF_8_Blob_with_repl_Text

        Text_from_UTF_8_Blob_with_repl_Text : (\Function : (
            returns : \$Text::Unicode,
            matches : (\$Blob, \$Text::Unicode),
            evaluates : (...),
        )),

I<TODO.  Each invalid octet encountered is replaced by the substitution text
(which can be a single character, or several, or the empty string).  For
consistency, even if the sequence decodes fine in one sense but is an out
of range character, the instances of substitution are per count of octets
not one per character.>

=head2 Text_from_UTF_8_Blob_with_repl_char

        Text_from_UTF_8_Blob_with_repl_char : (\Function : (
            returns : \$Text::Unicode,
            matches : (\$Blob,),
            evaluates : (Text_from_UTF_8_Blob_with_repl_Text::(args:.\0,"\\c<0xFFFD>")),
        )),

I<TODO.  The special Unicode char "REPLACEMENT CHARACTER" aka 0xFFFD is used.>

=head2 Text_to_UTF_8_Blob

        Text_to_UTF_8_Blob : (\Function : (
            returns : \$Blob,
            matches : (\$Text::Unicode,),
            evaluates : (...),
        )),

I<TODO.  This should just work as Text::Unicode excludes the surrogate
pairs and out of range etc stuff.>

=head2 Blob_is_ASCII

        Blob_is_ASCII : (\Function : (
            returns : \$Boolean,
            matches : (\$Blob,),
            evaluates : (Array::ASCII_Chars(Blob_to_Octets args:.\0)),
        )),

I<TODO.>

=head2 Text_from_ASCII_Blob

        Text_from_ASCII_Blob : (\Function : (
            returns : {\$Text::ASCII, \$ASCII::High_Bit_Not_Zero},
            matches : (\$Blob,),
            evaluates : (
                octets ::= Blob_to_Octets args:.\0;
                returns if Array::ASCII_Chars(octets)
                    then guard Text_from_ASCII_Chars octets
                    else ASCII::High_Bit_Not_Zero();
            ),
        )),

I<TODO.  Note, still have to define that Excuse.>

=head2 Text_from_ASCII_Blob_with_repl_Text

        Text_from_ASCII_Blob_with_repl_Text : (\Function : (
            returns : \$Text::ASCII,
            matches : (\$Blob, \$Text::ASCII),
            evaluates : (
                src_octets ::= Blob_to_Octets args:.\0;
                repl_chars ::= Text_to_ASCII_Chars args:.\1;
                result_chars ::=
                    given #repl_chars
                        when 0 then
                            src_octets where \in::( 1: 0..127 )
                        when 1 then guard
                            src_octets
                                map \(if args:.\0 in 0..127 then args:.\0 else args:.\1)
                                    <-- (1 : repl_chars.0,)
                        default
                            src_octets
                                map \(if args:.\0 in 0..127 then [args:.\0] else args:.\1)
                                    <-- (1 : repl_chars,)
                                reduce \catenate::()
                    ;
                returns Text_from_ASCII_Chars result_chars;
            ),
        )),

I<TODO.  Each invalid octet encountered is replaced by the substitution text
(which can be a single character, or several, or the empty string).
Note there is no alternate with a predefined substitution char as there
is no good implicit default in ASCII, unlike with Unicode.>

=head2 Text_to_ASCII_Blob

        Text_to_ASCII_Blob : (\Function : (
            returns : \$Blob,
            matches : (\$Text::ASCII,),
            evaluates : (Blob_from_Octets::(Text_to_ASCII_Chars args:.\0)),
        )),

I<TODO.>

=head1 COLLECTIVE DATA TYPES OVERVIEW

A I<collective> value either is a generic regular aggregate of a
multiplicity of other, I<component>, values whose composition is
transparent to the user or is something that can act as such an aggregate.
The C<System> package provides I<collective> types with a variety of
desirable alternative properties.

Some I<collective> values are fully I<discrete> and so their components can
be enumerated as individuals and counted, while other collective values are
at least partially I<continuous> and so at least some of their components
can not be enumerated or counted (the count is effectively an infinity);
however it is still possible to test the membership of a value in such a
collective.  C<Interval> is an example of a typically-continuous type while
most collective types provided by C<System> are discrete; however, an
C<Interval> can be treated as a discrete type iff the types it ranges over
are C<Bicessable>.

Some I<collective> values are I<positional> and define an explicit total
order for their components which does not necessarily depend on any order
specific to the component values themselves, and which does not necessarily
place multiple same-valued components consecutively.  Such a collective
value can reliably and consistently enumerate its components in its defined
order, and the collective also supports integral ordinal-position-based lookup of its
members where each member lives at a distinct ordinal position.  In contrast,
I<nonpositional> collective values simply track what values their
components are and don't provide ordinal-position-based lookup; those that are
enumerable do not guarantee that components are returned in any particular
order.  An example positional type is C<Array> while example nonpositional
types are C<Set>, C<Bag>, C<Relation>, and C<Interval>.  Some I<positional>
types are I<sorted>, ensuring that components are organized in the
collection corresponding to a total order that is specific to the component
values themselves, meaning also that all same-valued components are
consecutive; I<nonsorted> positional types don't do this.

Some I<collective> values are I<setty>, ensuring that each of their
components is unique, while other collective values are I<baggy>,
permitting multiple components to be the same value.  Examples of setty
types are C<Set>, C<Relation>, and C<Interval>, while examples of baggy
types are C<Array> and C<Bag>.  Some setty types may silently avoid
duplicate values, where adding the same value twice has the same effect as
adding it once, while other setty types may raise an error if adding a
duplicate value is attempted.

The I<collective> types provided by the C<System> package all speak to a
space having two orthogonal dimensions into which their components are
logically arranged, where one dimension is called I<homogeneous> and the
other dimension is called I<attributive>.  A I<collective> value's
components addressed in terms of their location along its I<homogeneous>
dimension are called I<members> of the collective, while components
addressed in terms of their location along its I<attributive> dimension are
called I<attributes>.  To be more accurate, a I<member> is a slice of the
space that includes all components with the same single location along the
homogeneous dimension, while an I<attribute> is a slice of the space that
includes all components with the same single location along the attributive
dimension; in many contexts, said slice is considered as a single value of
its own.  Every I<member> of a collective value is conceptually of a common
data type with its fellow members or should be interpreted logically in the
same way.  Every I<attribute> of a collective value is conceptually of its
own data type or should be interpreted logically in a distinct way from its
fellow attributes.

While many collective types just utilize one of the dimensions
I<homogeneous> or I<attributive>, some utilize both.  Values of the
C<Array>, C<Set>, C<Bag>, C<Interval> types each arrange their components
along just the I<homogeneous> dimension and ignore I<attributive>, so for
brevity we just say their components I<are> members.  Values of the
C<Tuple> type each arrange their components along just the I<attributive>
dimension and ignore I<homogeneous>, so for brevity we just say their
components I<are> attributes.  Values of the C<Relation> and C<Tuple_Bag>
types each arrange their components over both of the dimensions, so we say
they have both members and attributes.

Given that the I<homogeneous> and I<attributive> dimensions are to a large
extent isomorphic, and so there are a lot of corresponding operations that
apply to both, the C<System> package uses different terminology and operator
names for corresponding things so it is clear what dimension is being acted
on.  For example, a C<Relation> value has no single concept of its
component count, rather we say I<cardinality> is a count of its members
while I<degree> is a count of its attributes.  When equally terse and
pleasant terminology isn't available for a pair of corresponding concepts,
the homogeneous dimension is given priority for the nicer API since that is
expected to be the dominant one for typical usage patterns.

See the C<Homogeneous> type which defines the common API for all collective
types utilizing the I<homogeneous> dimension, and see the C<Attributive>
type which defines the orthogonal common API for all collective types
utilizing the I<attributive> dimension.  See the C<Relational> type which
defines the common API for all collective types utilizing both dimensions.

See also the C<Accessible> type which provides an extra API that can be
applied on a type-by-type basis to either the I<homogeneous> dimension or
the I<attributive> dimension (but not both) as makes the most sense for the
composing types in question; example composers are C<Array> and C<Tuple>
respectively for said dimensions.

=head1 ACCESSIBLE DATA TYPES

=head2 Accessible

        Accessible : (\Function : (
            is_type_definer : True,
            is_generalization : True,
        )),

The interface type definer C<Accessible> is semifinite.  An C<Accessible> value
is an aggregate of other, I<mapping> values such that each I<mapping> is a
pair of associated other values, a I<key> and I<asset> respectively, such
that no 2 I<key> of an C<Accessible> value are the same value.  The primary
reason for C<Accessible> is to provide an easy consistent and terse
accessor API for individual components in a collection of key-asset pairs,
where the pair exists at a slot addressable by its key.  An C<Accessible>
value is isomorphic to a space in which 0..N slots can exist; possible
operations include testing if a slot exists, fetching the content of a
slot, replacing the content of a slot, inserting another slot, removing an
existing slot.  C<Accessible> does not have anything to say about other
qualities of its composers, such as on matters of enumerating components or
slicing a subset of components into a new collection, and so composers
might have little in common besides sharing certain operator names.

If an C<Accessible> value is also C<Positional>, then each of its
I<key>/I<asset> pairs is actually an I<ordinal-position>/I<member> pair, and all of
its keys must be adjacent C<Integer>; therefore, since each C<Accessible>
operation may only affect 1 slot at a time, it is only valid to insert or
remove a slot at the C<first_unused_ord_pos> or C<last_ord_pos> respectively.
Other composers of C<Accessible> may have their own restrictions on
inserting or removing slots besides key uniqueness, but typically don't.

The default value of C<Accessible> is the C<Tuple> value with zero
attributes, C<()>.  C<Accessible> is composed, directly or indirectly, by:
C<Positional>, C<Array>, C<Tuple_Array>, C<Structural>, C<Tuple>.
I<TODO: Also composed by Dictionary.>

Note that this interface type definer could have as easily been mamed
I<Associative>, but it wasn't so that term could be reserved for the
C<associative> function trait which has a different meaning.

=head2 has_any_at .?

        has_any_at::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Accessible, \$Any),
        )),

        '.?' : (\Alias : ( of : \$has_any_at, )),

The virtual function C<has_any_at> aka C<.?> results in C<True> iff its
C<0> argument has a mapping whose key is equal to its C<1> argument;
otherwise it results in C<False>.  Other programming languages may name
their corresponding operators I<ContainsKey> or I<has_key?> or I<key?> or
I<exists> or I<in> or I<array_key_exists>.

=head2 not_has_any_at .!?

        not_has_any_at : (\Function : (
            negates : \$has_any_at,
        )),

        '.!?' : (\Alias : ( of : \$not_has_any_at, )),

The function C<not_has_any_at> aka C<.!?> results in C<True> iff its C<0>
argument does not have any mapping whose key is equal to its C<1> argument;
otherwise it results in C<False>.

=head2 has_mapping_at .:?

        has_mapping_at::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (Accessible, (Any, Any)),
        )),

        '.:?' : (\Alias : ( of : \$has_mapping_at, )),

The virtual function C<has_mapping_at> aka C<.:?> results in C<True> iff
its C<0> argument has a mapping that is equal to its C<1> argument;
otherwise it results in C<False>.  The C<1> argument is a binary C<Tuple>
whose C<0> and C<1> attributes are the mapping key and asset respectively.

=head2 mapping_at .:

        mapping_at::'' : (\Function : (
            virtual : True,
            returns : (\$Any, \$Any),
            matches : (\$Accessible, \$Any),
            accepts : (args:.\0 .? args:.\1),
        )),

        '.:' : (\Alias : ( of : \$mapping_at, )),

The virtual function C<mapping_at> aka C<.:> results in a binary C<Tuple>
whose C<0> attribute is the function's C<1> argument and whose C<1>
attribute is the asset value of the mapping of its C<0> argument where that
mapping's key is equal to its C<1> argument.  Other programming languages
may name their corresponding operators I<assoc>.

=head2 at .

        at::'' : (\Function : (
            virtual : True,
            returns : \$Any,
            matches : (\$Accessible, \$Any),
            accepts : (args:.\0 .? args:.\1),
        )),

        '.' : (\Alias : ( of : \$at, )),

The virtual function C<at> aka C<.> results in the asset value of the
mapping of its C<0> argument where that mapping's key is equal to its C<1>
argument.  This function will fail if the C<0> argument doesn't have such a
mapping.  Other programming languages may name their corresponding
operators I<ElementAt> or I<fetch>; it is also common to use
subscript/postcircumfix syntax.

=head2 maybe_at .!

        maybe_at::'' : (\Function : (
            virtual : True,
            returns : \$Any,
            matches : (\$Accessible, \$Any),
        )),

        '.!' : (\Alias : ( of : \$maybe_at, )),

The virtual function C<maybe_at> aka C<.!> results in the asset value of
the mapping of its C<0> argument where that mapping's key is equal to its
C<1> argument, iff there is such a mapping; otherwise it results in an
C<Excuse>.  Other programming languages may name their corresponding
operators I<ElementAtOrDefault> or I<at> or I<get> or I<fetch>; it is also
common to use subscript/postcircumfix syntax.

=head2 replace_at .:=

        replace_at::'' : (\Function : (
            virtual : True,
            returns : \$Accessible,
            matches : (Accessible, (Any, Any)),
            accepts : (args:.\0 .? (args:.\1.\0)),
        )),

        '.:=' : (\Alias : ( of : \$replace_at, )),

The virtual function C<replace_at> aka C<.:=> results in the value of its
C<0> argument's collection type that has all of the mappings of the
function's C<0> argument but that, for the 1 mapping of the C<0> argument
whose key I<K> is equal to the function's C<1> argument's C<0> attribute,
the result's mapping instead has an asset equal to the C<1> argument's C<1>
attribute.  This function will fail if the C<0> argument doesn't have a
mapping with the key I<K>.  Other programming languages commonly use
assignment syntax.

=head2 shiftless_insert_at .+

        shiftless_insert_at::'' : (\Function : (
            virtual : True,
            returns : \$Accessible,
            matches : (Accessible, (Any, Any)),
            accepts : (not args:.\0 .? (args:.\1.\0)),
        )),

        '.+' : (\Alias : ( of : \$shiftless_insert_at, )),

The virtual function C<shiftless_insert_at> aka C<.+> results in the value
of its C<0> argument's collection type that has all of the mappings of the
function's C<0> argument plus 1 additional mapping that is equal to its
C<1> argument.  The C<1> argument is a binary C<Tuple> whose C<0> and C<1>
attributes are the mapping key and asset respectively.  This function will
fail if the C<0> argument already has a mapping with that key.  Other
programming languages may name their corresponding operators I<Add>; it is
also common to use assignment syntax.

=head2 shiftless_remove_at .-

        shiftless_remove_at::'' : (\Function : (
            virtual : True,
            returns : \$Accessible,
            matches : (\$Accessible, \$Any),
            accepts : (args:.\0 .? args:.\1),
        )),

        '.-' : (\Alias : ( of : \$shiftless_remove_at, )),

The virtual function C<shiftless_remove_at> aka C<.-> results in the value
of its C<0> argument's collection type that has all of the mappings of the
function's C<0> argument minus 1 existing mapping whose key is equal to its
C<1> argument.  This function will fail if the C<0> argument doesn't have
such a mapping.  Other programming languages may name their corresponding
operators I<del> or I<delete_at>.

=head2 replace_or_insert_at .=+

        replace_or_insert_at::'' : (\Function : (
            virtual : True,
            returns : \$Accessible,
            matches : (Accessible, (Any, Any)),
        )),

        '.=+' : (\Alias : ( of : \$replace_or_insert_at, )),

The virtual function C<replace_or_insert_at> aka C<.=+> behaves identically
in turn to each of the functions C<replace_at> and C<shiftless_insert_at>
when given the same arguments, where the C<0> argument does or doesn't,
respectively, have a mapping whose key is equal to the C<1> argument's C<0>
attribute.  Other programming languages may name their corresponding
operators I<Item> or I<set> or I<put> or I<store> or I<update>; it is also
common to use subscript/postcircumfix syntax plus assignment syntax.

=head2 shiftless_maybe_remove_at .?-

        shiftless_maybe_remove_at::'' : (\Function : (
            virtual : True,
            returns : \$Accessible,
            matches : (\$Accessible, \$Any),
        )),

        '.?-' : (\Alias : ( of : \$shiftless_maybe_remove_at, )),

The virtual function C<shiftless_maybe_remove_at> aka C<.?-> behaves
identically to C<shiftless_remove_at> when given the same arguments but
that it simply results in its C<0> argument when that has no mapping whose
key matches its C<1> argument, rather than fail.  Other programming
languages may name their corresponding operators I<Remove> or I<remove> or
I<delete> or I<unset>.

=head1 HOMOGENEOUS DATA TYPES

=head2 Homogeneous

        Homogeneous : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Emptyable},
        )),

The interface type definer C<Homogeneous> is semifinite.  A C<Homogeneous> value
is a I<collective> value such that every one of its component I<members> is
conceptually of a common data type with its fellow members or should be
interpreted logically in the same way.  Idiomatically a C<Homogeneous> is a
generic collection which does not as a whole represent any kind of thing in
particular, such as a text or a graphic, and is simply the sum of its
I<members>; however some types which do represent such a particular kind of
thing may choose to compose C<Homogeneous> because it makes sense to
provide its operators.  The default value of C<Homogeneous> is the C<Array>
value with zero members, C<[]>.

If a C<Homogeneous> value is also C<Unionable>, then another value of its
collection type can be derived by either inserting new members whose values
are distinct from those already in the collection or by removing arbitrary
members from the collection; otherwise, that may not be possible.
If a C<Homogeneous> value is also C<Discrete>, all of its members can be
enumerated as individuals and counted; otherwise, that may not be possible.
If a C<Homogeneous> value is also C<Positional>, all of its members are
arranged in an explicit total order and can both be enumerated in that
order as well as be looked up by integral ordinal position against
that order; otherwise, that may not be possible.  If a C<Homogeneous> value
is also C<Setty>, all of its members are guaranteed to be distinct values;
otherwise, duplication of values may occur amongst members.

C<Homogeneous> is composed, directly or indirectly, by: C<Unionable>,
C<Discrete>, C<Positional>, C<Array>, C<Set>, C<Bag>,
C<Relational>, C<Tuple_Array>, C<Relation>, C<Tuple_Bag>, C<Intervalish>,
C<Interval>, C<Unionable_Intervalish>, C<Interval_Set>, C<Interval_Bag>.

I<TODO.  Note that for all the regular function-taking member-wise
Homogeneous operators that are logically supposed to work with each
collection member in isolation and/or shouldn't put significance on either
duplicate members or member position in the collection, including the likes
of {any, where, map} etc, but not including the likes of {reduce}, the
operators will only be passing the asset portion (where applicable) of the
member to the higher-order function, and not say the ordinal-position-asset pair for a
Positional or the asset-count pair for a Baggy.  Note that for a Relation
or Tuple_Bag each entire Tuple is the member asset, and for a Dictionary
the pair is the asset.  (With the corresponding attribute-wise Tuple
operators, they are given the whole attribute name-asset pair.)  The main
reason for this is to help ensure consistency of results while supporting a
variety of collection implementations including ones that are lazy, such as
by not eagerly counting duplicates, or that use distributed computation.
For that matter, the likes of {reduce} should probably be treated the same
way; see also the 'repeater' function trait that helps optimize it.
Surely, any time when one may think these operations need to know the
baggy count or ordinal position, its for a problem best solved differently.>

=head2 singular

        singular::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Homogeneous,),
        )),

The virtual function C<singular> results in C<True> iff its C<0> argument
has exactly 1 distinct member value, and C<False> otherwise.

=head2 only_member

        only_member::'' : (\Function : (
            virtual : True,
            returns : \$Any,
            matches : (\$Homogeneous,),
            accepts : (singular args:.\0),
        )),

The virtual function C<only_member> results in its C<0> argument's only
distinct member value.  This function will fail if the argument doesn't
have exactly 1 distinct member value.  The general deterministic way to
select a single possibly-unknown member of a C<Homogeneous> value is to
first derive from the latter another C<Homogeneous> value with exactly 1
distinct member (using means such as filtering or mapping or reducing) and
then use this function on the derived one.  For types such as C<Set> or
C<Bag> one can't select a member using an ordinal position, say, like with an
C<Array>, and a general solution provided by other programming languages
for simply picking I<a> member would give an effectively random one.

I<TODO: See also the C# methods "Single" and "SingleOrDefault" etc.>

=head2 in ∈

        in : (\Function : (
            commutes : \$has,
        )),

        Unicode_Aliases::'∈' : (\Alias : ( of : \$in, )),

The function C<in> aka C<∈> results in C<True> iff its C<0> argument is
equal to at least 1 member value of its C<1> argument; otherwise it results
in C<False>.  Note that this operation is also known as I<containment>.
Other programming languages may name their corresponding operators
I<in_array>.

=head2 not_in ∉

        not_in : (\Function : (
            commutes : \$not_has,
        )),

        Unicode_Aliases::'∉' : (\Alias : ( of : \$not_in, )),

The function C<not_in> aka C<∉> results in C<True> iff its C<0> argument is
equal to no member value of its C<1> argument; otherwise it results in
C<False>.

=head2 has ∋

        has : (\Function : (
            returns : \$Boolean,
            matches : (\$Homogeneous, \$Any),
            evaluates : (has_n::(args:.\0, args:.\1, 1)),
        )),

        Unicode_Aliases::'∋' : (\Alias : ( of : \$has, )),

The function C<has> aka C<∋> results in C<True> iff its C<0> argument has
at least 1 member whose value is equal to its C<1> argument; otherwise it
results in C<False>.  Other programming languages may name their
corresponding operators I<contains> or I<exists> or I<includes>.

=head2 not_has ∌

        not_has : (\Function : (
            negates : \$has,
        )),

        Unicode_Aliases::'∌' : (\Alias : ( of : \$not_has, )),

The function C<not_has> aka C<∌> results in C<True> iff its C<0> argument
does not have any member whose value is equal to its C<1> argument;
otherwise it results in C<False>.

=head2 has_n

        has_n::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Homogeneous, \$Any, \$Integer_NN),
        )),

The virtual function C<has_n> results in C<True> iff its C<0> argument has
at least N members such that each is equal to its C<1> argument, where N is
defined by its C<2> argument; otherwise it results in C<False>.  The
result is always C<True> when the C<2> argument is zero.  Note that
having a C<2> argument greater than 1 in combination with a C<Setty> typed
C<0> argument will always result in C<False>.

=head2 multiplicity

        multiplicity::'' : (\Function : (
            virtual : True,
            returns : \$Integer_NN,
            matches : (\$Homogeneous, \$Any),
        )),

The virtual function C<multiplicity> results in the integral count of
members of its C<0> argument such that each member value is equal to its
C<1> argument.  For a C<Setty> typed C<0> argument, the result is always
just 0 or 1.

=head2 all_unique

        all_unique::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Homogeneous,),
        )),

The virtual function C<all_unique> results in C<True> iff its C<0> argument
has no 2 members that are the same value, and C<False> otherwise.  The
result is always C<True> for a C<Setty> argument.

=head2 unique

        unique::'' : (\Function : (
            virtual : True,
            returns : \$Homogeneous,
            matches : (\$Homogeneous,),
        )),

The virtual function C<unique> results in the value of its C<0> argument's
collection type that has, for every distinct member value I<V> of the
function's C<0> argument, exactly 1 member whose value is equal to I<V>.
The result is always the same value as its argument when that is C<Setty>.
If the result's type is C<Positional>, then each retained member is the one
closest to the start of the argument out of those members sharing the
retained member's value.  See also the C<Positional> function C<squish>.

=head2 proper_subset_of ⊂

        proper_subset_of : (\Function : (
            returns : \$Boolean,
            matches : (\$Homogeneous, \$Homogeneous),
            evaluates : (args:.\0 != args:.\1 and (args:.\0 subset_of args:.\1)),
        )),

        Unicode_Aliases::'⊂' : (\Alias : ( of : \$proper_subset_of, )),

The function C<proper_subset_of> aka C<⊂> results in C<True> iff the
multiset of members of its C<0> argument is a proper subset of the
multiset of members of its C<1> argument; otherwise it results in C<False>.
Note that this operation is also known as I<strict multiset inclusion>.

=head2 not_proper_subset_of ⊄

        not_proper_subset_of : (\Function : (
            negates : \$proper_subset_of,
        )),

        Unicode_Aliases::'⊄' : (\Alias : ( of : \$not_proper_subset_of, )),

The function C<not_proper_subset_of> aka C<⊄> results in C<True> iff the
multiset of members of its C<0> argument is not a proper subset of the
multiset of members of its C<1> argument; otherwise it results in C<False>.

=head2 proper_superset_of ⊃

        proper_superset_of : (\Function : (
            commutes : \$proper_subset_of,
        )),

        Unicode_Aliases::'⊃' : (\Alias : ( of : \$proper_superset_of, )),

The function C<proper_superset_of> aka C<⊃> results in C<True> iff the
multiset of members of its C<0> argument is a proper superset of the
multiset of members of its C<1> argument; otherwise it results in C<False>.

=head2 not_proper_superset_of ⊅

        not_proper_superset_of : (\Function : (
            negates : \$proper_superset_of,
        )),

        Unicode_Aliases::'⊅' : (\Alias : ( of : \$not_proper_superset_of, )),

The function C<not_proper_superset_of> aka C<⊅> results in C<True> iff the
multiset of members of its C<0> argument is not a proper superset of the
multiset of members of its C<1> argument; otherwise it results in C<False>.

=head2 subset_of ⊆

        subset_of::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Homogeneous, \$Homogeneous),
        )),

        Unicode_Aliases::'⊆' : (\Alias : ( of : \$subset_of, )),

The function C<subset_of> aka C<⊆> results in C<True> iff the multiset of
members of its C<0> argument is a subset of the multiset of members of
its C<1> argument; otherwise it results in C<False>.  Note that this
operation is also known as I<multiset inclusion>.

=head2 not_subset_of ⊈

        not_subset_of : (\Function : (
            negates : \$subset_of,
        )),

        Unicode_Aliases::'⊈' : (\Alias : ( of : \$not_subset_of, )),

The function C<not_subset_of> aka C<⊈> results in C<True> iff the multiset
of members of its C<0> argument is not a subset of the multiset of
members of its C<1> argument; otherwise it results in C<False>.

=head2 superset_of ⊇

        superset_of : (\Function : (
            commutes : \$subset_of,
        )),

        Unicode_Aliases::'⊇' : (\Alias : ( of : \$superset_of, )),

The function C<superset_of> aka C<⊇> results in C<True> iff the multiset of
members of its C<0> argument is a superset of the multiset of members of
its C<1> argument; otherwise it results in C<False>.

=head2 not_superset_of ⊉

        not_superset_of : (\Function : (
            negates : \$superset_of,
        )),

        Unicode_Aliases::'⊉' : (\Alias : ( of : \$not_superset_of, )),

The function C<not_superset_of> aka C<⊉> results in C<True> iff the
multiset of members of its C<0> argument is not a superset of the multiset
of members of its C<1> argument; otherwise it results in C<False>.

=head2 same_members

        same_members::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Homogeneous, \$Homogeneous),
            is_commutative : True,
        )),

The virtual function C<same_members> results in C<True> iff the multiset of
members of its C<0> argument is the same as the multiset of members of its
C<1> argument; otherwise it results in C<False>.  This function may result
in C<True> for some C<Positional> arguments for which C<same> would result
in C<False> because the latter considers member order significant while the
former doesn't; for non-C<Positional> arguments, the 2 functions are
typically the same.

=head2 proper_subset_or_superset

        proper_subset_or_superset : (\Function : (
            returns : \$Boolean,
            matches : (\$Homogeneous, \$Homogeneous),
            is_commutative : True,
            evaluates : (not (args:.\0 same_members args:.\1) and (args:.\0 subset_or_superset args:.\1)),
        )),

The function C<proper_subset_or_superset> results in C<True> iff the
multiset of members of one of its 2 arguments C<0> and C<1> is a proper
subset of the multiset of members of its other argument; otherwise it
results in C<False>.

=head2 subset_or_superset

        subset_or_superset : (\Function : (
            returns : \$Boolean,
            matches : (\$Homogeneous, \$Homogeneous),
            is_commutative : True,
            evaluates : ((args:.\0 subset_of args:.\1) or (args:.\0 superset_of args:.\1)),
        )),

The function C<subset_or_superset> results in C<True> iff the multiset of
members of one of its 2 arguments C<0> and C<1> is a subset of the multiset
of members of its other argument; otherwise it results in C<False>.

=head2 overlaps_members

        overlaps_members::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Homogeneous, \$Homogeneous),
            is_commutative : True,
        )),

The virtual function C<overlaps_members> results in C<True> iff, given I<X> as the
multiset of members of its argument C<0> and I<Y> as the multiset of
members of its argument C<1>, there exists at least 1 member that both I<X>
and I<Y> have, and there also exists at least 1 other member each of I<X>
and I<Y> that the other doesn't have; otherwise it results in C<False>.

=head2 disjoint_members

        disjoint_members::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Homogeneous, \$Homogeneous),
            is_commutative : True,
        )),

The virtual function C<disjoint_members> results in C<True> iff the multiset of
members of its C<0> argument has no member values in common with the
multiset of members of its C<1> argument; otherwise it results in C<False>.

=head2 any there_exists ∃

        any::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Homogeneous, \$Signature),
        )),

        there_exists : (\Alias : ( of : \$any, )),

        Unicode_Aliases::'∃' : (\Alias : ( of : \$any, )),

I<TODO.  Result is true when at least one member evaluates to true.
This is logically equivalent to testing if a same-source 'where' result is nonempty,
but 'any' is instead virtual with applicable Foundation-level functions to
aid efficiency with less-savvy optimizers that don't know to short-circuit.>

I<TODO.  Definition of any/none/all etc would have to be altered for
intervalish types in general such that they only evaluate endpoints rather
than all members, and therefore the result would only be valid for
predicates that for the given member types would always produce the same
results for endpoints as for non-endpoints the former bound.>

=head2 none there_does_not_exist ∄

        none : (\Function : (
            negates : \$any,
        )),

        there_does_not_exist : (\Alias : ( of : \$none, )),

        Unicode_Aliases::'∄' : (\Alias : ( of : \$none, )),

I<TODO.  Result is true when no member evaluates to true.>

=head2 all for_all ∀

        all : (\Function : (
            returns : \$Boolean,
            matches : (\$Homogeneous, \$Signature),
            evaluates : (args:.\0 none \not_is_a::( 1: args:.\1 )),
        )),

        for_all : (\Alias : ( of : \$all, )),

        Unicode_Aliases::'∀' : (\Alias : ( of : \$all, )),

I<TODO.  Result is true when no member evaluates to false.>

I<Other languages: "TrueForAll".>

=head2 not_all

        not_all : (\Function : (
            negates : \$all,
        )),

I<TODO.  Result is true when at least one member evaluates to false.>

=head1 UNIONABLE DATA TYPES

=head2 Unionable

        Unionable : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Homogeneous},
        )),

The interface type definer C<Unionable> is semifinite.  A C<Unionable> value is a
C<Homogeneous> value such that another value of its collection type can be
derived by either inserting new members whose values are distinct from
those already in the collection or by removing arbitrary members from the
collection.  The default value of C<Unionable> is the C<Array> value with
zero members, C<[]>.

C<Unionable> is composed, directly or indirectly, by: C<Discrete>,
C<Positional>, C<Array>, C<Set>, C<Bag>, C<Relational>,
C<Tuple_Array>, C<Relation>, C<Tuple_Bag>, C<Unionable_Intervalish>,
C<Interval_Set>, C<Interval_Bag>.  A key type that composes C<Homogeneous>
but not C<Unionable> is C<Interval>; use C<Interval_Set> instead for its
C<Unionable> closest analogy.

=head2 insert

        insert : (\Function : (
            returns : \$Unionable,
            matches : (\$Unionable, \$Any),
            evaluates : (insert_n::(args:.\0, args:.\1, 1)),
        )),

The function C<insert> results in the value of its C<0> argument's
collection type that has all of the members of the function's C<0> argument
plus 1 additional member that is equal to its C<1> argument; its semantics
are identical to those of C<insert_n> where N is 1.  Other programming
languages may name their corresponding operators I<add>; it is also common
to use assignment syntax.

=head2 insert_n

        insert_n::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Unionable, \$Any, \$Integer_NN),
        )),

The virtual function C<insert_n> results in the value of its C<0>
argument's collection type that has all of the members of the function's
C<0> argument plus N additional members that are each equal to its C<1>
argument, where N is defined by its C<2> argument; however, if the result's
type is C<Setty>, the result will have no more than 1 member equal to the
C<1> argument (any duplicates will be silently eliminated), so the result
may equal the C<0> argument even when the C<2> argument is nonzero.  If the
result's type is C<Positional>, then the result starts with all of the
members of C<0> in the same order and ends with any added instances of C<1>.

=head2 remove

        remove : (\Function : (
            returns : \$Unionable,
            matches : (\$Unionable, \$Any),
            evaluates : (remove_n::(args:.\0, args:.\1, 1)),
        )),

The function C<remove> results in the value of its C<0> argument's
collection type that has all of the members of the function's C<0> argument
minus 1 existing member that is each equal to its C<1> argument; its
semantics are identical to those of C<remove_n> where N is 1.  Other
programming languages may name their corresponding operators I<delete>.

=head2 remove_n

        remove_n::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Unionable, \$Any, \$Integer_NN),
        )),

The virtual function C<remove_n> results in the value of its C<0>
argument's collection type that has all of the members of the function's
C<0> argument minus N existing members that are each equal to its C<1>
argument, where N is defined as the lesser of its C<2> argument and the
count of members of C<0> equal to the C<1> argument, so the result may
equal the C<0> argument even when the C<2> argument is nonzero.  If the
result's type is C<Positional>, then the removed instances of C<1> are
those closest to the end of C<0>.  Note that C<remove_n> is designed to
mirror C<insert_n>, so the identity C<c = remove_n::(insert_n::(c,x,n),x,n)>
should hold for any C<Unionable> type, even a C<Positional> one, except
with a C<Setty> C<c> that already has an C<x> element with a nonzero C<n>.

=head2 member_plus ⊎

        member_plus::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Unionable, \$Unionable),
            is_associative : True,
        )),

        Unicode_Aliases::'⊎' : (\Alias : ( of : \$member_plus, )),

The virtual function C<member_plus> aka C<⊎> results in the I<multiset sum>
of its 2 arguments C<0> and C<1>.  The result is a value of the function's
C<0> argument's collection type that has all of the members of the
function's C<0> argument plus all of the members of its C<1> argument.  For
every distinct member value of the result, its multiplicity is the integral
sum of the multiplicities of that same member value of each of the 2
arguments; however, if the result's type is C<Setty>, the result will only
have 1 member per distinct member value (any duplicates will be silently
eliminated).  If the result's type is C<Positional>, then the result starts
with all of the members of C<0> and ends with the members of C<1>, the
members from both in the same order as in their respective arguments; that
is, this function then behaves identically to C<catenate> aka C<~> when
given the same arguments.  This operation has a I<two-sided identity element> value of a
collection with zero members.  For non-ordered types, this operation is
also commutative.  Other programming languages may name their corresponding
operators I<union all> or I<+>.

=head2 except ∖

        except::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Unionable, \$Unionable),
        )),

        Unicode_Aliases::'∖' : (\Alias : ( of : \$except, )),

The virtual function C<except> aka C<∖> results in the I<multiset
difference> or I<multiset relative complement>
between its 2 arguments C<0> (I<minuend>) and C<1>
(I<subtrahend>).  The result is a value of the function's C<0> argument's
collection type that has all of the members of the function's C<0> argument
minus all of the matching members of its C<1> argument.  For every distinct
member value of the result, its multiplicity is the integral difference of
the multiplicities of that same member value of each of the 2 arguments
(when subtracting the I<subtrahend> from the I<minuend>); a multiplicity is
zero when it would otherwise be negative.  If the result's type is
C<Positional>, then the removed instances of any distinct member value are
those closest to the end of C<0>.  Note that C<except> is designed to
mirror C<member_plus>, so the identity C<x = except::(member_plus::(x,y),y)>
should hold for any C<Unionable> type, even a C<Positional> one, except
with C<Setty> C<x> and C<y> that have any members that are the same value.
This operation has a I<right identity element> value of a collection with zero members.
Other programming languages may name their corresponding operators I<minus>
or I<-> or I<difference> or I<\\> or I<complement> or I<setdiff> or I<diff>
or I<--> etc or I<subtract>.

=head2 intersect ∩

        intersect::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Unionable, \$Unionable),
            is_associative : True,
            is_idempotent : True,
        )),

        Unicode_Aliases::'∩' : (\Alias : ( of : \$intersect, )),

The virtual function C<intersect> aka C<∩> results in the I<multiset
intersection> of its 2 arguments C<0> and C<1>.  The result is a value of
the function's C<0> argument's collection type that has all of the members
of the function's C<0> argument that match their own members of its C<1>
argument.  For every distinct member value of the result, its multiplicity
is the integral minimum of the multiplicities of that same member value of
each of the 2 arguments (any nonmatched argument member does not appear in
the result).  If the result's type is C<Positional>, then the removed
instances of any distinct member value are those closest to the end of
C<0>.  This operation conceptually has a I<two-sided identity element> value of a collection
with an infinite number of members.  (For C<Setty> collections whose member type is
finite, the I<two-sided identity element> of C<intersect> instead simply has 1 member
for every member of that member type.)  For non-ordered types, this operation
is also commutative.  Other programming languages may name their
corresponding operators I<&> or I<*>.

=head2 union ∪

        union::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Unionable, \$Unionable),
            is_idempotent : True,
        )),

        Unicode_Aliases::'∪' : (\Alias : ( of : \$union, )),

The virtual function C<union> aka C<∪> results in the I<multiset union> of
its 2 arguments C<0> and C<1>.  The result is a value of the function's
C<0> argument's collection type that has all of the members of the
function's C<0> argument plus all of the nonmatching members of its C<1>
argument.  For every distinct member value of the result, its multiplicity
is the integral maximum of the multiplicities of that same member value of
each of the 2 arguments.  If the result's type is C<Positional>, then the
result starts with all of the members of C<0> and ends with the nonmatching
members of C<1>, the members from both in the same order as in their
respective arguments; the removed (due to matching) instances of any
distinct member value are those closest to the end of C<1>.  Note that the
identity C<union::(x,y) = member_plus::(x,except::(y,x))> should hold for
any C<Unionable> type, even a C<Positional> one.  This operation has a
I<two-sided identity element> value of a collection with zero members.  For non-ordered types,
this operation is also associative and commutative.  Other programming
languages may name their corresponding operators I<|> or I<+>.

=head2 exclusive symm_diff ∆

        exclusive::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Unionable, \$Unionable),
        )),

        symm_diff : (\Alias : ( of : \$exclusive, )),

        Unicode_Aliases::'∆' : (\Alias : ( of : \$exclusive, )),

The virtual function C<exclusive> aka C<symm_diff> aka C<∆> results in the
I<multiset symmetric difference> of its 2 arguments C<0> and C<1>.  The
result is a value of the function's C<0> argument's collection type that
has all of the members of each of the function's C<0> and C<1> arguments
that do not have matching members of their counterpart argument.  For every
distinct member value of the result, its multiplicity is the integral
maximum of the multiplicities of that same member value of each of the 2
arguments, minus the integral minimum of the same.  If the result's type is
C<Positional>, then the result starts with the nonmatching members of C<0>
and ends with the nonmatching members of C<1>, the members from both in the
same order as in their respective arguments; the removed (due to matching)
instances of any distinct member value are those closest to the end of C<0>
or C<1> respectively.  Note that the identity C<exclusive::(x,y) =
member_plus::(except::(x,y),except::(y,x)) =
except::(union::(x,y),intersect::(x,y))>
should hold for any C<Unionable> type, even a C<Positional> one.  This
operation has a I<two-sided identity element> value of a collection with zero members.  For
non-ordered types, this operation is also associative and commutative.
Other programming languages may name their corresponding operators
I<symmdiff> or I<^> or I<%>.

=head2 nest group

        nest::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Unionable,),
            accepts : (...),
        )),

        group : (\Alias : ( of : \$nest, )),

I<TODO.>

=head2 unnest ungroup

        unnest::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Unionable,),
            accepts : (...),
        )),

        ungroup : (\Alias : ( of : \$unnest, )),

I<TODO.>

=head2 where σ

        where::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Unionable, \$Signature),
        )),

        Unicode_Aliases::'σ' : (\Alias : ( of : \$where, )),

I<TODO.  The function-call is expected to be a Article with 2 attributes
named 'call' and 'args', which are a Function_Name and a Tuple respectively.
The 'args' can be any Tuple that doesn't have a '0' attr.
The function referenced by 'call' is expected to take a Tuple argument
whose '0' attr is a member of the collection and whose other attrs match
those given in 'args'.>
I<Ruby calls its version "select" (returns list) and a "keep_if" (mutates).>

I<Note: Ruby has a "reject" and a "delete_if" function that is like SQL's delete.>

I<TODO.  Definition of where/map/reduce etc would have to be altered for
intervalish types in general such that they only evaluate endpoints rather
than all members, and therefore the result may not be valid depending on
what the function argument does or on the given member types.>

=head2 filtering

        filtering : (\Function : (
            commutes : \$where,
        )),

I<TODO.>

=head2 map

        map::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Unionable, \$Function_Call_But_0),
        )),

I<TODO.  The function-call is as per that of 'where'.>
I<Ruby calls its version "collect".>

I<TODO.  For the general case of intervalish types, the result of map would
only be valid when the function argument is order-preserving, for example a
plain function that adds 3 to all members or multiplies all members by 2,
etc.  Also note that for intervals over successable types, some operations
may result in every member becoming discontinuous from the others, such as
multiply with integers.>

=head2 reduce

        reduce::'' : (\Function : (
            virtual : True,
            returns : \$Any,
            matches : (\$Unionable, \$Function_Call_But_0_1),
        )),

I<TODO.  Restrict "reduce" to take dyadic functions which are commutative
plus associative only, corresponding to more formal common usage of the
term "reduce".  Then add a pair of other operators, foo() and bar(),
to Positional where foo() requires associative but not commutative, and
is used for catenation, and bar() requires neither assoc/commut, and is used
for more esoteric things perhaps resembling a common meaning of "fold".
The next paragraph is partly obsoleted by this.>

I<TODO.  The function-call is expected to be a Article with 2 attributes
named 'call' and 'args', which are a Function_Name and a Tuple respectively.
The 'args' can be any Tuple that doesn't have either '0' or '1' attrs.
The function referenced by 'call' is expected to take a Tuple argument
whose '0' and '1' attrs are members of the collection and whose other attrs match
those given in 'args'.
Depending on the collection subtype, order of 0,1 may or may not be significant.
If collection is unordered, 'call' must be both associative and commutative,
and reduce can always be parallelized.
If collection is ordered, reduce can be parallelized if 'call' is associative;
otherwise the reduce is a serial operation, at least naively.>

I<TODO.  Also define related No_Identity_Element type.>

=head1 DISCRETE DATA TYPES

=head2 Discrete

        Discrete : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Unionable},
            provides_default_for : {\$Unionable},
        )),

The interface type definer C<Discrete> is semifinite.  A C<Discrete> value is a
C<Unionable> value such that all of its members can be enumerated as
individuals and counted.  The default value of C<Discrete> is the C<Array>
value with zero members, C<[]>.

C<Discrete> is composed, directly or indirectly, by: C<Positional>,
C<Array>, C<Set>, C<Bag>, C<Relational>, C<Tuple_Array>,
C<Relation>, C<Tuple_Bag>.

=head2 to_Set ?|

        to_Set::'' : (\Function : (
            virtual : True,
            returns : \$Set,
            matches : (\$Discrete,),
        )),

        '?|' : (\Alias : ( of : \$to_Set, )),

The virtual function C<to_Set> aka C<?|> results in the C<Set> value
that represents the same set of distinct member values as its C<0>
argument.  The purpose of C<to_Set> is to canonicalize C<Discrete> values
so they can be treated abstractly as sets of discrete values, for
operations where neither multiplicity nor order is significant.

=head2 to_Bag +|

        to_Bag::'' : (\Function : (
            virtual : True,
            returns : \$Bag,
            matches : (\$Discrete,),
        )),

        '+|' : (\Alias : ( of : \$to_Bag, )),

The virtual function C<to_Bag> aka C<+|> results in the C<Bag> value
that represents the same multiset of members as its C<0> argument.  The
purpose of C<to_Bag> is to canonicalize C<Discrete> values so they can be
treated abstractly as multisets of discrete values, for operations where
multiplicity possibly is significant but order isn't.

=head2 count cardinality #

        count::'' : (\Function : (
            virtual : True,
            returns : \$Integer_NN,
            matches : (\$Discrete,),
        )),

        cardinality : (\Alias : ( of : \$count, )),
        '#'         : (\Alias : ( of : \$count, )),

The virtual function C<count> aka C<cardinality> aka C<#> results in the
integral count of the members of its C<0> argument; when multiple members
have the same member value, every member counts as 1 towards the total.

=head2 unique_count

        unique_count::'' : (\Function : (
            virtual : True,
            returns : \$Integer_NN,
            matches : (\$Discrete,),
        )),

The virtual function C<unique_count> results in the integral count of the
distinct member values of its C<0> argument.

=head2 order

        order : (\Function : (
            returns : \$Positional,
            matches : (\$Discrete,),
            evaluates : (args:.\0 order_using \in_order::()),
        )),

The function C<order> results in the C<Positional> value that represents
the same multiset of members as its C<0> argument but that the members are
arranged into a sequence in accordance with a total order defined by the
function C<in_order>.  This function will succeed iff C<in_order> is
defined for the types of the members or they are of an C<Orderable> type.

=head2 order_using

        order_using::'' : (\Function : (
            virtual : True,
            returns : \$Positional,
            matches : (\$Discrete, \$Function_Call_But_0_1),
        )),

The virtual function C<order_using> results in the C<Positional> value that
represents the same multiset of members as its C<0> argument but that the
members are arranged into a sequence in accordance with a total order
defined by the function given in its C<1> argument.  Iff the members of the
C<0> argument are all of the same C<Orderable>-composing type, then the
generic C<in_order> function may be used as the C<1> argument; regardless,
the C<1> argument can define any total order it likes for members that are
of any type, both C<Orderable> or not.

=head1 POSITIONAL DATA TYPES

=head2 Positional

        Positional : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Stringy, \$Discrete, \$Accessible},
            provides_default_for : {\$Discrete},
        )),

The interface type definer C<Positional> is semifinite.  A C<Positional> value is
both a C<Stringy> value and a C<Discrete> value; it is a homogeneous
aggregate of other, I<member> values that are arranged in an explicit total
order and can both be enumerated in that order as well as be looked up by
integral ordinal position against that order; there is a single
canonical interpretation of where each I<member> begins and ends within the
aggregate.  A C<Positional> value is dense, meaning that every one of its
members exists at a distinct adjacent integral position; a C<Positional>
preserves the identity that the count of its members is equal to one plus
the difference of its first and last ordinal positions.  Idiomatically, a
C<Positional> value is zero-based, meaning its first-ordered member is at
ordinal position C<0>, but that doesn't have to be the case.  C<Positional>
requires that for every composing type definer I<T> there is a single integral
value I<P> such that the first ordinal position of every value of I<T> is
I<P>; as such, any catenation or slice of C<Positional> values would have
well-definined shifting of member values between ordinal positions.

The default value of C<Positional> is the C<Array> value with zero members,
C<[]>.  C<Positional> is C<Orderable> in the general case conditionally
depending on whether all of its member values are mutually C<Orderable>
themselves; its minimum value is the same C<[]> as its default value; it
has no maximum value.  The ordering algorithm of C<Positional> is based on
pairwise comparison of its members by matching ordinal position starting at the lowest
ordinal position; iff C<Positional> value X is a leading sub-sequence of C<Positional>
value Y, then X is ordered before Y; otherwise, the mutual ordering of the
lowest-ordinal-positioned non-matching members of X and Y determines that the ordering
of X and Y as a whole are the same as said members.  C<Positional> is
composed, directly or indirectly, by: C<Array>, C<Tuple_Array>.

=head2 singular (Positional)

        singular::Positional : (\Function : (
            returns : \$Boolean,
            matches : (\$Positional,),
            implements : \$folder::'',
            evaluates : ((unique_count args:.\0) = 1),
        )),

The function C<singular::Positional> results in C<True> iff its C<0>
argument has exactly 1 distinct member value, and C<False> otherwise.  This
function implements the C<Homogeneous> virtual function C<singular> for the
composing type C<Positional>.

=head2 only_member (Positional)

        only_member::Positional : (\Function : (
            returns : \$Any,
            matches : (\$Positional,),
            implements : \$folder::'',
            accepts : (singular args:.\0),
            evaluates : (first args:.\0),
        )),

The function C<only_member::Positional> results in its C<0> argument's only
distinct member value.  This function will fail if the argument doesn't
have exactly 1 distinct member value.  This function implements the
C<Homogeneous> virtual function C<only_member> for the composing type
C<Positional>.

=head2 subset_of (Positional)

        subset_of::Positional : (\Function : (
            returns : \$Boolean,
            matches : (\$Positional, \$Positional),
            implements : \$folder::'',
            evaluates : ((to_Bag args:.\0) subset_of (to_Bag args:.\1)),
        )),

The function C<subset_of::Positional> results in C<True> iff the multiset
of members of its C<0> argument is a subset of the multiset of members of
its C<1> argument; otherwise it results in C<False>.  This function
implements the C<Homogeneous> virtual function C<subset_of> aka C<⊆> for
the composing type C<Positional>.

=head2 same_members (Positional)

        same_members::Positional : (\Function : (
            returns : \$Boolean,
            matches : (\$Positional, \$Positional),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : ((to_Bag args:.\0) same_members (to_Bag args:.\1)),
        )),

The function C<same_members::Positional> results in C<True> iff the
multiset of members of its C<0> argument is the same as the multiset of
members of its C<1> argument; otherwise it results in C<False>.  This
function may result in C<True> for some arguments for which C<same> would
result in C<False> because the latter considers member order significant
while the former doesn't.  This function implements the C<Homogeneous>
virtual function C<same_members> for the composing type C<Positional>.

=head2 overlaps_members (Positional)

        overlaps_members::Positional : (\Function : (
            returns : \$Boolean,
            matches : (\$Positional, \$Positional),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : ((to_Bag args:.\0) overlaps_members (to_Bag args:.\1)),
        )),

The function C<overlaps_members::Positional> results in C<True> iff, given
I<X> as the multiset of members of its argument C<0> and I<Y> as the
multiset of members of its argument C<1>, there exists at least 1 member
that both I<X> and I<Y> have, and there also exists at least 1 other member
each of I<X> and I<Y> that the other doesn't have; otherwise it results in
C<False>.  This function implements the C<Homogeneous> virtual function
C<overlaps_members> for the composing type C<Positional>.

=head2 disjoint_members (Positional)

        disjoint_members::Positional : (\Function : (
            returns : \$Boolean,
            matches : (\$Positional, \$Positional),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : ((to_Bag args:.\0) disjoint_members (to_Bag args:.\1)),
        )),

The function C<disjoint_members::Positional> results in C<True> iff the
multiset of members of its C<0> argument has no member values in common
with the multiset of members of its C<1> argument; otherwise it results in
C<False>.  This function implements the C<Homogeneous> virtual function
C<disjoint_members> for the composing type C<Positional>.

=head2 member_plus (Positional)

        member_plus::Positional : (\Function : (
            returns : \$Positional,
            matches : (\$Positional, \$Positional),
            implements : \$folder::'',
            is_associative : True,
            evaluates : (args:.\0 ~ args:.\1),
        )),

The function C<member_plus::Positional> results in the I<multiset sum> of
its 2 arguments C<0> and C<1>; it behaves identically to C<catenate> aka
C<~> when given the same arguments.  This function implements the
C<Unionable> virtual function C<member_plus> aka C<⊎> for the composing
type C<Positional>.

=head2 unique_count (Positional)

        unique_count::Positional : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Positional,),
            implements : \$folder::'',
            evaluates : (count::(to_Set args:.\0)),
        )),

The function C<unique_count::Positional> results in the integral count of
the distinct member values of its C<0> argument.  This function implements
the C<Discrete> virtual function C<unique_count> for the composing type
C<Positional>.

=head2 has_any_at (Positional)

        has_any_at::Positional : (\Function : (
            returns : \$Boolean,
            matches : (\$Positional, \$Integer),
            implements : \$folder::'',
            evaluates : (args:.\1 >= first_possible_ord_pos::(args:.\0)
                and args:.\1 < first_unused_ord_pos::(args:.\0)),
        )),

The function C<has_any_at::Positional> results in C<True> iff its C<0>
argument has a member whose ordinal position is equal to its C<1> argument;
otherwise it results in C<False>.  This function implements the
C<Accessible> virtual function C<has_any_at> aka C<.?> for the composing
type C<Positional>.

=head2 has_mapping_at (Positional)

        has_mapping_at::Positional : (\Function : (
            returns : \$Boolean,
            matches : (Positional, (Integer, Any)),
            implements : \$folder::'',
            evaluates : (args:.\0 .? (args:.\1.\0) and_then guard args:.\0.(args:.\1.\0) = (args:.\1.\1)),
        )),

The function C<has_mapping_at::Positional> results in C<True> iff its C<0>
argument has a member that is equal to its C<1> argument's C<1> attribute,
where the ordinal position of that member is equal to the C<1> argument's
C<0> attribute; otherwise it results in C<False>.  This function implements
the C<Accessible> virtual function C<has_mapping_at> aka C<.:?> for the
composing type C<Positional>.

=head2 mapping_at (Positional)

        mapping_at::Positional : (\Function : (
            returns : (\$Integer, \$Any),
            matches : (\$Positional, \$Integer),
            implements : \$folder::'',
            accepts : (args:.\0 .? args:.\1),
            evaluates : ((args:.\1, args:.\0.args:.\1)),
        )),

The function C<mapping_at::Positional> results in a binary C<Tuple> whose
C<0> attribute is the function's C<1> argument and whose C<1> attribute is
the member value of the function's C<0> argument whose ordinal position is
equal to its C<1> argument.  This function will fail if the C<0> argument
doesn't have such a member.  This function implements the C<Accessible>
virtual function C<mapping_at> aka C<.:> for the composing type
C<Positional>.

=head2 maybe_at (Positional)

        maybe_at::Positional : (\Function : (
            returns : \$Any,
            matches : (\$Positional, \$Integer),
            implements : \$folder::'',
            evaluates : (if args:.\0 .? args:.\1 then guard args:.\0.args:.\1 else \!No_Such_Ord_Pos),
        )),

The function C<maybe_at::Positional> results in the member value of its
C<0> argument whose ordinal position is equal to its C<1> argument, iff there
is such a member; otherwise it results in C<\!No_Such_Ord_Pos>.  This function
implements the C<Accessible> virtual function C<maybe_at> aka C<.!> for the
composing type C<Positional>.

=head2 replace_at (Positional)

        replace_at::Positional : (\Function : (
            returns : \$Positional,
            matches : (Positional, (Integer, Any)),
            implements : \$folder::'',
            accepts : (args:.\0 .? (args:.\1.\0)),
            evaluates : (
                src ::= args:.\0;
                rop ::= args:.\1.\0;
                repl_member ::= args:.\1.\1;
                fop ::= first_ord_pos src;
                lop ::= last_ord_pos src;
                emp ::= empty src;
                returns (if rop > fop then guard slice_range::(src, fop, --rop) else emp)
                    insert repl_member
                    catenate (if rop < lop then guard slice_range::(src, ++rop, lop) else emp);
            ),
        )),

The function C<replace_at::Positional> results in the value of its C<0>
argument's collection type that has all of the members of the function's
C<0> argument in the same order but that, for the 1 member of the C<0>
argument whose ordinal position I<K> is equal to the function's C<1>
argument's C<0> attribute, the result instead has at ordinal position I<K> a
member whose value is equal to the C<1> argument's C<1> attribute.  This
function will fail if the C<0> argument doesn't have a member with the
ordinal position I<K>.  This function implements the C<Accessible> virtual
function C<replace_at> aka C<.:=> for the composing type C<Positional>.

=head2 shiftless_insert_at (Positional)

        shiftless_insert_at::Positional : (\Function : (
            returns : \$Positional,
            matches : (Positional, (Integer, Any)),
            implements : \$folder::'',
            accepts : (args:.\1.\0 = first_unused_ord_pos::(args:.\0)),
            evaluates : (args:.\0 insert args:.\1.\1),
        )),

The function C<shiftless_insert_at::Positional> results in the value of its
C<0> argument's collection type that has all of the members of the
function's C<0> argument in the same order, plus 1 additional member I<M>
that is equal to its C<1> argument's C<1> attribute, where the ordinal
position I<K> in the result of I<M> is equal to the C<1> argument's C<0>
attribute.  This function will fail if I<K> is not equal to the C<0>
argument's C<first_unused_ord_pos>.  This function implements the
C<Accessible> virtual function C<shiftless_insert_at> aka C<.+> for the
composing type C<Positional>.

=head2 shiftless_remove_at (Positional)

        shiftless_remove_at::Positional : (\Function : (
            returns : \$Positional,
            matches : (\$Positional, \$Integer),
            implements : \$folder::'',
            accepts : (args:.\1 >= first_possible_ord_pos::(args:.\0)
                and args:.\1 = --first_unused_ord_pos::(args:.\0)),
            evaluates : (nonlast args:.\0),
        )),

The function C<shiftless_remove_at::Positional> results in the value of its
C<0> argument's collection type that has all of the members of the
function's C<0> argument in the same order minus 1 existing element whose
ordinal position I<K> is equal to its C<1> argument.  This function will fail
if either the C<0> argument doesn't have any members or if I<K> is not
equal to the C<0> argument's C<last_ord_pos>.  This function implements the
C<Accessible> virtual function C<shiftless_remove_at> aka C<.-> for the
composing type C<Positional>.

=head2 replace_or_insert_at (Positional)

        replace_or_insert_at::Positional : (\Function : (
            returns : \$Positional,
            matches : (Positional, (Integer, Any)),
            implements : \$folder::'',
            accepts : (args:.\1 >= first_possible_ord_pos::(args:.\0)
                and args:.\1 <= first_unused_ord_pos::(args:.\0)),
            evaluates : (if args:.\0 .? (args:.\1.\0) then guard args:.\0 .:= (args:.\1.\0) else guard args:.\0 .+ (args:.\1.\0)),
        )),

The function C<replace_or_insert_at::Positional> behaves identically in
turn to each of the functions C<replace_at> and C<shiftless_insert_at> when
given the same arguments, where the C<0> argument does or doesn't,
respectively, have a member whose ordinal position is equal to the C<1>
argument's C<0> attribute.  This function implements the C<Accessible>
virtual function C<replace_or_insert_at> aka C<.=+> for the composing type
C<Positional>.

=head2 shiftless_maybe_remove_at (Positional)

        shiftless_maybe_remove_at::Positional : (\Function : (
            returns : \$Positional,
            matches : (\$Positional, \$Integer),
            implements : \$folder::'',
            accepts : (args:.\1 >= first_possible_ord_pos::(args:.\0)
                and args:.\1 >= --first_unused_ord_pos::(args:.\0)),
            evaluates : (if args:.\1 = --first_unused_ord_pos::(args:.\0) then guard nonlast args:.\0 else args:.\0),
        )),

The function C<shiftless_maybe_remove_at::Positional> behaves identically
to C<shiftless_remove_at> when given the same arguments but that it simply
results in its C<0> argument when that has no member whose ordinal position
matches its C<1> argument, rather than fail.  This function implements the
C<Accessible> virtual function C<shiftless_maybe_remove_at> aka C<.?-> for
the composing type C<Positional>.

=head2 to_Array ~|

        to_Array::'' : (\Function : (
            virtual : True,
            returns : \$Array,
            matches : (\$Positional,),
        )),

        '~|' : (\Alias : ( of : \$to_Array, )),

The virtual function C<to_Array> aka C<~|> results in the C<Array>
value that represents the same sequence of members as its C<0> argument.
The purpose of C<to_Array> is to canonicalize C<Positional> values so they
can be treated abstractly as sequences of discrete values with minimal
effort.

=head2 squish

        squish : (\Function : (
            returns : \$Positional,
            matches : (\$Positional,),
            evaluates : (args:.\0 map \(( group : args:.\0, member : False ))
                pipe nest map \(args:.\0.\group)),
        )),

The function C<squish> results in the value of its C<0> argument's
ordered collection type that has all of the members of the function's C<0>
argument but that, for every run of 2 or more consecutive members that are
all the same value, that run retains only 1 of those members.  Iff the
argument is such that all appearances of each distinct value are adjacent
members, the result of C<squish> is the same as that of C<unique>.
TODO CAN AVOID SECOND LAMBDA?

=head2 first_possible_ord_pos

        first_possible_ord_pos::'' : (\Function : (
            virtual : True,
            returns : \$Integer,
            matches : (\$Positional,),
        )),

The virtual function C<first_possible_ord_pos> results in the integral first
possible ordinal position I<P> that every value of its C<0> argument's
collection type has in common.  Iff the C<0> argument I<C> is nonempty then
the result is also equal to the actual ordinal position of I<C>'s first
member; otherwise I<P> is the ordinal position that a subsequently-added
first member of I<C> would have.

=head2 first_unused_ord_pos

        first_unused_ord_pos : (\Function : (
            returns : \$Integer,
            matches : (\$Positional,),
            evaluates : (first_possible_ord_pos::(args:.\0) + #args:.\0),
        )),

The function C<first_unused_ord_pos> results in the integral first unused
ordinal position of its C<0> argument, which is the ordinal position that the
first subsequently-appended member would have.  Iff the C<0> argument I<C>
is nonempty then the result is one greater than the last ordinal position of
I<C>; otherwise the result is equal to the first possible ordinal position.  For a
zero-based C<Positional>, the result is equal to its C<count>.

=head2 first_ord_pos

        first_ord_pos : (\Function : (
            returns : \$Integer,
            matches : (\$Positional,),
            accepts : (so args:.\0),
            evaluates : (first_possible_ord_pos::(args:.\0)),
        )),

The function C<first_ord_pos> results in the integral ordinal position of its
nonempty C<0> argument's first member.

=head2 last_ord_pos

        last_ord_pos : (\Function : (
            returns : \$Integer,
            matches : (\$Positional,),
            accepts : (so args:.\0),
            evaluates : (--first_unused_ord_pos::(args:.\0)),
        )),

The function C<last_ord_pos> results in the integral ordinal position of its
nonempty C<0> argument's last member.

=head2 slice_n

        slice_n::'' : (\Function : (
            virtual : True,
            returns : \$Positional,
            matches : (\$Positional, \$Integer, \$NN_Integer),
            accepts : (args:.\1 >= first_possible_ord_pos::(args:.\0)
                and args:.\1 + args:.\2 <= first_unused_ord_pos::(args:.\0)),
        )),

The virtual function C<slice_n> results in the value of its C<0> argument's
ordered collection type that has exactly I<N> consecutive members of the
function's C<0> argument I<C>, in the same order, such that the first
member existed at ordinal position I<F> of I<C> where I<F> and I<N> are equal
to the function's C<1> and C<2> arguments respectively.  The taken members
do not necessarily occupy the same ordinal positions in the result as they
did in I<C>.  This function will fail if I<C> does not have members at any
of the ordinal positions requested.  Other programming languages may name
their corresponding operators I<array_slice>.

=head2 slice_range

        slice_range : (\Function : (
            returns : \$Positional,
            matches : (\$Positional, \$Integer, \$Integer),
            accepts : (so args:.\0 and args:.\1 >= first_possible_ord_pos::(args:.\0)
                and args:.\2 < first_unused_ord_pos::(args:.\0)),
            evaluates : (slice_n::(args:.\0, args:.\1, args:.\2 - args:.\1 + 1)),
        )),

The function C<slice_range> results in the value of its C<0> argument's
ordered collection type that has all of the members of the function's C<0>
argument I<C>, in the same order, such that the first and last members
existed at ordinal positions equal to the function's C<1> and C<2> arguments
respectively.  The taken members do not necessarily occupy the same ordinal
positions in the result as they did in I<C>.  This function will fail if
I<C> does not have members at any of the ordinal positions requested.  Other
programming languages may name their corresponding operators I<slice> or
may instead overload their array element subscripting syntax.

=head2 first

        first : (\Function : (
            returns : \$Any,
            matches : (\$Positional,),
            accepts : (so args:.\0),
            evaluates : (args:.\0 . first_ord_pos::(args:.\0)),
        )),

The function C<first> results in its nonempty C<0> argument's first member.

=head2 nonfirst

        nonfirst : (\Function : (
            returns : \$Positional,
            matches : (\$Positional,),
            accepts : (so args:.\0),
            evaluates : (slice_range::(args:.\0, ++first_ord_pos::(args:.\0), last_ord_pos::(args:.\0))),
        )),

The function C<nonfirst> results in the value of its C<0> argument's
ordered collection type that has all of the members of the function's
nonempty C<0> argument, in the same order, except for its very first one.
The taken members occupy ordinal positions of exactly 1 less in the result as
they did in the argument.

=head2 last

        last : (\Function : (
            returns : \$Any,
            matches : (\$Positional,),
            accepts : (so args:.\0),
            evaluates : (args:.\0 . last_ord_pos::(args:.\0)),
        )),

The function C<last> results in its nonempty C<0> argument's last member.

=head2 nonlast

        nonlast : (\Function : (
            returns : \$Positional,
            matches : (\$Positional,),
            accepts : (so args:.\0),
            evaluates : (slice_range::(args:.\0, first_ord_pos::(args:.\0), --last_ord_pos::(args:.\0))),
        )),

The function C<nonlast> results in the value of its C<0> argument's ordered
collection type that has all of the members of the function's nonempty C<0>
argument, in the same order, except for its very last one.  The taken
members occupy the exact same ordinal positions in the result as they did in
the argument.

=head2 ord_pos_succ_all_matches

        ord_pos_succ_all_matches::'' : (\Function : (
            virtual : True,
            returns : \$Integer,
            matches : (\$Positional, \$Positional),
            is_commutative : True,
        )),

I<TODO.  Also consider ord_pos_first_diff_elem or ord_pos_succ_common_prefix as name.>

=head1 ARRAY DATA TYPES

=head2 Array

        Array::'' : (\Function : (
            is_type_definer : True,
            composes : {\$Positional},
            provides_default_for : {\$Positional},
            evaluates : \foundation::Array(),
            default : [],
        )),

The selection type definer C<Array> represents the infinite Muldis D Foundation
type C<foundation::Array>.  An C<Array> value is a general purpose
arbitrarily-long ordered sequence of any other, I<member> values, which
explicitly does not represent any kind of thing in particular, and is
simply the sum of its members.  An C<Array> value is dense; iff it has any
members, then its first-ordered member is at ordinal position C<0>, and its
last-ordinal-positioned member is at the ordinal position that is one less than the
count of its members.  An C<Array> in the general case may have
multiple members that are the same value, and any duplicates may or may not
exist at consecutive ordinal positions.  The default value of C<Array> is C<[]>, the
only zero-member C<Array> value.  C<Array> is C<Orderable>; its minimum
value is the same C<[]> as its default value; it has no maximum value; its
ordering algorithm is defined by C<Positional>.  Other programming
languages may name their corresponding types I<List>.

=head2 Array_C0 ~∅

        Array_C0 : (\Function : (
            is_type_definer : True,
            constant : [],
        )),

        Unicode_Aliases::'~∅' : (\Alias : ( of : \$Array_C0, )),

The singleton type definer C<Array_C0> aka C<~∅> represents the only zero-member
C<Array> value, C<[]>.

=head2 in_order (Array)

        in_order::Array : (\Function : (
            returns : \$Boolean,
            matches : (\$Array, \$Array),
            implements : \$folder::'',
            evaluates : (
                i ::= args:.\0 ord_pos_succ_all_matches args:.\1;
              returns
                if not args:.\0 .? i then
                    e1 ::= True
                else if not args:.\1 .? i then
                    False
                else guard
                    e2 ::= args:.\0.i in_order args:.\1.i;

                e1 note "This is the case where LHS is a leading subsequence of or is equal to RHS.";
                e2 note "This will succeed iff in_order() is defined for the member type.";
            ),
        )),

The function C<in_order::Array> implements the C<Orderable> virtual
function C<in_order> for the composing type C<Array>.  This function
will succeed iff C<in_order> is also defined for the types of the members.

=head2 to_Boolean (Array)

        to_Boolean::Array : (\Function : (
            returns : \$Boolean,
            matches : (\$Array,),
            implements : \$folder::'',
            evaluates : (args:.\0 != []),
        )),

The function C<to_Boolean::Array> results in C<True> iff its C<0> argument
has any members, and in C<False> iff it has no members.  This function
implements the C<Boolable> virtual function C<to_Boolean> aka C<so> aka
C<?> for the composing type C<Array>.

=head2 empty (Array)

        empty::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array,),
            implements : \$folder::'',
            evaluates : ([]),
        )),

The function C<empty::Array> results in the only zero-member C<Array>
value.  This function implements the C<Emptyable> virtual function C<empty>
aka C<∅> for the composing type C<Array>.

=head2 substring_of (Array)

        substring_of::Array : (\Function : (
            returns : \$Boolean,
            matches : (\$Array, \$Array),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_substring_of()),
        )),

The function C<substring_of::Array> results in C<True> iff the sequence of
members of its C<0> argument is a substring of the sequence of members of
its C<1> argument; otherwise it results in C<False>.  This function
implements the C<Stringy> virtual function C<substring_of> for the
composing type C<Array>.

=head2 overlaps_string (Array)

        overlaps_string::Array : (\Function : (
            returns : \$Boolean,
            matches : (\$Array, \$Array),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (evaluates args --> \foundation::Array_overlaps_string()),
        )),

The function C<overlaps_string::Array> results in C<True> iff, given I<X>
as the sequence of members of its argument C<0> and I<Y> as the sequence of
members of its argument C<1>, when I<X> and I<Y> are overlapped to the
greatest possible extent such that every corresponding member pair has 2 of
the same value, the overlap of I<X> and I<Y> has at least 1 member, and
each of I<X> and I<Y> has at least 1 member that is not overlapped;
otherwise it results in C<False>.  This function implements the C<Stringy>
virtual function C<overlaps_string> for the composing type C<Array>.

=head2 disjoint_string (Array)

        disjoint_string::Array : (\Function : (
            returns : \$Boolean,
            matches : (\$Array, \$Array),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (evaluates args --> \foundation::Array_disjoint_string()),
        )),

The function C<disjoint_string::Array> results in C<True> iff the sequence
of members of its C<0> argument can not be overlapped with the sequence of
members of its C<1> argument by at least 1 member such that every
corresponding member pair has 2 of the same value; otherwise it results in
C<False>.  This function implements the C<Stringy> virtual function
C<disjoint_string> for the composing type C<Array>.

=head2 catenate (Array)

        catenate::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$Array),
            implements : \$folder::'',
            is_associative : True,
            identity : [],
            repeater : \$replicate::Array,
            evaluates : (evaluates args --> \foundation::Array_catenate()),
        )),

The function C<catenate::Array> results in the catenation of its 2
arguments C<0> and C<1> such that the result starts with the members of
C<0> and ends with the members of C<1>.  This function implements the
C<Stringy> virtual function C<catenate> aka C<~> for the composing type
C<Array>.

=head2 replicate (Array)

        replicate::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_replicate()),
        )),

The function C<replicate::Array> results in the catenation of N instances
of its C<0> argument where N is defined by its C<1> argument.  If the C<1>
argument is zero then the result is the only zero-member C<Array>.  This
function implements the C<Stringy> virtual function C<replicate> aka C<~#>
for the composing type C<Array>.

=head2 has_n (Array)

        has_n::Array : (\Function : (
            returns : \$Boolean,
            matches : (\$Array, \$Any, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_has_n()),
        )),

The function C<has_n::Array> results in C<True> iff its C<0> argument has
at least N members at any ordinal-positions such that each is equal to its C<1>
argument, where N is defined by its C<2> argument; otherwise it results in
C<False>.  This function implements the C<Homogeneous> virtual function
C<has_n> for the composing type C<Array>.

=head2 multiplicity (Array)

        multiplicity::Array : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Array, \$Any),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_multiplicity()),
        )),

The function C<multiplicity::Array> results in the integral count
of members of its C<0> argument such that each member value is equal to its
C<1> argument.  This function implements the C<Homogeneous> virtual
function C<multiplicity> for the composing type C<Array>.

=head2 all_unique (Array)

        all_unique::Array : (\Function : (
            returns : \$Boolean,
            matches : (\$Array,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_all_unique()),
        )),

The function C<all_unique::Array> results in C<True> iff its C<0> argument
has no 2 members that are the same value, and C<False> otherwise.  This
function implements the C<Homogeneous> virtual function C<all_unique> for
the composing type C<Array>.

=head2 unique (Array)

        unique::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_unique()),
        )),

The function C<unique::Array> results in the C<Array> value that has, for
every distinct member value I<V> of the function's C<0> argument, exactly 1
member whose value is equal to I<V>.  Each retained member is the one
closest to the start of the argument out of those members sharing the
retained member's value.  This function implements the C<Homogeneous>
virtual function C<unique> for the composing type C<Array>.

=head2 any (Array)

        any::Array : (\Function : (
            returns : \$Boolean,
            matches : (\$Array, \$Signature),
            implements : \$folder::'',
            evaluates : (foundation::Array_any(args:.\0, Signature_to_Function_Call_But_0::(args:.\1))),
        )),

I<TODO.>

=head2 insert_n (Array)

        insert_n::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$Any, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_insert_n()),
        )),

The function C<insert_n::Array> results in the C<Array> value that has all
of the members of the function's C<0> argument plus N additional members
that are each equal to its C<1> argument, where N is defined by its C<2>
argument.  The result starts with all of the members of C<0> in the same
order and ends with any added instances of C<1>.  This function implements
the C<Unionable> virtual function C<insert_n> for the composing type
C<Array>.

=head2 remove_n (Array)

        remove_n::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$Any, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_remove_n()),
        )),

The function C<remove_n::Array> results in the C<Array> value that has all
of the members of the function's C<0> argument minus N existing members
that are each equal to its C<1> argument, where N is defined as the lesser
of its C<2> argument and the count of members of C<0> equal to the C<1>
argument.  The removed instances of C<1> are those closest to the end of
C<0>.  This function implements the C<Unionable> virtual function
C<remove_n> for the composing type C<Array>.

=head2 except (Array)

        except::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$Array),
            implements : \$folder::'',
            right_identity : [],
            evaluates : (evaluates args --> \foundation::Array_except()),
        )),

The function C<except::Array> results in the I<multiset difference> between
its 2 arguments C<0> (I<minuend>) and C<1> (I<subtrahend>).  The result is
the C<Array> value that has all of the members of the function's C<0>
argument minus all of the matching members of its C<1> argument.  The
removed instances of any distinct member value are those closest to the end
of C<0>.  This function implements the C<Unionable> virtual function
C<except> aka C<∖> for the composing type C<Array>.

=head2 intersect (Array)

        intersect::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$Array),
            implements : \$folder::'',
            is_associative : True,
            is_idempotent : True,
            evaluates : (evaluates args --> \foundation::Array_intersect()),
        )),

The function C<intersect::Array> results in the I<multiset intersection> of
its 2 arguments C<0> and C<1>.  The result is the C<Array> value that has
all of the members of the function's C<0> argument that match their own
members of its C<1> argument.  The removed instances of any distinct member
value are those closest to the end of C<0>.  This operation conceptually
has a I<two-sided identity element> value of a collection with an infinite number of members.
This function implements the C<Unionable> virtual function C<intersect>
aka C<∩> for the composing type C<Array>.

=head2 union (Array)

        union::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$Array),
            implements : \$folder::'',
            is_idempotent : True,
            identity : [],
            evaluates : (evaluates args --> \foundation::Array_union()),
        )),

The function C<union::Array> results in the I<multiset union> of its 2
arguments C<0> and C<1>.  The result is the C<Array> value that has all of
the members of the function's C<0> argument plus all of the nonmatching
members of its C<1> argument.  The result starts with all of the members of
C<0> and ends with the nonmatching members of C<1>, the members from both
in the same order as in their respective arguments; the removed (due to
matching) instances of any distinct member value are those closest to the
end of C<1>.  This function implements the C<Unionable> virtual function
C<union> aka C<∪> for the composing type C<Array>.

=head2 exclusive (Array)

        exclusive::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$Array),
            implements : \$folder::'',
            identity : [],
            evaluates : (evaluates args --> \foundation::Array_exclusive()),
        )),

The function C<exclusive::Array> results in the I<multiset symmetric
difference> of its 2 arguments C<0> and C<1>.  The result is the C<Array>
value that has all of the members of each of the function's C<0> and C<1>
arguments that do not have matching members of their counterpart argument.
The result starts with the nonmatching members of C<0> and ends with the
nonmatching members of C<1>, the members from both in the same order as in
their respective arguments; the removed (due to matching) instances of any
distinct member value are those closest to the end of C<0> or C<1>
respectively.  This function implements the C<Unionable> virtual function
C<exclusive> aka C<symm_diff> aka C<∆> for the composing type C<Array>.

=head2 nest (Array)

        nest::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array,),
            implements : \$folder::'',
            accepts : (...),
            evaluates : (evaluates args --> \foundation::Array_nest()),
        )),

I<TODO.>

=head2 unnest (Array)

        unnest::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array,),
            implements : \$folder::'',
            accepts : (...),
            evaluates : (evaluates args --> \foundation::Array_unnest()),
        )),

I<TODO.>

=head2 where (Array)

        where::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$Signature),
            implements : \$folder::'',
            evaluates : (foundation::Array_where(args:.\0, Signature_to_Function_Call_But_0::(args:.\1))),
        )),

I<TODO.>

=head2 map (Array)

        map::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$Function_Call_But_0),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_map()),
        )),

I<TODO.>

=head2 reduce (Array)

        reduce::Array : (\Function : (
            returns : \$Any,
            matches : (\$Array, \$Function_Call_But_0_1),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_reduce()),
        )),

I<TODO.>

=head2 to_Set (Array)

        to_Set::Array : (\Function : (
            returns : \$Set,
            matches : (\$Array,),
            implements : \$folder::'',
            evaluates : (to_Set::(to_Bag args:.\0)),
        )),

The function C<to_Set::Array> results in the C<Set> value that has, for
every distinct member value I<V> of the function's C<0> argument, exactly 1
member whose value is equal to I<V>.  This function implements the
C<Discrete> virtual function C<to_Set> aka C<?|> for the composing type
C<Array>.

=head2 to_Bag (Array)

        to_Bag::Array : (\Function : (
            returns : \$Bag,
            matches : (\$Array,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_to_Bag()),
        )),

The function C<to_Bag::Array> results in the C<Bag> value that has all of
the members of the function's C<0> argument.  This function implements the
C<Discrete> virtual function C<to_Bag> aka C<+|> for the composing type
C<Array>.

=head2 count (Array)

        count::Array : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Array,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_count()),
        )),

The function C<count::Array> results in the integral count of the members
of its C<0> argument.  This function implements the C<Discrete> virtual
function C<count> aka C<cardinality> aka C<#> for the composing type
C<Array>.

=head2 order_using (Array)

        order_using::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$Function_Call_But_0_1),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Array_order_using()),
        )),

The function C<order_using::Array> results in the C<Array> value that
represents the same multiset of members as its C<0> argument but that the
members are arranged into a sequence in accordance with a total order
defined by the function given in its C<1> argument.  This function
implements the C<Discrete> virtual function C<order_using> for the composing
type C<Array>.

=head2 at (Array)

        at::Array : (\Function : (
            returns : \$Any,
            matches : (\$Array, \$Integer_NN),
            implements : \$folder::'',
            accepts : (args:.\0 .? args:.\1),
            evaluates : (evaluates args --> \foundation::Array_at()),
        )),

The function C<at::Array> results in the member value of its C<0> argument
whose ordinal position is equal to its C<1> argument.  This function will
fail if the C<0> argument doesn't have such a member.  This function
implements the C<Accessible> virtual function C<at> aka C<.> for the
composing type C<Array>.

=head2 to_Array (Array)

        to_Array::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array,),
            implements : \$folder::'',
            evaluates : (args:.\0),
        )),

The function C<to_Array::Array> simply results in its C<0> argument.  This
function implements the C<Positional> virtual function C<to_Array> aka
C<~|> for the composing type C<Array>.

=head2 first_possible_ord_pos (Array)

        first_possible_ord_pos::Array : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Array,),
            implements : \$folder::'',
            evaluates : (0),
        )),

The function C<first_possible_ord_pos::Array> simply results in C<0>.  This
function implements the C<Positional> virtual function
C<first_possible_ord_pos> for the composing type C<Array>.

=head2 slice_n (Array)

        slice_n::Array : (\Function : (
            returns : \$Array,
            matches : (\$Array, \$NN_Integer, \$NN_Integer),
            implements : \$folder::'',
            accepts : (args:.\1 + args:.\2 <= #args:.\0),
            evaluates : (evaluates args --> \foundation::Array_slice_n()),
        )),

The function C<slice_n::Array> results in the C<Array> value that has
exactly I<N> consecutive members of the function's C<0> argument I<C>, in
the same order, such that the first member existed at ordinal position I<F>
of I<C> where I<F> and I<N> are equal to the function's C<1> and C<2>
arguments respectively.  The taken members do not necessarily occupy the
same ordinal positions in the result as they did in I<C>.  This function will
fail if I<C> does not have members at any of the ordinal positions requested.
This function implements the C<Positional> virtual function C<slice_n> for
the composing type C<Array>.

=head2 ord_pos_succ_all_matches (Array)

        ord_pos_succ_all_matches::Array : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Array, \$Array),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (evaluates args --> \foundation::Array_ord_pos_succ_all_matches()),
        )),

I<TODO.  While conceivably implementable at a higher level, make low level
for perceived efficiency.>

=head1 SETTY DATA TYPES

=head2 Setty

        Setty : (\Function : (
            is_type_definer : True,
            is_generalization : True,
        )),

The semantic type definer C<Setty> is semifinite.  A C<Setty> value is a
I<collective> value such that every one of its component I<members> is a
distinct value.  The default value of C<Setty> is the C<Set> value with
zero members, C<{}>.  C<Setty> is composed, directly or indirectly, by:
C<Set>, C<Relation>, C<Interval>, C<Interval_Set>.

=head1 SET DATA TYPES

=head2 Set

        Set : (\Function : (
            is_type_definer : True,
            composes : {\$Discrete, \$Setty},
            provides_default_for : {\$Setty},
            evaluates : (\$Signature::Article_Match : (
                label : \Set,
                attrs : (
                    members : [\Bag::(), \all_unique::()],
                ),
            )),
            default : {},
        )),

The selection type definer C<Set> is infinite.  A C<Set> value is a general
purpose arbitrarily-large unordered collection of any other, I<member>
values, which explicitly does not represent any kind of thing in
particular, and is simply the sum of its members.  A C<Set> ensures that no
2 of its members are the same value.  The default value of C<Set> is C<{}>,
the only zero-member C<Set> value.

=head2 Set_C0 ?∅

        Set_C0 : (\Function : (
            is_type_definer : True,
            constant : {},
        )),

        Unicode_Aliases::'?∅' : (\Alias : ( of : \$Set_C0, )),

The singleton type definer C<Set_C0> aka C<?∅> represents the only zero-member
C<Set> value, C<{}>.

=head2 to_Boolean (Set)

        to_Boolean::Set : (\Function : (
            returns : \$Boolean,
            matches : (\$Set,),
            implements : \$folder::'',
            evaluates : (args:.\0 != {}),
        )),

The function C<to_Boolean::Set> results in C<True> iff its C<0> argument
has any members, and in C<False> iff it has no members.  This function
implements the C<Boolable> virtual function C<to_Boolean> aka C<so> aka
C<?> for the composing type C<Set>.

=head2 empty (Set)

        empty::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set,),
            implements : \$folder::'',
            evaluates : ({}),
        )),

The function C<empty::Set> results in the only zero-member C<Set>
value.  This function implements the C<Emptyable> virtual function C<empty>
aka C<∅> for the composing type C<Set>.

=head2 singular (Set)

        singular::Set : (\Function : (
            returns : \$Boolean,
            matches : (\$Set,),
            implements : \$folder::'',
            evaluates : (singular args:.\0:>.\members),
        )),

The function C<singular::Set> results in C<True> iff its C<0> argument has
exactly 1 member value, and C<False> otherwise.  This function implements
the C<Homogeneous> virtual function C<singular> for the composing type
C<Set>.

=head2 only_member (Set)

        only_member::Set : (\Function : (
            returns : \$Any,
            matches : (\$Set,),
            implements : \$folder::'',
            accepts : (singular args:.\0),
            evaluates : (only_member args:.\0:>.\members),
        )),

The function C<only_member::Set> results in its C<0> argument's only member
value.  This function will fail if the argument doesn't have exactly 1
member value.  This function implements the C<Homogeneous> virtual function
C<only_member> for the composing type C<Set>.

=head2 has_n (Set)

        has_n::Set : (\Function : (
            returns : \$Boolean,
            matches : (\$Set, \$Any, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (has_n::(args:.\0:>.\members, args:.\1, args:.\2)),
        )),

The function C<has_n::Set> results in C<True> iff either its C<2> argument
is zero or its C<0> argument has a member equal to its C<1> argument and
its C<2> argument is 1; otherwise it results in C<False>.  This function
implements the C<Homogeneous> virtual function C<has_n> for the composing
type C<Set>.

=head2 multiplicity (Set)

        multiplicity::Set : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Set, \$Any),
            implements : \$folder::'',
            evaluates : (args:.\0:>.\members multiplicity args:.\1),
        )),

The function C<multiplicity::Set> results in 1 if its C<0> argument
has a member equal to its C<1> argument; otherwise it results in 0.  This
function implements the C<Homogeneous> virtual function C<multiplicity> for
the composing type C<Set>.

=head2 all_unique (Set)

        all_unique::Set : (\Function : (
            returns : \$Boolean,
            matches : (\$Set,),
            implements : \$folder::'',
            evaluates : (True),
        )),

The function C<all_unique::Set> simply results in C<True>.  This function
implements the C<Homogeneous> virtual function C<all_unique> for the
composing type C<Set>.

=head2 unique (Set)

        unique::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set,),
            implements : \$folder::'',
            evaluates : (args:.\0),
        )),

The function C<unique::Set> simply results in its C<0> argument.  This
function implements the C<Homogeneous> virtual function C<unique> for the
composing type C<Set>.

=head2 subset_of (Set)

        subset_of::Set : (\Function : (
            returns : \$Boolean,
            matches : (\$Set, \$Set),
            implements : \$folder::'',
            evaluates : (args:.\0:>.\members subset_of args:.\1:>.\members),
        )),

The function C<subset_of::Set> results in C<True> iff the set of members of
its C<0> argument is a subset of the set of members of its C<1> argument;
otherwise it results in C<False>.  This function implements the
C<Homogeneous> virtual function C<subset_of> aka C<⊆> for the composing
type C<Set>.

=head2 same_members (Set)

        same_members::Set : (\Function : (
            returns : \$Boolean,
            matches : (\$Set, \$Set),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (args:.\0 = args:.\1),
        )),

The function C<same_members::Set> results in C<True> iff its 2 arguments
C<0> and C<1> are exactly the same C<Set> value, and C<False> otherwise;
its behaviour is identical to that of the function C<same> when given the
same arguments.  This function implements the C<Homogeneous> virtual
function C<same_members> for the composing type C<Set>.

=head2 overlaps_members (Set)

        overlaps_members::Set : (\Function : (
            returns : \$Boolean,
            matches : (\$Set, \$Set),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (args:.\0:>.\members overlaps_members args:.\1:>.\members),
        )),

The function C<overlaps_members::Set> results in C<True> iff, given
I<X> as the set of members of its argument C<0> and I<Y> as the
set of members of its argument C<1>, there exists at least 1 member
that both I<X> and I<Y> have, and there also exists at least 1 other member
each of I<X> and I<Y> that the other doesn't have; otherwise it results in
C<False>.  This function implements the C<Homogeneous> virtual function
C<overlaps_members> for the composing type C<Set>.

=head2 disjoint_members (Set)

        disjoint_members::Set : (\Function : (
            returns : \$Boolean,
            matches : (\$Set, \$Set),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (args:.\0:>.\members disjoint_members args:.\1:>.\members),
        )),

The function C<disjoint_members::Set> results in C<True> iff the
set of members of its C<0> argument has no member values in common
with the set of members of its C<1> argument; otherwise it results in
C<False>.  This function implements the C<Homogeneous> virtual function
C<disjoint_members> for the composing type C<Set>.

=head2 any (Set)

        any::Set : (\Function : (
            returns : \$Boolean,
            matches : (\$Set, \$Signature),
            implements : \$folder::'',
            evaluates : (args:.\0:>.\members any args:.\1),
        )),

I<TODO.>

=head2 insert_n (Set)

        insert_n::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set, \$Any, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (if args:.\0 has args:.\1 or args:.\2 = 0 then args:.\0
                else (\Set : (members : insert_n::(args:.\0:>.\members, args:.\1, 1),))),
        )),

The function C<insert_n::Set> results in the C<Set> value that has all of
the members of the function's C<0> argument; but, if the C<2> argument is
nonzero then the result has a member equal to the C<1> argument, whether or
not C<0> had it.  This function implements the C<Unionable> virtual
function C<insert_n> for the composing type C<Set>.

=head2 remove_n (Set)

        remove_n::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set, \$Any, \$Integer_NN),
            implements : \$folder::'',
            evaluates : ((\Set : (members : remove_n::(args:.\0:>.\members, args:.\1, args:.\2),))),
        )),

The function C<remove_n::Set> results in the C<Set> value that has all of
the members of the function's C<0> argument; but, if the C<2> argument is
nonzero and C<0> had a member equal to the C<1> argument, the result lacks
that member.  This function implements the C<Unionable> virtual function
C<remove_n> for the composing type C<Set>.

=head2 member_plus (Set)

        member_plus::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set, \$Set),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            identity : {},
            evaluates : (args:.\0 union args:.\1),
        )),

The function C<member_plus::Set> results in the I<set union> of its 2
arguments C<0> and C<1>; it behaves identically to C<union> aka C<∪> when
given the same arguments.  This function implements the C<Unionable>
virtual function C<member_plus> aka C<⊎> for the composing type C<Set>.

=head2 except (Set)

        except::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set, \$Set),
            implements : \$folder::'',
            right_identity : {},
            evaluates : ((\Set : (members : args:.\0:>.\members except args:.\1:>.\members,))),
        )),

The function C<except::Set> results in the I<set difference> between
its 2 arguments C<0> (I<minuend>) and C<1> (I<subtrahend>).  The result is
the C<Set> value that has all of the members of the function's C<0>
argument minus all of the matching members of its C<1> argument.  This
function implements the C<Unionable> virtual function C<except> aka C<∖>
for the composing type C<Set>.

=head2 intersect (Set)

        intersect::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set, \$Set),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            evaluates : ((\Set : (members : args:.\0:>.\members intersect args:.\1:>.\members,))),
        )),

The function C<intersect::Set> results in the I<set intersection> of
its 2 arguments C<0> and C<1>.  The result is the C<Set> value that has all
of the members of the function's C<0> argument that match their own members
of its C<1> argument.  This function implements the C<Unionable> virtual
function C<intersect> aka C<∩> for the composing type C<Set>.

=head2 union (Set)

        union::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set, \$Set),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            identity : {},
            evaluates : ((\Set : (members : args:.\0:>.\members union args:.\1:>.\members,))),
        )),

The function C<union::Set> results in the I<set union> of its 2
arguments C<0> and C<1>.  The result is the C<Set> value that has all of
the members of the function's C<0> argument plus all of the nonmatching
members of its C<1> argument.  This function implements the C<Unionable>
virtual function C<union> aka C<∪> for the composing type C<Set>.

=head2 exclusive (Set)

        exclusive::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set, \$Set),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            identity : {},
            evaluates : ((\Set : (members : args:.\0:>.\members exclusive args:.\1:>.\members,))),
        )),

The function C<exclusive::Set> results in the I<set symmetric
difference> of its 2 arguments C<0> and C<1>.  The result is the C<Set>
value that has all of the members of each of the function's C<0> and C<1>
arguments that do not have matching members of their counterpart argument.
This function implements the C<Unionable> virtual function C<exclusive>
aka C<symm_diff> aka C<∆> for the composing type C<Set>.

=head2 nest (Set)

        nest::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set,),
            implements : \$folder::'',
            accepts : (...),
            evaluates : ((\Set : (members : nest args:.\0:>.\members,))),
        )),

I<TODO.>

=head2 unnest (Set)

        unnest::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set,),
            implements : \$folder::'',
            accepts : (...),
            evaluates : ((\Set : (members : unnest args:.\0:>.\members,))),
        )),

I<TODO.>

=head2 where (Set)

        where::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set, \$Signature),
            implements : \$folder::'',
            evaluates : ((\Set : (members : args:.\0:>.\members where args:.\1,))),
        )),

I<TODO.>

=head2 map (Set)

        map::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set, \$Function_Call_But_0),
            implements : \$folder::'',
            evaluates : ((\Set : (members : args:.\0:>.\members map args:.\1,))),
        )),

I<TODO.>

=head2 reduce (Set)

        reduce::Set : (\Function : (
            returns : \$Any,
            matches : (\$Set, \$Function_Call_But_0_1),
            implements : \$folder::'',
            evaluates : ((\Set : (members : args:.\0:>.\members reduce args:.\1,))),
        )),

I<TODO.>

=head2 to_Set (Set)

        to_Set::Set : (\Function : (
            returns : \$Set,
            matches : (\$Set,),
            implements : \$folder::'',
            evaluates : (args:.\0),
        )),

The function C<to_Set::Set> simply results in its C<0> argument.  This
function implements the C<Discrete> virtual function C<to_Set> aka
C<?|> for the composing type C<Set>.

=head2 to_Bag (Set)

        to_Bag::Set : (\Function : (
            returns : \$Bag,
            matches : (\$Set,),
            implements : \$folder::'',
            evaluates : (args:.\0:>.\members),
        )),

The function C<to_Bag::Set> results in the C<Bag> value that has all of the
members of the function's C<0> argument.  This function implements the
C<Discrete> virtual function C<to_Bag> aka C<+|> for the composing type
C<Set>.

=head2 count (Set)

        count::Set : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Set,),
            implements : \$folder::'',
            evaluates : (count args:.\0:>.\members),
        )),

The function C<count::Set> results in the integral count of the members
of its C<0> argument.  This function implements the C<Discrete> virtual
function C<count> aka C<cardinality> aka C<#> for the composing type C<Set>.

=head2 unique_count (Set)

        unique_count::Set : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Set,),
            implements : \$folder::'',
            evaluates : (count args:.\0),
        )),

The function C<unique_count::Set> results in the integral count of the
members of its C<0> argument; its behaviour is identical to C<count> given
the same argument.  This function implements the C<Discrete> virtual
function C<unique_count> for the composing type C<Set>.

=head2 order_using (Set)

        order_using::Set : (\Function : (
            returns : \$Array,
            matches : (\$Set, \$Function_Call_But_0_1),
            implements : \$folder::'',
            evaluates : (args:.\0:>.\members order_using args:.\1),
        )),

The function C<order_using::Set> results in the C<Array> value that represents
the same set of members as its C<0> argument but that the members are
arranged into a sequence in accordance with a total order defined by the
function given in its C<1> argument.  This function implements the
C<Discrete> virtual function C<order_using> for the composing type C<Set>.

=head1 BAG DATA TYPES

=head2 Bag

        Bag : (\Function : (
            is_type_definer : True,
            composes : {\$Discrete},
            evaluates : \foundation::Bag(),
            default : {0:0},
        )),

The selection type definer C<Bag> represents the infinite Muldis D Foundation
type C<foundation::Bag>.  A C<Bag> value is a general purpose arbitrarily-large
unordered collection of any other, I<member> values, which explicitly does
not represent any kind of thing in particular, and is simply the sum of its
members.  A C<Bag> in the general case may have multiple members that are
the same value.  The default value of C<Bag> is C<{0:0}>, the only
zero-member C<Bag> value.  Other programming languages may name their
corresponding types I<Multiset>.

=head2 Bag_C0 +∅

        Bag_C0 : (\Function : (
            is_type_definer : True,
            constant : {0:0},
        )),

        Unicode_Aliases::'+∅' : (\Alias : ( of : \$Bag_C0, )),

The singleton type definer C<Bag_C0> aka C<+∅> represents the only zero-member
C<Bag> value, C<{0:0}>.

=head2 to_Boolean (Bag)

        to_Boolean::Bag : (\Function : (
            returns : \$Boolean,
            matches : (\$Bag,),
            implements : \$folder::'',
            evaluates : (args:.\0 != {0:0}),
        )),

The function C<to_Boolean::Bag> results in C<True> iff its C<0> argument
has any members, and in C<False> iff it has no members.  This function
implements the C<Boolable> virtual function C<to_Boolean> aka C<so> aka
C<?> for the composing type C<Bag>.

=head2 empty (Bag)

        empty::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag,),
            implements : \$folder::'',
            evaluates : ({0:0}),
        )),

The function C<empty::Bag> results in the only zero-member C<Bag>
value.  This function implements the C<Emptyable> virtual function C<empty>
aka C<∅> for the composing type C<Bag>.

=head2 singular (Bag)

        singular::Bag : (\Function : (
            returns : \$Boolean,
            matches : (\$Bag,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_singular()),
        )),

The function C<singular::Bag> results in C<True> iff its C<0> argument has
exactly 1 distinct member value, and C<False> otherwise.  This function
implements the C<Homogeneous> virtual function C<singular> for the
composing type C<Bag>.

=head2 only_member (Bag)

        only_member::Bag : (\Function : (
            returns : \$Any,
            matches : (\$Bag,),
            implements : \$folder::'',
            accepts : (singular args:.\0),
            evaluates : (evaluates args --> \foundation::Bag_only_member()),
        )),

The function C<only_member::Bag> results in its C<0> argument's only
distinct member value.  This function will fail if the argument doesn't
have exactly 1 distinct member value.  This function implements the
C<Homogeneous> virtual function C<only_member> for the composing type
C<Bag>.

=head2 has_n (Bag)

        has_n::Bag : (\Function : (
            returns : \$Boolean,
            matches : (\$Bag, \$Any, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_has_n()),
        )),

The function C<has_n::Bag> results in C<True> iff its C<0> argument has at
least N members such that each is equal to its C<1> argument, where N is
defined by its C<2> argument; otherwise it results in C<False>.  This
function implements the C<Homogeneous> virtual function C<has_n> for the
composing type C<Bag>.

=head2 multiplicity (Bag)

        multiplicity::Bag : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Bag, \$Any),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_multiplicity()),
        )),

The function C<multiplicity::Bag> results in the integral count
of members of its C<0> argument such that each member value is equal to its
C<1> argument.  This function implements the C<Homogeneous> virtual
function C<multiplicity> for the composing type C<Bag>.

=head2 all_unique (Bag)

        all_unique::Bag : (\Function : (
            returns : \$Boolean,
            matches : (\$Bag,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_all_unique()),
        )),

The function C<all_unique::Bag> results in C<True> iff its C<0> argument
has no 2 members that are the same value, and C<False> otherwise.  This
function implements the C<Homogeneous> virtual function C<all_unique> for
the composing type C<Bag>.

=head2 unique (Bag)

        unique::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_unique()),
        )),

The function C<unique::Bag> results in the C<Bag> value that has, for every
distinct member value I<V> of the function's C<0> argument, exactly 1
member whose value is equal to I<V>.  This function implements the
C<Homogeneous> virtual function C<unique> for the composing type C<Bag>.

=head2 subset_of (Bag)

        subset_of::Bag : (\Function : (
            returns : \$Boolean,
            matches : (\$Bag, \$Bag),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_subset_of()),
        )),

The function C<subset_of::Bag> results in C<True> iff the multiset of
members of its C<0> argument is a subset of the multiset of members of its
C<1> argument; otherwise it results in C<False>.  This function implements
the C<Homogeneous> virtual function C<subset_of> aka C<⊆> for the composing
type C<Bag>.

=head2 same_members (Bag)

        same_members::Bag : (\Function : (
            returns : \$Boolean,
            matches : (\$Bag, \$Bag),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (args:.\0 = args:.\1),
        )),

The function C<same_members::Bag> results in C<True> iff its 2 arguments
C<0> and C<1> are exactly the same C<Bag> value, and C<False> otherwise;
its behaviour is identical to that of the function C<same> when given the
same arguments.  This function implements the C<Homogeneous> virtual
function C<same_members> for the composing type C<Bag>.

=head2 overlaps_members (Bag)

        overlaps_members::Bag : (\Function : (
            returns : \$Boolean,
            matches : (\$Bag, \$Bag),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (evaluates args --> \foundation::Bag_overlaps_members()),
        )),

The function C<overlaps_members::Bag> results in C<True> iff, given
I<X> as the multiset of members of its argument C<0> and I<Y> as the
multiset of members of its argument C<1>, there exists at least 1 member
that both I<X> and I<Y> have, and there also exists at least 1 other member
each of I<X> and I<Y> that the other doesn't have; otherwise it results in
C<False>.  This function implements the C<Homogeneous> virtual function
C<overlaps_members> for the composing type C<Bag>.

=head2 disjoint_members (Bag)

        disjoint_members::Bag : (\Function : (
            returns : \$Boolean,
            matches : (\$Bag, \$Bag),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (evaluates args --> \foundation::Bag_disjoint_members()),
        )),

The function C<disjoint_members::Bag> results in C<True> iff the
multiset of members of its C<0> argument has no member values in common
with the multiset of members of its C<1> argument; otherwise it results in
C<False>.  This function implements the C<Homogeneous> virtual function
C<disjoint_members> for the composing type C<Bag>.

=head2 any (Bag)

        any::Bag : (\Function : (
            returns : \$Boolean,
            matches : (\$Bag, \$Signature),
            implements : \$folder::'',
            evaluates : (foundation::Bag_any(args:.\0, Signature_to_Function_Call_But_0::(args:.\1))),
        )),

I<TODO.>

=head2 insert_n (Bag)

        insert_n::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag, \$Any, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_insert_n()),
        )),

The function C<insert_n::Bag> results in the C<Bag> value that has all of
the members of the function's C<0> argument plus N additional members that
are each equal to its C<1> argument, where N is defined by its C<2>
argument.  This function implements the C<Unionable> virtual function
C<insert_n> for the composing type C<Bag>.

=head2 remove_n (Bag)

        remove_n::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag, \$Any, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_remove_n()),
        )),

The function C<remove_n::Bag> results in the C<Bag> value that has all of
the members of the function's C<0> argument minus N existing members that
are each equal to its C<1> argument, where N is defined as the lesser of
its C<2> argument and the count of members of C<0> equal to the C<1>
argument.  This function implements the C<Unionable> virtual function
C<remove_n> for the composing type C<Bag>.

=head2 member_plus (Bag)

        member_plus::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag, \$Bag),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            identity : {0:0},
            evaluates : (evaluates args --> \foundation::Bag_member_plus()),
        )),

The function C<member_plus::Bag> results in the I<multiset sum> of its 2
arguments C<0> and C<1>.  The result is the C<Bag> value that has all of
the members of the function's C<0> argument plus all of the members of its
C<1> argument.  This function implements the C<Unionable> virtual
function C<member_plus> aka C<⊎> for the composing type C<Bag>.

=head2 except (Bag)

        except::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag, \$Bag),
            implements : \$folder::'',
            right_identity : {0:0},
            evaluates : (evaluates args --> \foundation::Bag_except()),
        )),

The function C<except::Bag> results in the I<multiset difference> between
its 2 arguments C<0> (I<minuend>) and C<1> (I<subtrahend>).  The result is
the C<Bag> value that has all of the members of the function's C<0>
argument minus all of the matching members of its C<1> argument.  This
function implements the C<Unionable> virtual function C<except> aka C<∖>
for the composing type C<Bag>.

=head2 intersect (Bag)

        intersect::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag, \$Bag),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            evaluates : (evaluates args --> \foundation::Bag_intersect()),
        )),

The function C<intersect::Bag> results in the I<multiset intersection> of
its 2 arguments C<0> and C<1>.  The result is the C<Bag> value that has all
of the members of the function's C<0> argument that match their own members
of its C<1> argument.  This function implements the C<Unionable> virtual
function C<intersect> aka C<∩> for the composing type C<Bag>.

=head2 union (Bag)

        union::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag, \$Bag),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            identity : {0:0},
            evaluates : (evaluates args --> \foundation::Bag_union()),
        )),

The function C<union::Bag> results in the I<multiset union> of its 2
arguments C<0> and C<1>.  The result is the C<Bag> value that has all of
the members of the function's C<0> argument plus all of the nonmatching
members of its C<1> argument.  This function implements the C<Unionable>
virtual function C<union> aka C<∪> for the composing type C<Bag>.

=head2 exclusive (Bag)

        exclusive::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag, \$Bag),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            identity : {0:0},
            evaluates : (evaluates args --> \foundation::Bag_exclusive()),
        )),

The function C<exclusive::Bag> results in the I<multiset symmetric
difference> of its 2 arguments C<0> and C<1>.  The result is the C<Bag>
value that has all of the members of each of the function's C<0> and C<1>
arguments that do not have matching members of their counterpart argument.
This function implements the C<Unionable> virtual function C<exclusive>
aka C<symm_diff> aka C<∆> for the composing type C<Bag>.

=head2 nest (Bag)

        nest::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag,),
            implements : \$folder::'',
            accepts : (...),
            evaluates : (evaluates args --> \foundation::Bag_nest()),
        )),

I<TODO.>

=head2 unnest (Bag)

        unnest::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag,),
            implements : \$folder::'',
            accepts : (...),
            evaluates : (evaluates args --> \foundation::Bag_unnest()),
        )),

I<TODO.>

=head2 where (Bag)

        where::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag, \$Signature),
            implements : \$folder::'',
            evaluates : (foundation::Bag_where(args:.\0, Signature_to_Function_Call_But_0::(args:.\1))),
        )),

I<TODO.>

=head2 map (Bag)

        map::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag, \$Function_Call_But_0),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_map()),
        )),

I<TODO.>

=head2 reduce (Bag)

        reduce::Bag : (\Function : (
            returns : \$Any,
            matches : (\$Bag, \$Function_Call_But_0_1),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_reduce()),
        )),

I<TODO.>

=head2 to_Set (Bag)

        to_Set::Bag : (\Function : (
            returns : \$Set,
            matches : (\$Bag,),
            implements : \$folder::'',
            evaluates : ((\Set : (members : unique args:.\0,))),
        )),

The function C<to_Set::Bag> results in the C<Set> value that has, for every
distinct member value I<V> of the function's C<0> argument, exactly 1
member whose value is equal to I<V>.  This function implements the
C<Discrete> virtual function C<to_Set> aka C<?|> for the composing type
C<Bag>.

=head2 to_Bag (Bag)

        to_Bag::Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Bag,),
            implements : \$folder::'',
            evaluates : (args:.\0),
        )),

The function C<to_Bag::Bag> simply results in its C<0> argument.  This
function implements the C<Discrete> virtual function C<to_Bag> aka
C<+|> for the composing type C<Bag>.

=head2 count (Bag)

        count::Bag : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Bag,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_count()),
        )),

The function C<count::Bag> results in the integral count of the members
of its C<0> argument.  This function implements the C<Discrete> virtual
function C<count> aka C<cardinality> aka C<#> for the composing type C<Bag>.

=head2 unique_count (Bag)

        unique_count::Bag : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Bag,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_unique_count()),
        )),

The function C<unique_count::Bag> results in the integral count of the
distinct member values of its C<0> argument.  This function implements the
C<Discrete> virtual function C<unique_count> for the composing type C<Bag>.

=head2 order_using (Bag)

        order_using::Bag : (\Function : (
            returns : \$Array,
            matches : (\$Bag, \$Function_Call_But_0_1),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Bag_order_using()),
        )),

The function C<order_using::Bag> results in the C<Array> value that represents
the same multiset of members as its C<0> argument but that the members are
arranged into a sequence in accordance with a total order defined by the
function given in its C<1> argument.  This function implements the
C<Discrete> virtual function C<order_using> for the composing type C<Bag>.

=head1 ATTRIBUTIVE DATA TYPES

=head2 Attributive

        Attributive : (\Function : (
            is_type_definer : True,
            is_generalization : True,
        )),

The interface type definer C<Attributive> is semifinite.  An C<Attributive> value
is a I<collective> value such that every one of its component I<attributes>
is conceptually of its own data type or should be interpreted logically in
a distinct way from its fellow attributes.  Idiomatically an C<Attributive>
is a generic collection which does not as a whole represent any kind of
thing in particular, such as a text or a graphic, and is simply the sum of
its I<attributes>; however some types which do represent such a particular
kind of thing may choose to compose C<Attributive> because it makes sense
to provide its operators.  The default value of C<Attributive> is the
C<Tuple> value with zero attributes, C<()>.

An C<Attributive> value either is a C<Tuple> or is isomorphic to a C<Tuple>
or is isomorphic to a C<Homogeneous> collection each of whose I<members> is
a C<Tuple>.  An C<Attributive> value is always I<discrete> in its
I<attributive> dimension such that all of its attributes can be enumerated
as individuals and counted.  However, an C<Attributive> value is not
necessarily I<discrete> in its I<homogeneous> dimension, depending on the
specific collection type in question.  If an C<Attributive> value is also
C<Discrete>, then it is I<discrete> in both dimensions.

Every attribute of an C<Attributive> value has a distinct I<attribute name>
by which it can be looked up.  An C<Attributive> value has a I<heading>
which is a set of distinct attribute names; corresponding to this, an
C<Attributive> value has a I<body> which has the zero or more other values
that can be looked up using the attribute names.  Each attribute then is
conceptually a name-asset pair.  An I<attribute name> is characterized by
an arbitrarily-long sequence of Unicode 12.1 standard I<character
code points> but it is not a C<Text> value; every distinct I<attribute name>
is represented canonically in the type system by a distinct
C<Attr_Name> value.  More precisely, an I<attribute name> is an unqualified
program identifier and forms the basis for Muldis D identifiers.

If an C<Attributive> value is also C<Structural>, it has exactly 1 member,
meaning the value is or is isomorphic to a C<Tuple>, meaning each attribute
name has exactly 1 corresponding attribute asset; otherwise, that may not
be the case.  If an C<Attributive> value is also C<Relational>, it
potentially has any number of members, zero or more, meaning the value is
isomorphic to a C<Homogeneous> collection each of whose I<members> is a
C<Tuple>; otherwise, that may not be the case.

C<Attributive> is composed, directly or indirectly, by: C<Structural>,
C<Tuple>, C<Relational>, C<Tuple_Array>, C<Relation>, C<Tuple_Bag>.

=head2 has_any_attrs ?$

        has_any_attrs::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Attributive,),
        )),

        '?$' : (\Alias : ( of : \$has_any_attrs, )),

The virtual function C<has_any_attrs> aka C<?$> results in C<True> iff its
C<0> argument has any attributes, and in C<False> iff it has no attributes.

=head2 is_nullary !?$

        is_nullary : (\Function : (
            negates : \$has_any_attrs,
        )),

        '!?$' : (\Alias : ( of : \$is_nullary, )),

The function C<is_nullary> aka C<!?$> results in C<True> iff its C<0>
argument has no attributes, and in C<False> iff it has any attributes.

=head2 nullary

        nullary::'' : (\Function : (
            virtual : True,
            returns : \$Attributive,
            matches : (\$Attributive,),
        )),

The virtual function C<nullary> results in the value of its C<0> argument's
collection type that has zero attributes.  For C<Structural> types like
C<Tuple>, this is a constant value, but for types like C<Tuple_Bag>, there
is a distinct result for each I<cardinality>; for types like C<Relation>
there are exactly 2 possible result values.

=head2 is_unary

        is_unary::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Attributive,),
        )),

The virtual function C<is_unary> results in C<True> iff its C<0> argument
has exactly 1 attribute, and C<False> otherwise.

=head2 degree #$

        degree::'' : (\Function : (
            virtual : True,
            returns : \$Integer_NN,
            matches : (\$Attributive,),
        )),

        '#$' : (\Alias : ( of : \$degree, )),

The virtual function C<degree> aka C<#$> results in the integral count of
the attributes of its C<0> argument.

=head2 heading $

        heading::'' : (\Function : (
            virtual : True,
            returns : \$Heading,
            matches : (\$Attributive,),
        )),

        '$' : (\Alias : ( of : \$heading, )),

The virtual function C<heading> aka C<$> results in the
relational I<heading> of its C<0> argument, that is its set of distinct
attribute names.  The form that this takes is a C<Tuple> having just the
same attribute names where every one of its assets is the value C<True>.
The purpose of C<heading> is to canonicalize C<Attributive> values such
that their headings can be reasoned about in isolation from their bodies.

=head2 subheading_of ⊆$

        subheading_of : (\Function : (
            returns : \$Boolean,
            matches : (\$Attributive, \$Attributive),
            evaluates : (foundation::Tuple_subheading_of($args:.\0, $args:.\1)),
        )),

        Unicode_Aliases::'⊆$' : (\Alias : ( of : \$subheading_of, )),

The function C<subheading_of> aka C<⊆$> results in C<True> iff the heading
of its C<0> argument is a subset of the heading of its C<1> argument;
otherwise it results in C<False>.

=head2 superheading_of has_subheading $? ⊇$

        superheading_of : (\Function : (
            commutes : \$subheading_of,
        )),

        has_subheading : (\Alias : ( of : \$superheading_of, )),
        '$?'           : (\Alias : ( of : \$superheading_of, )),

        Unicode_Aliases::'⊇$' : (\Alias : ( of : \$superheading_of, )),

The function C<superheading_of> aka C<has_subheading> aka C<$?> aka C<⊇$>
results in C<True> iff the heading of its C<0> argument is a superset of
the heading of its C<1> argument; otherwise it results in C<False>.  Note
that using this function with a C<Heading> as its C<1> argument is the
idiomatic way to test if an C<Attributive> has all of a specific set of
attribute names; whereas the C<.?> function is idiomatic for testing for
exactly 1 name.

=head2 same_heading =$

        same_heading : (\Function : (
            returns : \$Boolean,
            matches : (\$Attributive, \$Attributive),
            is_commutative : True,
            evaluates : ($args:.\0 = $args:.\1),
        )),

        '=$' : (\Alias : ( of : \$same_heading, )),

The function C<same_heading> aka C<=$> results in C<True> iff the heading
of its C<0> argument is the same as the heading of its C<1> argument;
otherwise it results in C<False>.

=head2 proper_subheading_or_superheading

        proper_subheading_or_superheading : (\Function : (
            returns : \$Boolean,
            matches : (\$Attributive, \$Attributive),
            is_commutative : True,
            evaluates : ($args:.\0 != $args:.\1 and (args:.\0 subheading_or_superheading args:.\1)),
        )),

The function C<proper_subheading_or_superheading> results in C<True> iff
the heading of one of its 2 arguments C<0> and C<1> is a proper subset of
the heading of its other argument; otherwise it results in C<False>.

=head2 subheading_or_superheading

        subheading_or_superheading : (\Function : (
            returns : \$Boolean,
            matches : (\$Attributive, \$Attributive),
            is_commutative : True,
            evaluates : ((args:.\0 subheading_of args:.\1) or (args:.\0 superheading_of args:.\1)),
        )),

The function C<subheading_or_superheading> results in C<True> iff the
heading of one of its 2 arguments C<0> and C<1> is a subset of the heading
of its other argument; otherwise it results in C<False>.

=head2 overlaps_heading

        overlaps_heading : (\Function : (
            returns : \$Boolean,
            matches : (\$Attributive, \$Attributive),
            is_commutative : True,
            evaluates : (foundation::Tuple_overlaps_heading($args:.\0, $args:.\1)),
        )),

The function C<overlaps_heading> results in C<True> iff, given I<X> as the
heading of its argument C<0> and I<Y> as the heading of its argument C<1>,
there exists at least 1 attribute name that both I<X> and I<Y> have, and
there also exists at least 1 other attribute name each of I<X> and I<Y>
that the other doesn't have; otherwise it results in C<False>.

=head2 disjoint_heading

        disjoint_heading : (\Function : (
            returns : \$Boolean,
            matches : (\$Attributive, \$Attributive),
            is_commutative : True,
            evaluates : (foundation::Tuple_disjoint_heading($args:.\0, $args:.\1)),
        )),

The function C<disjoint_heading> results in C<True> iff the heading of its
C<0> argument has no attribute names in common with the heading of its C<1>
argument; otherwise it results in C<False>.

=head2 except_heading ∖$

        except_heading : (\Function : (
            returns : \$Heading,
            matches : (\$Attributive, \$Attributive),
            evaluates : (foundation::Tuple_except_heading($args:.\0, $args:.\1)),
        )),

        Unicode_Aliases::'∖$' : (\Alias : ( of : \$except_heading, )),

The function C<except_heading> aka C<∖$> results in the I<set difference>
between the I<headings> of its 2 arguments C<0> (I<minuend>) and C<1>
(I<subtrahend>).  The result is the C<Heading> value that just has every
attribute name that exists in the heading of the function's C<0> argument
but not in the heading of its C<1> argument.

=head2 intersect_heading ∩$

        intersect_heading : (\Function : (
            returns : \$Heading,
            matches : (\$Attributive, \$Attributive),
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            evaluates : (foundation::Tuple_intersect_heading($args:.\0, $args:.\1)),
        )),

        Unicode_Aliases::'∩$' : (\Alias : ( of : \$intersect_heading, )),

The function C<intersect_heading> aka C<∩$> results in the I<set
intersection> of the I<headings> of its 2 arguments C<0> and C<1>.  The
result is the C<Heading> value that just has every attribute name that
exists in both of the headings of the function's C<0> and C<1> arguments.
This operation conceptually has a I<two-sided identity element> value of a C<Heading> with an
infinite number of attribute names.

=head2 union_heading ∪$

        union_heading : (\Function : (
            returns : \$Heading,
            matches : (\$Attributive, \$Attributive),
            is_associative : True,
            is_commutative : True,
            is_idempotent : True,
            identity : (),
            evaluates : (foundation::Tuple_union_heading($args:.\0, $args:.\1)),
        )),

        Unicode_Aliases::'∪$' : (\Alias : ( of : \$union_heading, )),

The function C<union_heading> aka C<∪$> results in the I<set union> of the
I<headings> of its 2 arguments C<0> and C<1>.  The result is the C<Heading>
value that just has every attribute name that exists in either or both of
the headings of the function's C<0> and C<1> arguments.

=head2 exclusive_heading symm_diff_heading ∆$

        exclusive : (\Function : (
            returns : \$Heading,
            matches : (\$Attributive, \$Attributive),
            is_associative : True,
            is_commutative : True,
            identity : (),
            evaluates : (foundation::Tuple_exclusive_heading($args:.\0, $args:.\1)),
        )),

        symm_diff_heading : (\Alias : ( of : \$exclusive_heading, )),

        Unicode_Aliases::'∆$' : (\Alias : ( of : \$exclusive_heading, )),

The function C<exclusive_heading> aka C<symm_diff_heading> aka C<∆$>
results in the I<set symmetric difference> of the I<headings> of its 2
arguments C<0> and C<1>.  The result is the C<Heading> value that just has
every attribute name that exists in the heading of exactly one of the
function's C<0> or C<1> arguments.

=head2 rename $:= ρ

        rename::'' : (\Function : (
            virtual : True,
            returns : \$Attributive,
            matches : (\$Attributive, \$Renaming),
            accepts : (...),
        )),

        '$:=' : (\Alias : ( of : \$rename, )),

        Unicode_Aliases::'ρ' : (\Alias : ( of : \$rename, )),

The virtual function C<rename> aka C<$:=> aka C<ρ> results results in the
I<relational rename> of its C<0> argument in terms of its C<1> argument.
The result is a value of the function's C<0> argument's collection type
that has all of the attributes of the function's C<0> argument but that,
for each attribute of the C<0> argument whose attribute name matches a
I<before> element of its C<1> argument, the result's corresponding
attribute instead has an attribute name that matches the corresponding
I<after> element of the C<1> argument.  The I<degree> and the
I<cardinality> of the result are both equal to the degree and cardinality
of the C<0> argument, respectively.  This function will fail if any
I<before> element of the C<1> argument does not match the name of an
attribute name of the C<0> argument, or if any I<after> element of the
former does match an attribute name of the latter that isn't being renamed.

I<TODO: Revisit the spec_2011 docs of this operator after the Renaming type
is fully defined, for additional useful language for describing "rename".>

=head2 renaming

        renaming : (\Function : (
            commutes : \$rename,
        )),

The function C<renaming> behaves identically to the function C<rename> when
given the same arguments in swapped positions.

=head2 can_project_matching %=?

        can_project_matching::'' : (\Function : (
            virtual : True,
            returns : \$Boolean,
            matches : (\$Attributive, \$Structural),
        )),

        '%=?' : (\Alias : ( of : \$can_project_matching, )),

The virtual function C<can_project_matching> aka C<%=?> results in C<True>
iff the heading of its C<0> argument is a superset of the heading of its
C<1> argument and every commonly-named attribute of the two arguments also
has the same attribute asset; otherwise it results in C<False>.  Note that
by definition, the identity C<can_project_matching::(update::(a,s),s) = True>
aka C<a %:= s %=? s = True> should hold for all valid C<a> and C<s>.

=head2 on project %= π

        on::'' : (\Function : (
            virtual : True,
            returns : \$Attributive,
            matches : (\$Attributive, \$Heading),
            accepts : (args:.\0 $? args:.\1),
        )),

        project : (\Alias : ( of : \$on, )),
        '%='    : (\Alias : ( of : \$on, )),

        Unicode_Aliases::'π' : (\Alias : ( of : \$on, )),

The virtual function C<on> aka C<project> aka C<%=> aka C<π> results in the
I<relational projection> of its C<0> argument in terms of its C<1>
argument.  The result is a value of the function's C<0> argument's
collection type that has all of the attributes of the function's C<0>
argument whose names match the names of attributes of its C<1> argument.
The I<degree> of the result is equal to the degree of the C<1> argument,
and the I<cardinality> of the result is either equal to or less than the
cardinality of the C<0> argument; a lesser cardinality would be the result
of duplicate result member elimination for a I<setty> type.  For every
member tuple or isomorphism I<X> in the C<0> argument, the result has a
member tuple or isomorphism I<Y> that is equal to the result of projecting
I<X> on all of the attribute names of the C<1> argument.  This function
will fail if the I<heading> of the C<1> argument is not a subset of the
I<heading> of the C<0> argument.  Other programming languages may name
their corresponding operators I<over> or I<select>; it is also common to
use subscript/postcircumfix syntax.

=head2 from

        from : (\Function : (
            commutes : \$on,
        )),

The function C<from> behaves identically to the function C<on>
when given the same arguments in swapped positions.

=head2 maybe_on %!

        maybe_on : (\Function : (
            returns : \$Attributive,
            matches : (\$Attributive, \$Heading),
            evaluates : (args:.\0 on (args:.\0 intersect_heading args:.\1)),
        )),

        '%!' : (\Alias : ( of : \$maybe_on, )),

The function C<maybe_on> aka C<%!> behaves identically to C<on>
when given the same arguments but that it simply ignores the existence of
attributes of its C<1> argument whose names don't match attributes of its
C<0> argument rather than failing.

=head2 update %:=

        update::'' : (\Function : (
            virtual : True,
            returns : \$Attributive,
            matches : (\$Attributive, \$Structural),
            accepts : (args:.\0 $? args:.\1),
        )),

        '%:=' : (\Alias : ( of : \$update, )),

The virtual function C<update> aka C<%:=> results in the value of its C<0>
argument's collection type that has all of the attributes of the function's
C<0> argument but that, for each attribute of the C<0> argument whose
attribute name I<K> matches the name of an attribute of its C<1> argument,
the result takes its corresponding attribute from the C<1> argument rather
than from the C<0> argument.  The I<degree> of the result is equal to the
degree of the C<0> argument, and the I<cardinality> of the result is either
equal to or less than the cardinality of the C<0> argument; a lesser
cardinality would be the result of duplicate result member elimination for
a I<setty> type.  For every member tuple or isomorphism I<X> in the C<0>
argument, the result has a member tuple or isomorphism I<Y> that is equal
to I<X> but that, for each attribute name I<K> of I<X>, the asset of the
attribute named I<K> of I<Y> is equal to the asset of the attribute named
I<K> of the C<1> argument rather than its value from I<X>.  This function
will fail if the I<heading> of the C<1> argument is not a subset of the
I<heading> of the C<0> argument.  Other programming languages may name
their corresponding operators I<update ... set>; it is also common to use
subscript/postcircumfix syntax plus assignment syntax.

=head2 extend %+

        extend::'' : (\Function : (
            virtual : True,
            returns : \$Attributive,
            matches : (\$Attributive, \$Structural),
            accepts : (args:.\0 disjoint_heading args:.\1),
        )),

        '%+' : (\Alias : ( of : \$extend, )),

The virtual function C<extend> aka C<%+> results in the I<relational
extension> of its C<0> argument in terms of its C<1> argument.  The result
is a value of the function's C<0> argument's collection type that has all
of the attributes of the function's C<0> argument plus all of the
attributes of its C<1> argument.  The I<degree> of the result is equal to
the integral sum of the degrees of the 2 arguments, and the I<cardinality>
of the result is equal to the cardinality of the C<0> argument.  There is a
bijection between the C<0> argument and the result with respect to their
I<members>; for every tuple or isomorphism I<X> in the C<0> argument, the
result has a tuple or isomorphism I<Y> that is equal to the result of
extending I<X> with all of the attributes (names and assets) of the C<1>
argument.  This function will fail if the I<headings> of the 2 arguments
are not disjoint.  Other programming languages may name their corresponding
operators I<join>.

I<TODO.  There is no version of 'extend' that takes a routine; one can just
use the above version plus an eg-tuple selector. In the case of relation
extend, one can do the aforementioned within a 'map' routine, for the
routine version; they can use join/cross-product for the static version.
Also, this is analogous to set union (disjoint).
The Structural version is commutative and associative, but not Attributive in general.>

=head2 but project_all_but %-

        but : (\Function : (
            returns : \$Attributive,
            matches : (\$Attributive, \$Heading),
            accepts : (args:.\0 $? args:.\1),
            evaluates : (args:.\0 on (args:.\0 except_heading args:.\1)),
        )),

        project_all_but : (\Alias : ( of : \$but, )),
        '%-'            : (\Alias : ( of : \$but, )),

The function C<but> aka C<project_all_but> aka C<%-> results in the
I<relational projection> of its C<0> argument in terms of its C<1>
argument.  The result is a value of the function's C<0> argument's
collection type that has all of the attributes of the function's C<0>
argument whose names do not match the names of attributes of its C<1>
argument.  The I<degree> of the result is equal to the integral difference
of the degrees of the 2 arguments, and the I<cardinality> of the result is
either equal to or less than the cardinality of the C<0> argument; a lesser
cardinality would be the result of duplicate result member elimination for
a I<setty> type.  For every member tuple or isomorphism I<X> in the C<0>
argument, the result has a member tuple or isomorphism I<Y> that is equal
to the result of projecting I<X> on all of its attribute names except for
those in common with the C<1> argument.  This function will fail if the
I<heading> of the C<1> argument is not a subset of the I<heading> of the
C<0> argument.  Other programming languages may name their corresponding
operators I<remove>.

=head2 update_or_extend %=+

        update_or_extend : (\Function : (
            returns : \$Attributive,
            matches : (\$Attributive, \$Structural),
            evaluates : (args:.\0 on (args:.\0 except_heading args:.\1) extend args:.\1),
        )),

        '%=+' : (\Alias : ( of : \$update_or_extend, )),

The function C<update_or_extend> aka C<%=+> is a hybrid of the 2
functions C<update> and C<extend>.  The result is a value of the function's
C<0> argument's collection type that has attribute assets substituted with
those from the function's C<1> argument where the attribute names match,
and the result has new attributes added from the C<1> argument where the
latter's names don't match.

=head2 maybe_but %?-

        maybe_but : (\Function : (
            returns : \$Attributive,
            matches : (\$Attributive, \$Heading),
            evaluates : (args:.\0 but (args:.\0 intersect_heading args:.\1)),
        )),

        '%?-' : (\Alias : ( of : \$maybe_but, )),

The function C<maybe_but> aka C<%?-> behaves identically to C<but>
when given the same arguments but that it simply ignores the existence of
attributes of its C<1> argument whose names don't match attributes of its
C<0> argument rather than failing.

=head1 STRUCTURAL DATA TYPES

=head2 Structural

        Structural : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Attributive, \$Accessible},
            provides_default_for : {\$Attributive, \$Accessible},
        )),

The interface type definer C<Structural> is semifinite.  A C<Structural> value is
an C<Attributive> value that has exactly 1 member, meaning the value is or
is isomorphic to a C<Tuple>, meaning each attribute name has exactly 1
corresponding attribute asset.  The default value of C<Structural> is the
C<Tuple> value with zero attributes, C<()>.  C<Structural> is composed by
C<Tuple>.

=head2 can_project_matching (Structural)

        can_project_matching::Structural : (\Function : (
            returns : \$Boolean,
            matches : (\$Structural, \$Structural),
            implements : \$folder::'',
            evaluates : (args:.\0 $? args:.\1 and_then guard args:.\0 %= $args:.\1 = args:.\1),
        )),

The function C<can_project_matching::Structural> results in C<True> iff the
heading of its C<0> argument is a superset of the heading of its C<1>
argument and every commonly-named attribute of the two arguments also has
the same attribute asset; otherwise it results in C<False>.  Note that by
definition, the identity C<can_project_matching::(update::(a,s),s) = True>
aka C<a %:= s %=? s = True> should hold for all valid C<a> and C<s>.  This
function implements the C<Attributive> virtual function
C<can_project_matching> aka C<%=?> for the composing type C<Structural>.

=head2 has_any_at (Structure)

        has_any_at::Structure : (\Function : (
            returns : \$Boolean,
            matches : (\$Structure, \$Attr_Name),
            implements : \$folder::'',
            evaluates : (args:.\0 $? args:.\1),
        )),

The function C<has_any_at::Structure> results in C<True> iff its C<0>
argument has an attribute whose attribute name is equal to its C<1>
argument; otherwise it results in C<False>.  This function implements the
C<Accessible> virtual function C<has_any_at> aka C<.?> for the composing
type C<Structure>.

=head2 has_mapping_at (Structural)

        has_mapping_at::Structural : (\Function : (
            returns : \$Boolean,
            matches : (Structural, (Attr_Name, Any)),
            implements : \$folder::'',
            evaluates : (args:.\0 .? (args:.\1.\0) and_then guard args:.\0.(args:.\1.\0) = (args:.\1.\1)),
        )),

The function C<has_mapping_at::Structural> results in C<True> iff its C<0>
argument has an attribute whose name and asset are equal to the function's
C<1> argument's C<0> and C<1> attribute assets, respectively; otherwise it
results in C<False>.  This function implements the C<Accessible> virtual
function C<has_mapping_at> aka C<.:?> for the composing type C<Structural>.

=head2 mapping_at (Structural)

        mapping_at::Structural : (\Function : (
            returns : (\$Attr_Name, \$Any),
            matches : (\$Structural, \$Attr_Name),
            implements : \$folder::'',
            accepts : (args:.\0 .? args:.\1),
            evaluates : ((args:.\1, args:.\0.args:.\1)),
        )),

The function C<mapping_at::Structural> results in a binary C<Tuple> whose
C<0> attribute is the function's C<1> argument and whose C<1> attribute is
the attribute asset value of the function's C<0> argument whose attribute
name is equal to its C<1> argument.  This function will fail if the C<0>
argument doesn't have such an attribute.  This function implements the
C<Accessible> virtual function C<mapping_at> aka C<.:> for the composing
type C<Structural>.

=head2 maybe_at (Structural)

        maybe_at::Structural : (\Function : (
            returns : \$Any,
            matches : (\$Structural, \$Attr_Name),
            implements : \$folder::'',
            evaluates : (if args:.\0 .? args:.\1 then guard args:.\0.args:.\1 else \!No_Such_Attr_Name),
        )),

The function C<maybe_at::Structural> results in the attribute asset value
of its C<0> argument whose attribute name is equal to its C<1> argument,
iff there is such an attribute; otherwise it results in C<\!No_Such_Attr_Name>.  This
function implements the C<Accessible> virtual function C<maybe_at> aka
C<.!> for the composing type C<Structural>.

=head2 replace_at (Structure)

        replace_at::Structure : (\Function : (
            returns : \$Structure,
            matches : (Structure, (Attr_Name, Any)),
            implements : \$folder::'',
            accepts : (args:.\0 .? (args:.\1.\0)),
            evaluates : (args:.\0 update D1::(args:.\1)),
        )),

The function C<replace_at::Structure> results in the value of its C<0>
argument's collection type that has all of the attributes of the function's
C<0> argument but that, for the 1 attribute of the C<0> argument whose
attribute name is equal to the function's C<1> argument's C<0> attribute
asset, the result's attribute instead has an asset equal to the C<1>
argument's C<1> attribute asset.  This function will fail if the C<0>
argument doesn't have an attribute with that name.  This function
implements the C<Accessible> virtual function C<replace_at> aka C<.:=> for
the composing type C<Structure>.

=head2 shiftless_insert_at (Structure)

        shiftless_insert_at::Structure : (\Function : (
            returns : \$Structure,
            matches : (Structure, (Attr_Name, Any)),
            implements : \$folder::'',
            accepts : (not args:.\0 .? (args:.\1.\0)),
            evaluates : (args:.\0 extend D1::(args:.\1)),
        )),

The function C<shiftless_insert_at::Structure> results in the value of its
C<0> argument's collection type that has all of the attributes of the
function's C<0> argument plus 1 additional attribute whose name and asset
are equal to the function's C<1> argument's C<0> and C<1> attribute assets,
respectively.  This function will fail if the C<0> argument already has an
attribute with that name.  This function implements the C<Accessible>
virtual function C<shiftless_insert_at> aka C<.+> for the composing type
C<Structure>.

=head2 shiftless_remove_at (Structure)

        shiftless_remove_at::Structure : (\Function : (
            returns : \$Structure,
            matches : (\$Structure, \$Attr_Name),
            implements : \$folder::'',
            accepts : (args:.\0 .? args:.\1),
            evaluates : (args:.\0 but args:.\1),
        )),

The function C<shiftless_remove_at::Structure> results in the value of its
C<0> argument's collection type that has all of the attributes of the
function's C<0> argument minus 1 existing attribute whose name is equal to
its C<1> argument.  This function will fail if the C<0> argument doesn't
have such an attribute.  This function implements the C<Accessible> virtual
function C<shiftless_remove_at> aka C<.-> for the composing type
C<Structure>.

=head2 replace_or_insert_at (Structural)

        replace_or_insert_at::Structural : (\Function : (
            returns : \$Structural,
            matches : (Structural, (Attr_Name, Any)),
            implements : \$folder::'',
            evaluates : (args:.\0 update_or_extend D1::(args:.\1)),
        )),

The function C<replace_or_insert_at::Structural> behaves identically in
turn to each of the functions C<replace_at> and C<shiftless_insert_at> when
given the same arguments, where the C<0> argument does or doesn't,
respectively, have an attribute whose attribute name is equal to the C<1>
argument's C<0> attribute.  This function implements the C<Accessible>
virtual function C<replace_or_insert_at> aka C<.=+> for the composing type
C<Structural>.

=head2 shiftless_maybe_remove_at (Structural)

        shiftless_maybe_remove_at::Structural : (\Function : (
            returns : \$Structural,
            matches : (\$Structural, \$Attr_Name),
            implements : \$folder::'',
            evaluates : (args:.\0 maybe_but args:.\1),
        )),

The function C<shiftless_maybe_remove_at::Structural> behaves identically
to C<shiftless_remove_at> when given the same arguments but that it simply
results in its C<0> argument when that has no attribute whose attribute
name matches its C<1> argument, rather than fail.  This function implements
the C<Accessible> virtual function C<shiftless_maybe_remove_at> aka C<.?->
for the composing type C<Structural>.

=head2 to_Tuple %

        to_Tuple::'' : (\Function : (
            virtual : True,
            returns : \$Tuple,
            matches : (\$Structural,),
        )),

        '%' : (\Alias : ( of : \$to_Tuple, )),

The virtual function C<to_Tuple> aka C<%> results in the C<Tuple> value
that represents the same set of attributes as its C<0> argument.  The
purpose of C<to_Tuple> is to canonicalize C<Structural> values so they can
be treated abstractly as sets of attributes with minimal effort.

=head1 TUPLE DATA TYPES

=head2 Tuple

        Tuple::'' : (\Function : (
            is_type_definer : True,
            composes : {\$Structural},
            provides_default_for : {\$Structural},
            evaluates : \foundation::Tuple(),
            default : (),
        )),

The selection type definer C<Tuple> represents the infinite Muldis D Foundation
type C<foundation::Tuple>.  A C<Tuple> value is a general purpose
arbitrarily-large unordered heterogeneous collection of named
I<attributes>, such that no 2 attributes have the same I<attribute name>,
which explicitly does not represent any kind of thing in particular, and is
simply the sum of its attributes.  An attribute is conceptually a
name-asset pair, the name being used to look up the attribute in a
C<Tuple>.  An I<attribute name> is an unqualified program identifier,
characterized by an arbitrarily-long sequence of Unicode 12.1 standard
I<character code points> but it is not a
C<Text> value; it forms the basis for Muldis D identifiers.
In the general case each attribute of a tuple is of a distinct data type,
though multiple attributes often have the same type.  The set of attribute
names of a C<Tuple> is called its I<heading>, and the corresponding
attribute assets are called its I<body>.  With respect to the relational
model of data, a I<heading> represents a predicate, for which each
I<attribute name> is a free variable, and a C<Tuple> as a whole represents
a derived proposition, where the corresponding attribute asset values
substitute for the free variables; however, any actual predicate/etc is
defined by the context of a C<Tuple> value and a C<Tuple> in isolation
explicitly does not represent any proposition in particular.  The default
value of C<Tuple> is C<()>, the only zero-attribute C<Tuple> value.  Other
programming languages may name their corresponding types I<Capture> or
I<Stash> or I<record> or I<struct> or I<row> or I<DataRow> or I<Hash>.

=head2 Tuple_D0

        Tuple_D0 : (\Function : (
            is_type_definer : True,
            constant : (),
        )),

The singleton type definer C<Tuple_D0> represents the only zero-attribute
C<Tuple> value.

=head2 Tuple_D1

        Tuple_D1 : (\Function : (
            is_type_definer : True,
            evaluates : [\Tuple::(), \is_unary::()],
            default : (False,),
        )),

The selection type definer C<Tuple_D1> represents the infinite type consisting
just of the C<Tuple> values having exactly 1 attribute.  Its default value
has just the attribute with the name C<0> and asset value of C<False>.

=head2 D1

        D1 : (\Function : (
            returns : \$Tuple_D1,
            matches : (\$Attr_Name, \$Any),
            evaluates : (evaluates args --> \foundation::Tuple_D1_select()),
        )),

The function C<D1> results in the C<Tuple_D1> value whose sole attribute's
name is is specified in its C<0> argument and that attribute's value is
specified in its C<1> argument.

=head2 has_any_attrs (Tuple)

        has_any_attrs::Tuple : (\Function : (
            returns : \$Boolean,
            matches : (\$Tuple,),
            implements : \$folder::'',
            evaluates : (args:.\0 != ()),
        )),

The function C<has_any_attrs::Tuple> results in C<True> iff its C<0>
argument has any attributes, and in C<False> iff it has no attributes.
This function implements the C<Attributive> virtual function
C<has_any_attrs> aka C<?$> for the composing type C<Tuple>.

=head2 nullary (Tuple)

        nullary::Tuple : (\Function : (
            returns : \$Tuple,
            matches : (\$Tuple,),
            implements : \$folder::'',
            evaluates : (()),
        )),

The function C<nullary::Tuple> results in the only zero-attribute C<Tuple>
value.  This function implements the C<Attributive> virtual function
C<nullary> for the composing type C<Tuple>.

=head2 is_unary (Tuple)

        is_unary::Tuple : (\Function : (
            returns : \$Boolean,
            matches : (\$Tuple,),
            implements : \$folder::'',
            evaluates : (degree::(args:.\0) = 1),
        )),

The function C<is_unary::Tuple> results in C<True> iff its C<0> argument
has exactly 1 attribute, and C<False> otherwise.  This function implements
the C<Attributive> virtual function C<is_unary> for the composing type
C<Tuple>.

=head2 degree (Tuple)

        degree::Tuple : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Tuple,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Tuple_degree()),
        )),

The function C<degree::Tuple> results in the integral count of the
attributes of its C<0> argument.  This function implements the
C<Attributive> virtual function C<degree> aka C<#$> for the composing type
C<Tuple>.

=head2 heading (Tuple)

        heading::Tuple : (\Function : (
            returns : \$Heading,
            matches : (\$Tuple,),
            implements : \$folder::'',
            evaluates : (evaluates args --> \foundation::Tuple_heading()),
        )),

The function C<heading::Tuple> results in the relational I<heading> of its
C<0> argument, that is its set of distinct attribute names.  This function
implements the C<Attributive> virtual function C<heading> aka C<$> for the
composing type C<Tuple>.

=head2 rename (Tuple)

        rename::Tuple : (\Function : (
            returns : \$Tuple,
            matches : (\$Tuple, \$Renaming),
            implements : \$folder::'',
            accepts : (...),
            evaluates : (evaluates args --> \foundation::Tuple_rename()),
        )),

The function C<rename::Tuple> results results in the I<relational rename>
of its C<0> argument in terms of its C<1> argument.  The result is a
C<Tuple> value that has all of the attributes of the function's C<0>
argument but that, for each attribute of the C<0> argument whose attribute
name matches a I<before> element of its C<1> argument, the result's
corresponding attribute instead has an attribute name that matches the
corresponding I<after> element of the C<1> argument.  The I<degree> of the
result is equal to the degree of the C<0> argument.  This function will
fail if any I<before> element of the C<1> argument does not match the name
of an attribute name of the C<0> argument, or if any I<after> element of
the former does match an attribute name of the latter that isn't being
renamed.  This function implements the C<Attributive> virtual function
C<rename> aka C<$:=> aka C<ρ> for the composing type C<Tuple>.

=head2 on (Tuple)

        on::Tuple : (\Function : (
            returns : \$Tuple,
            matches : (\$Tuple, \$Heading),
            implements : \$folder::'',
            accepts : (args:.\0 $? args:.\1),
            evaluates : (evaluates args --> \foundation::Tuple_on()),
        )),

The function C<on::Tuple> results in the I<relational projection> of its
C<0> argument in terms of its C<1> argument.  The result is a C<Tuple>
value that has all of the attributes of the function's C<0> argument whose
names match the names of attributes of its C<1> argument. The I<degree> of
the result is equal to the degree of the C<1> argument.  This function will
fail if the I<heading> of the C<1> argument is not a subset of the
I<heading> of the C<0> argument.  This function implements the
C<Attributive> virtual function C<on> aka C<project> aka C<%=> aka C<π> for
the composing type C<Tuple>.

=head2 update (Tuple)

        update::Tuple : (\Function : (
            returns : \$Tuple,
            matches : (\$Tuple, \$Tuple),
            implements : \$folder::'',
            accepts : (args:.\0 $? args:.\1),
            evaluates : (evaluates args --> \foundation::Tuple_update()),
        )),

The function C<update::Tuple> results in a C<Tuple> value that has all of
the attributes of the function's C<0> argument but that, for each attribute
of the C<0> argument whose attribute name I<K> matches the name of an
attribute of its C<1> argument, the result takes its corresponding
attribute from the C<1> argument rather than from the C<0> argument.  The
I<degree> of the result is equal to the degree of the C<0> argument.  This
function will fail if the I<heading> of the C<1> argument is not a subset
of the I<heading> of the C<0> argument.  This function implements the
C<Attributive> virtual function C<update> aka C<%:=> for the composing type
C<Tuple>.

=head2 extend (Tuple)

        extend::Tuple : (\Function : (
            returns : \$Tuple,
            matches : (\$Tuple, \$Tuple),
            implements : \$folder::'',
            accepts : (args:.\0 disjoint_heading args:.\1),
            is_associative : True,
            is_commutative : True,
            identity : (),
            evaluates : (evaluates args --> \foundation::Tuple_extend()),
        )),

The function C<extend::Tuple> results in the I<relational extension> of its
C<0> argument in terms of its C<1> argument.  The result is a C<Tuple>
value that has all of the attributes of the function's C<0> argument plus
all of the attributes of its C<1> argument.  The I<degree> of the result is
equal to the integral sum of the degrees of the 2 arguments.  This function
will fail if the I<headings> of the 2 arguments are not disjoint.  This
function implements the C<Attributive> virtual function C<extend> aka C<%+>
for the composing type C<Tuple>.

=head2 at (Tuple)

        at::Tuple : (\Function : (
            returns : \$Any,
            matches : (\$Tuple, \$Attr_Name),
            implements : \$folder::'',
            accepts : (args:.\0 .? args:.\1),
            evaluates : (args:.\0 :. (args:.\1)),
        )),

The function C<at::Tuple> results in the attribute asset value of its C<0>
argument whose attribute name is equal to its C<1> argument.  This function
will fail if the C<0> argument doesn't have such an attribute.  This
function implements the C<Accessible> virtual function C<at> aka C<.> for
the composing type C<Tuple>.

Note that while the C<at::Tuple> definition could have been in terms of a
plain invocation of the Foundation function C<Tuple_at>, it is instead in
terms of the special-purpose expression syntax for a C<Tuple> attribute
asset accessor aka C<:.>.

=head2 to_Tuple (Tuple)

        to_Tuple::Tuple : (\Function : (
            returns : \$Tuple,
            matches : (\$Tuple,),
            implements : \$folder::'',
            evaluates : (args:.\0),
        )),

The function C<to_Tuple::Tuple> simply results in its C<0> argument.  This
function implements the C<Structural> virtual function C<to_Tuple> aka C<%>
for the composing type C<Tuple>.

=head2 any_attrs

        any_attrs : (\Function : (
            returns : \$Boolean,
            matches : (\$Tuple, \$Signature),
            evaluates : (foundation::Tuple_any_attrs(args:.\0, Signature_to_Function_Call_But_0::(args:.\1))),
        )),

I<TODO.>

=head2 none_of_attrs

        none_of_attrs : (\Function : (
            negates : \$any_attrs,
        )),

I<TODO.>

=head2 all_attrs

        all_attrs : (\Function : (
            returns : \$Boolean,
            matches : (\$Tuple, \$Signature),
            evaluates : (args:.\0 none_of_attrs \not_is_a::( 1: args:.\1 )),
        )),

I<TODO.>

=head2 not_all_attrs

        not_all_attrs : (\Function : (
            negates : \$all_attrs,
        )),

I<TODO.>

=head2 all_attr_assets

        all_attr_assets : (\Function : (
            returns : \$Boolean,
            matches : (\$Tuple, \$Signature),
            evaluates : (args:.\0 all_attrs \(evaluates args:.\1 <-- (args:.\0.\asset,))
                <-- (1 : Signature_to_Function_Call_But_0::(args:.\1))),
        )),

I<TODO.>

=head2 attrs_where

        attrs_where : (\Function : (
            returns : \$Tuple,
            matches : (\$Tuple, \$Signature),
            evaluates : (foundation::Tuple_attrs_where(args:.\0, Signature_to_Function_Call_But_0::(args:.\1))),
        )),

I<TODO.>

=head2 attrs_map

        attrs_map : (\Function : (
            returns : \$Tuple,
            matches : (\$Tuple, \$Function_Call_But_0),
            evaluates : (evaluates args --> \foundation::Tuple_attrs_map()),
        )),

I<TODO.>

=head2 attrs_reduce

        attrs_reduce : (\Function : (
            returns : \$Any,
            matches : (\$Tuple, \$Function_Call_But_0_1),
            evaluates : (evaluates args --> \foundation::Tuple_attrs_reduce()),
        )),

I<TODO.>

=head1 RELATIONAL DATA TYPES

=head2 Relational

        Relational : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Unionable, \$Attributive},
        )),

The interface type definer C<Relational> is semifinite.  A C<Relational> value is
a C<Unionable> value that is also an C<Attributive> value.  This means the
value is or is isomorphic to a C<Homogeneous> collection each of whose
I<members> is a C<Tuple>, and every member has the same relational
I<heading>; but a C<Relational> value still has a heading even if it has no
members.  The default value of C<Relational> is the C<Relation> value with
zero attributes and zero members, C<\?%()>.  C<Relational> is composed by
C<Tuple_Array>, C<Relation>, C<Tuple_Bag>.

=head2 to_Boolean (Relational)

        to_Boolean::Relational : (\Function : (
            returns : \$Boolean,
            matches : (\$Relational,),
            implements : \$folder::'',
            evaluates : (? |args:.\0),
        )),

The function C<to_Boolean::Relational> results in C<True> iff its C<0>
argument has any tuples, and in C<False> iff it has no tuples.  This
function implements the C<Boolable> virtual function C<to_Boolean> aka
C<so> aka C<?> for the composing type C<Relational>.

=head2 empty (Relational)

        empty::Relational : (\Function : (
            returns : \$Relational,
            matches : (\$Relational,),
            implements : \$folder::'',
            evaluates : (select_Relational::( like: args:.\0, heading: $args:.\0, body: empty |args:.\0 )),
        )),

The function C<empty::Relational> results in the only value of its C<0>
argument's relational type that has the same I<heading> as that argument
and whose I<body> has zero tuples.  This function implements the
C<Emptyable> virtual function C<empty> aka C<∅> for the composing type
C<Relational>.

=head2 singular (Relational)

        singular::Relational : (\Function : (
            returns : \$Boolean,
            matches : (\$Relational,),
            implements : \$folder::'',
            evaluates : (singular |args:.\0),
        )),

The function C<singular::Relational> results in C<True> iff its C<0>
argument has exactly 1 distinct tuple, and C<False> otherwise.  This
function implements the C<Homogeneous> virtual function C<singular> for the
composing type C<Relational>.

=head2 only_member (Relational)

        only_member::Relational : (\Function : (
            returns : \$Structural,
            matches : (\$Relational,),
            implements : \$folder::'',
            accepts : (singular args:.\0),
            evaluates : (only_member |args:.\0),
        )),

The function C<only_member::Relational> results in its C<0> argument's only
distinct tuple.  This function will fail if the argument doesn't have
exactly 1 distinct tuple.  This function implements the C<Homogeneous>
virtual function C<only_member> for the composing type C<Relational>.

=head2 has_n (Relational)

        has_n::Relational : (\Function : (
            returns : {\$Boolean, \$Not_Same_Heading},
            matches : (\$Relational, \$Structural, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (if args:.\0 =$ args:.\1 then guard has_n::(|args:.\0, args:.\1, args:.\2)
                else \!Not_Same_Heading),
        )),

The function C<has_n::Relational> results in C<True> iff its C<0> argument
has at least N tuples such that each is equal to its C<1> argument, where N
is defined by its C<2> argument; otherwise it results in C<False>.  The
result is always C<True> when the C<2> argument is zero.  Note that having
a C<2> argument greater than 1 in combination with a C<Setty> typed C<0>
argument will always result in C<False>.  The result is only I<defined>
when the C<0> and C<1> arguments have the same I<heading>; it is
C<\!Not_Same_Heading> otherwise.  This function implements the C<Homogeneous> virtual
function C<has_n> for the composing type C<Relational>.

=head2 multiplicity (Relational)

        multiplicity::Relational : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Relational, \$Structural),
            implements : \$folder::'',
            evaluates : (|args:.\0 multiplicity args:.\1),
        )),

The function C<multiplicity::Relational> results in the integral count
of tuples of its C<0> argument such that each tuple is equal to its
C<1> argument.  For a C<Setty> typed C<0> argument, the result is always
just 0 or 1.  This function implements the C<Homogeneous> virtual
function C<multiplicity> for the composing type C<Relational>.

=head2 all_unique (Relational)

        all_unique::Relational : (\Function : (
            returns : \$Boolean,
            matches : (\$Relational,),
            implements : \$folder::'',
            evaluates : (all_unique |args:.\0),
        )),

The function C<all_unique::Relational> results in C<True> iff its C<0>
argument has no 2 members that are the same tuple, and C<False> otherwise.
The result is always C<True> for a C<Setty> argument.  This function
implements the C<Homogeneous> virtual function C<all_unique> for the
composing type C<Relational>.

=head2 unique (Relational)

        unique::Relational : (\Function : (
            returns : \$Relational,
            matches : (\$Relational,),
            implements : \$folder::'',
            evaluates : (select_Relational::( like: args:.\0, heading: $args:.\0, body: unique |args:.\0 )),
        )),

The function C<unique::Relational> results in the value of its C<0>
argument's relational type that has, for every distinct tuple I<V> of the
function's C<0> argument, exactly 1 tuple that is equal to I<V>. The result
is always the same value as its argument when that is C<Setty>.  If the
result's type is C<Positional>, then each retained tuple is the one closest
to the start of the argument out of those tuples sharing the retained
tuple's value.  See also the C<Positional> function C<squish>.  This
function implements the C<Homogeneous> virtual function C<unique> for the
composing type C<Relational>.

=head2 subset_of (Relational)

        subset_of::Relational : (\Function : (
            returns : {\$Boolean, \$Not_Same_Heading},
            matches : (\$Relational, \$Relational),
            implements : \$folder::'',
            evaluates : (if args:.\0 =$ args:.\1 then guard |args:.\0 subset_of |args:.\1
                else \!Not_Same_Heading),
        )),

The function C<subset_of::Relational> results in C<True> iff the multiset
of tuples of its C<0> argument is a subset of the multiset of tuples of its
C<1> argument; otherwise it results in C<False>.  The result is only
I<defined> when the 2 arguments have the same I<heading>; it is
C<\!Not_Same_Heading> otherwise.  This function implements the C<Homogeneous> virtual
function C<subset_of> aka C<⊆> for the composing type C<Relational>.

=head2 same_members (Relational)

        same_members::Relational : (\Function : (
            returns : {\$Boolean, \$Not_Same_Heading},
            matches : (\$Relational, \$Relational),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (if args:.\0 =$ args:.\1 then guard |args:.\0 same_members |args:.\1
                else \!Not_Same_Heading),
        )),

The function C<same_members::Relational> results in C<True> iff the
multiset of tuples of its C<0> argument is the same as the multiset of
tuples of its C<1> argument; otherwise it results in C<False>.  This
function may result in C<True> for some C<Positional> arguments for which
C<same> would result in C<False> because the latter considers tuple order
significant while the former doesn't; for non-C<Positional> arguments, the
2 functions are typically the same.  The result is only I<defined> when the
2 arguments have the same I<heading>; it is C<\!Not_Same_Heading> otherwise.  This
function implements the C<Homogeneous> virtual function C<same_members> for
the composing type C<Relational>.

=head2 overlaps_members (Relational)

        overlaps_members::Relational : (\Function : (
            returns : {\$Boolean, \$Not_Same_Heading},
            matches : (\$Relational, \$Relational),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (if args:.\0 =$ args:.\1 then guard |args:.\0 overlaps_members |args:.\1
                else \!Not_Same_Heading),
        )),

The function C<overlaps_members::Relational> results in C<True> iff, given
I<X> as the multiset of tuples of its argument C<0> and I<Y> as the
multiset of tuples of its argument C<1>, there exists at least 1 tuple that
both I<X> and I<Y> have, and there also exists at least 1 other tuple each
of I<X> and I<Y> that the other doesn't have; otherwise it results in
C<False>.  The result is only I<defined> when the 2 arguments have the same
I<heading>; it is C<\!Not_Same_Heading> otherwise.  This function implements the
C<Homogeneous> virtual function C<overlaps_members> for the composing type
C<Relational>.

=head2 disjoint_members (Relational)

        disjoint_members::Relational : (\Function : (
            returns : {\$Boolean, \$Not_Same_Heading},
            matches : (\$Relational, \$Relational),
            implements : \$folder::'',
            is_commutative : True,
            evaluates : (if args:.\0 =$ args:.\1 then guard |args:.\0 disjoint_members |args:.\1
                else \!Not_Same_Heading),
        )),

The function C<disjoint_members::Relational> results in C<True> iff the
multiset of tuples of its C<0> argument has no tuples in common with the
multiset of tuples of its C<1> argument; otherwise it results in C<False>.
The result is only I<defined> when the 2 arguments have the same
I<heading>; it is C<\!Not_Same_Heading> otherwise.  This function implements the
C<Homogeneous> virtual function C<disjoint_members> for the composing type
C<Relational>.

=head2 any (Relational)

        any::Relational : (\Function : (
            returns : \$Boolean,
            matches : (\$Relational, \$Signature),
            implements : \$folder::'',
            evaluates : (|args:.\0 any args:.\1),
        )),

I<TODO.>

=head2 insert_n (Relational)

        insert_n::Relational : (\Function : (
            returns : {\$Relational, \$Not_Same_Heading},
            matches : (\$Relational, \$Structural, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (if args:.\0 =$ args:.\1 then guard select_Relational::
                    ( like: args:.\0, heading: $args:.\0, body: insert_n::(|args:.\0, args:.\1, args:.\2) )
                else \!Not_Same_Heading),
        )),

The function C<insert_n::Relational> results in the value of its C<0>
argument's relational type that has all of the tuples of the function's
C<0> argument plus N additional tuples that are each equal to its C<1>
argument, where N is defined by its C<2> argument; however, if the result's
type is C<Setty>, the result will have no more than 1 tuple equal to the
C<1> argument (any duplicates will be silently eliminated), so the result
may equal the C<0> argument even when the C<2> argument is nonzero.  If the
result's type is C<Positional>, then the result starts with all of the
tuples of C<0> in the same order and ends with any added instances of C<1>.
The result is only I<defined> when the C<0> and C<1> arguments have the
same I<heading>; it is C<\!Not_Same_Heading> otherwise. This function implements the
C<Unionable> virtual function C<insert_n> for the composing type
C<Relational>.

=head2 remove_n (Relational)

        remove_n::Relational : (\Function : (
            returns : {\$Relational, \$Not_Same_Heading},
            matches : (\$Relational, \$Structural, \$Integer_NN),
            implements : \$folder::'',
            evaluates : (if args:.\0 =$ args:.\1 then guard select_Relational::
                    ( like: args:.\0, heading: $args:.\0, body: remove_n::(|args:.\0, args:.\1, args:.\2) )
                else \!Not_Same_Heading),
        )),

The function C<remove_n::Relational> results in the value of its C<0>
argument's relational type that has all of the tuples of the function's
C<0> argument minus N existing tuples that are each equal to its C<1>
argument, where N is defined as the lesser of its C<2> argument and the
count of tuples of C<0> equal to the C<1> argument, so the result may equal
the C<0> argument even when the C<2> argument is nonzero.  If the result's
type is C<Positional>, then the removed instances of C<1> are those closest
to the end of C<0>.  The result is only I<defined> when the C<0> and C<1>
arguments have the same I<heading>; it is C<\!Not_Same_Heading> otherwise.  This
function implements the C<Unionable> virtual function C<remove_n> for the
composing type C<Relational>.

=head2 member_plus (Relational)

        member_plus::Relational : (\Function : (
            returns : {\$Relational, \$Not_Same_Heading},
            matches : (\$Relational, \$Relational),
            implements : \$folder::'',
            is_associative : True,
            evaluates : (if args:.\0 =$ args:.\1 then guard select_Relational::
                    ( like: args:.\0, heading: $args:.\0, body: |args:.\0 member_plus |args:.\1 )
                else \!Not_Same_Heading),
        )),

The function C<member_plus::Relational> results in the I<multiset sum> of
its 2 arguments C<0> and C<1>.  The result is the value of its C<0>
argument's relational type that has all of the tuples of the function's
C<0> argument plus all of the tuples of its C<1> argument.  For every
distinct tuple of the result, its multiplicity is the integral sum of the
multiplicities of that same tuple of each of the 2 arguments; however, if
the result's type is C<Setty>, the result will only have 1 tuple per
distinct tuple (any duplicates will be silently eliminated).  If the
result's type is C<Positional>, then the result starts with all of the
members of C<0> and ends with the members of C<1>, the members from both in
the same order as in their respective arguments; that is, this function
then behaves identically to C<catenate> aka C<~> when given the same
arguments.  This operation has a I<two-sided identity element> value of a collection with zero
members.  For non-ordered types, this operation is also commutative.  The
result is only I<defined> when the 2 arguments have the same I<heading>; it
is C<\!Not_Same_Heading> otherwise.  This function implements the C<Unionable>
virtual function C<member_plus> aka C<⊎> for the composing type
C<Relational>.

=head2 except (Relational)

        except::Relational : (\Function : (
            returns : {\$Relational, \$Not_Same_Heading},
            matches : (\$Relational, \$Relational),
            implements : \$folder::'',
            evaluates : (if args:.\0 =$ args:.\1 then guard select_Relational::
                    ( like: args:.\0, heading: $args:.\0, body: |args:.\0 except |args:.\1 )
                else \!Not_Same_Heading),
        )),

The function C<except::Relational> results in the I<multiset difference>
between its 2 arguments C<0> (I<minuend>) and C<1> (I<subtrahend>).  The
result is the value of its C<0> argument's relational type that has all of
the tuples of the function's C<0> argument minus all of the matching tuples
of its C<1> argument.  For every distinct tuple of the result, its
multiplicity is the integral difference of the multiplicities of that same
tuple of each of the 2 arguments (when subtracting the I<subtrahend> from
the I<minuend>); a multiplicity is zero when it would otherwise be
negative.  If the result's type is C<Positional>, then the removed
instances of any distinct tuple are those closest to the end of C<0>.  This
operation has a I<right identity element> value of a collection with zero members.  The
result is only I<defined> when the 2 arguments have the same I<heading>; it
is C<\!Not_Same_Heading> otherwise.  This function implements the C<Unionable>
virtual function C<except> aka C<∖> for the composing type C<Relational>.

=head2 intersect (Relational)

        intersect::Relational : (\Function : (
            returns : {\$Relational, \$Not_Same_Heading},
            matches : (\$Relational, \$Relational),
            implements : \$folder::'',
            is_associative : True,
            is_idempotent : True,
            evaluates : (if args:.\0 =$ args:.\1 then guard select_Relational::
                    ( like: args:.\0, heading: $args:.\0, body: |args:.\0 intersect |args:.\1 )
                else \!Not_Same_Heading),
        )),

The function C<intersect::Relational> results in the I<multiset
intersection> of its 2 arguments C<0> and C<1>.  The result is the value of
its C<0> argument's relational type that has all of the tuples of the
function's C<0> argument that match their own tuples of its C<1> argument.
For every distinct tuple of the result, its multiplicity is the integral
minimum of the multiplicities of that same tuple of each of the 2 arguments
(any nonmatched argument tuple does not appear in the result).  If the
result's type is C<Positional>, then the removed instances of any distinct
tuple are those closest to the end of C<0>.  This operation conceptually
has a I<two-sided identity element> value of a collection with an infinite number of members.
For non-ordered types, this operation is also commutative.  The result is
only I<defined> when the 2 arguments have the same I<heading>; it is
C<\!Not_Same_Heading> otherwise.  This function implements the C<Unionable> virtual
function C<intersect> aka C<∩> for the composing type C<Relational>.

=head2 union (Relational)

        union::Relational : (\Function : (
            returns : {\$Relational, \$Not_Same_Heading},
            matches : (\$Relational, \$Relational),
            implements : \$folder::'',
            is_idempotent : True,
            evaluates : (if args:.\0 =$ args:.\1 then guard select_Relational::
                    ( like: args:.\0, heading: $args:.\0, body: |args:.\0 union |args:.\1 )
                else \!Not_Same_Heading),
        )),

The function C<union::Relational> results in the I<multiset union> of its 2
arguments C<0> and C<1>.  The result is the value of its C<0> argument's
relational type that has all of the tuples of the function's C<0> argument
plus all of the nonmatching tuples of its C<1> argument.  For every
distinct tuple of the result, its multiplicity is the integral maximum of
the multiplicities of that same tuple of each of the 2 arguments.  If the
result's type is C<Positional>, then the result starts with all of the
members of C<0> and ends with the nonmatching members of C<1>, the members
from both in the same order as in their respective arguments; the removed
(due to matching) instances of any distinct tuple are those closest to the
end of C<1>.  This operation has a I<two-sided identity element> value of a collection with
zero members.  For non-ordered types, this operation is also associative
and commutative.  The result is only I<defined> when the 2 arguments have
the same I<heading>; it is C<\!Not_Same_Heading> otherwise.  This function
implements the C<Unionable> virtual function C<union> aka C<∪> for the
composing type C<Relational>.

=head2 exclusive (Relational)

        exclusive::Relational : (\Function : (
            returns : {\$Relational, \$Not_Same_Heading},
            matches : (\$Relational, \$Relational),
            implements : \$folder::'',
            is_associative : True,
            is_commutative : True,
            evaluates : (if args:.\0 =$ args:.\1 then guard select_Relational::
                    ( like: args:.\0, heading: $args:.\0, body: |args:.\0 exclusive |args:.\1 )
                else \!Not_Same_Heading),
        )),

The function C<exclusive::Relational> results in the I<multiset symmetric
difference> of its 2 arguments C<0> and C<1>.  The result is the value of
its C<0> argument's relational type that has all of the tuples of each of
the function's C<0> and C<1> arguments that do not have matching tuples of
their counterpart argument.  For every distinct tuple of the result, its
multiplicity is the integral maximum of the multiplicities of that same
tuple of each of the 2 arguments, minus the integral minimum of the same.
If the result's type is C<Positional>, then the result starts with the
nonmatching members of C<0> and ends with the nonmatching members of C<1>,
the members from both in the same order as in their respective arguments;
the removed (due to matching) instances of any distinct tuple are those
closest to the end of C<0> or C<1> respectively.  This operation has a
I<two-sided identity element> value of a collection with zero members.  For non-ordered types,
this operation is also associative and commutative.  The result is only
I<defined> when the 2 arguments have the same I<heading>; it is
C<\!Not_Same_Heading> otherwise. This function implements the C<Unionable> virtual
function C<exclusive> aka C<symm_diff> aka C<∆> for the composing type
C<Relational>.

=head2 nest (Relational)

        nest::Relational : (\Function : (
            returns : \$Relational,
            matches : (\$Relational,),
            implements : \$folder::'',
            accepts : (...),
            evaluates : (...),
        )),

I<TODO.>

=head2 unnest (Relational)

        unnest::Relational : (\Function : (
            returns : \$Relational,
            matches : (\$Relational,),
            implements : \$folder::'',
            accepts : (...),
            evaluates : (...),
        )),

I<TODO.>

=head2 where (Relational)

        where::Relational : (\Function : (
            returns : \$Relational,
            matches : (\$Relational, \$Signature),
            implements : \$folder::'',
            evaluates : (select_Relational::( like: args:.\0, heading: $args:.\0, body: |args:.\0 where args:.\1 )),
        )),

I<TODO.>

=head2 map (Relational)

        map::Relational : (\Function : (
            returns : \$Relational,
            matches : (\$Relational, \$Function_Call_But_0),
            implements : \$folder::'',
            evaluates : (...),
        )),

I<TODO.>

=head2 reduce (Relational)

        reduce::Relational : (\Function : (
            returns : \$Any,
            matches : (\$Relational, \$Function_Call_But_0_1),
            implements : \$folder::'',
            evaluates : (|args:.\0 reduce args:.\1),
        )),

I<TODO.>

=head2 has_any_attrs (Relational)

        has_any_attrs::Relational : (\Function : (
            returns : \$Boolean,
            matches : (\$Relational,),
            implements : \$folder::'',
            evaluates : ($args:.\0 != ()),
        )),

The function C<has_any_attrs::Relational> results in C<True> iff its C<0>
argument has any attributes, and in C<False> iff it has no attributes.
This function implements the C<Attributive> virtual function
C<has_any_attrs> aka C<?$> for the composing type C<Relational>.

=head2 nullary (Relational)

        nullary::Relational : (\Function : (
            returns : \$Relational,
            matches : (\$Relational,),
            implements : \$folder::'',
            evaluates : (args:.\0 on ()),
        )),

The function C<nullary::Relational> results in the value of its C<0>
argument's relational type that has zero attributes.  For types like
C<Tuple_Bag>, there is a distinct result for each I<cardinality>; for types
like C<Relation> there are exactly 2 possible result values.  This function
implements the C<Attributive> virtual function C<nullary> for the composing
type C<Relational>.

=head2 is_unary (Relational)

        is_unary::Relational : (\Function : (
            returns : \$Boolean,
            matches : (\$Relational,),
            implements : \$folder::'',
            evaluates : (is_unary $args:.\0),
        )),

The function C<is_unary::Relational> results in C<True> iff its C<0>
argument has exactly 1 attribute, and C<False> otherwise.  This function
implements the C<Attributive> virtual function C<is_unary> for the
composing type C<Relational>.

=head2 degree (Relational)

        degree::Relational : (\Function : (
            returns : \$Integer_NN,
            matches : (\$Relational,),
            implements : \$folder::'',
            evaluates : (degree $args:.\0),
        )),

The function C<degree::Relational> results in the integral count of the
attributes of its C<0> argument.  This function implements the
C<Attributive> virtual function C<degree> aka C<#$> for the composing type
C<Relational>.

=head2 rename (Relational)

        rename::Relational : (\Function : (
            returns : \$Relational,
            matches : (\$Relational, \$Renaming),
            implements : \$folder::'',
            accepts : (...),
            evaluates : (select_Relational::( like: args:.\0, heading: $args:.\0 rename args:.\1,
                body: |args:.\0 map \rename::( 1: args:.\1 ) )),
        )),

The function C<rename::Relational> results results in the I<relational
rename> of its C<0> argument in terms of its C<1> argument.  The result is
a value of the function's C<0> argument's relational type that has all of
the attributes of the function's C<0> argument but that, for each attribute
of the C<0> argument whose attribute name matches a I<before> element of
its C<1> argument, the result's corresponding attribute instead has an
attribute name that matches the corresponding I<after> element of the C<1>
argument.  The I<degree> and the I<cardinality> of the result are both
equal to the degree and cardinality of the C<0> argument, respectively.
This function will fail if any I<before> element of the C<1> argument does
not match the name of an attribute name of the C<0> argument, or if any
I<after> element of the former does match an attribute name of the latter
that isn't being renamed.  This function implements the C<Attributive>
virtual function C<rename> aka C<$:=> aka C<ρ> for the composing type
C<Relational>.

=head2 can_project_matching (Relational)

        can_project_matching::Relational : (\Function : (
            returns : \$Boolean,
            matches : (\$Relational, \$Structural),
            implements : \$folder::'',
            evaluates : (args:.\0 $? args:.\1
                and_then guard |args:.\0 all \(args:.\0 %= $args:.\1 = args:.\1) <-- (1 : args:.\1,)),
        )),

The function C<can_project_matching::Relational> results in C<True> iff the
heading of its C<0> argument is a superset of the heading of its C<1>
argument and every commonly-named attribute of the two arguments also has
the same attribute asset; otherwise it results in C<False>.  Note that by
definition, the identity C<can_project_matching::(update::(a,s),s) = True>
aka C<a %:= s %=? s = True> should hold for all valid C<a> and C<s>.  This
function implements the C<Attributive> virtual function
C<can_project_matching> aka C<%=?> for the composing type C<Relational>.

=head2 on (Relational)

        on::Relational : (\Function : (
            returns : \$Relational,
            matches : (\$Relational, \$Heading),
            implements : \$folder::'',
            accepts : (args:.\0 $? args:.\1),
            evaluates : (select_Relational::( like: args:.\0, heading: $args:.\0 on args:.\1,
                body: |args:.\0 map \on::( 1: args:.\1 ) )),
        )),

The function C<on::Relational> results in the I<relational projection> of
its C<0> argument in terms of its C<1> argument.  The result is a value of
the function's C<0> argument's relational type that has all of the
attributes of the function's C<0> argument whose names match the names of
attributes of its C<1> argument.  The I<degree> of the result is equal to
the degree of the C<1> argument, and the I<cardinality> of the result is
either equal to or less than the cardinality of the C<0> argument; a lesser
cardinality would be the result of duplicate result member elimination for
a I<setty> type.  For every member tuple or isomorphism I<X> in the C<0>
argument, the result has a member tuple or isomorphism I<Y> that is equal
to the result of projecting I<X> on all of the attribute names of the C<1>
argument.  This function will fail if the I<heading> of the C<1> argument
is not a subset of the I<heading> of the C<0> argument.  This function
implements the C<Attributive> virtual function C<on> aka C<project> aka
C<%=> aka C<π> for the composing type C<Relational>.

=head2 update (Relational)

        update::Relational : (\Function : (
            returns : \$Relational,
            matches : (\$Relational, \$Structural),
            implements : \$folder::'',
            accepts : (args:.\0 $? args:.\1),
            evaluates : (select_Relational::( like: args:.\0, heading: $args:.\0,
                body: |args:.\0 map \update::( 1: args:.\1 ) )),
        )),

The function C<update::Relational> results in the value of its C<0>
argument's relational type that has all of the attributes of the function's
C<0> argument but that, for each attribute of the C<0> argument whose
attribute name I<K> matches the name of an attribute of its C<1> argument,
the result takes its corresponding attribute from the C<1> argument rather
than from the C<0> argument.  The I<degree> of the result is equal to the
degree of the C<0> argument, and the I<cardinality> of the result is either
equal to or less than the cardinality of the C<0> argument; a lesser
cardinality would be the result of duplicate result member elimination for
a I<setty> type.  For every member tuple or isomorphism I<X> in the C<0>
argument, the result has a member tuple or isomorphism I<Y> that is equal
to I<X> but that, for each attribute name I<K> of I<X>, the asset of the
attribute named I<K> of I<Y> is equal to the asset of the attribute named
I<K> of the C<1> argument rather than its value from I<X>.  This function
will fail if the I<heading> of the C<1> argument is not a subset of the
I<heading> of the C<0> argument.  This function implements the
C<Attributive> virtual function C<update> aka C<%:=> for the composing type
C<Relational>.

=head2 extend (Relational)

        extend::Relational : (\Function : (
            returns : \$Relational,
            matches : (\$Relational, \$Structural),
            implements : \$folder::'',
            accepts : (args:.\0 disjoint_heading args:.\1),
            evaluates : (select_Relational::( like: args:.\0, heading: $args:.\0 extend args:.\1,
                body: |args:.\0 map \extend::( 1: args:.\1 ) )),
        )),

The function C<extend::Relational> results in the I<relational extension>
of its C<0> argument in terms of its C<1> argument.  The result is a value
of the function's C<0> argument's relational type that has all of the
attributes of the function's C<0> argument plus all of the attributes of
its C<1> argument.  The I<degree> of the result is equal to the integral
sum of the degrees of the 2 arguments, and the I<cardinality> of the result
is equal to the cardinality of the C<0> argument.  There is a bijection
between the C<0> argument and the result with respect to their I<members>;
for every tuple or isomorphism I<X> in the C<0> argument, the result has a
tuple or isomorphism I<Y> that is equal to the result of extending I<X>
with all of the attributes (names and assets) of the C<1> argument.  This
function will fail if the I<headings> of the 2 arguments are not disjoint.
This function implements the C<Attributive> virtual function C<extend> aka
C<%+> for the composing type C<Relational>.

=head2 body |

        body::'' : (\Function : (
            virtual : True,
            returns : \$Unionable,
            matches : (\$Relational,),
        )),

        '|' : (\Alias : ( of : \$body, )),

The virtual function C<body> aka C<|> results in the relational I<body> of
its C<0> argument, that is its multiset of member tuples.  The form that
this takes is a generic C<Unionable> (such as a generic C<Array> or C<Set>
or C<Bag>) each of whose members is a C<Structural> value.  Note that the 2
functions C<heading> and C<body> are complementary for C<Relational>
values; between the two of them, one can obtain all primary components of a
typical C<Relational> value, and select that same value again.

=head2 select_Relational

        select_Relational::'' : (\Function : (
            virtual : True,
            returns : {\$Relational, \$...},
            matches : (like : \$Relational, heading : \$Heading, body : \$Unionable),
        )),

The virtual function C<select_Relational> results in the value of its
C<like> argument's relational type that has the same I<heading> as its
C<heading> argument and whose I<body> consists of just the member I<tuples>
of its C<body> argument.  The purpose of C<select_Relational> is to help
facilitate ease of reuse of code between different C<Relational> types.
Note that the identity C<r = select_Relational::( like : r, heading : $r,
body : |r )> should hold for any C<Relational> type.

I<TODO: Flesh out the set of predefined Excuse values, such as for body
members that aren't compatible with the heading, or possibly for composing
types that restrict their possible headings at the type level.>

=head1 TUPLE-ARRAY DATA TYPES

=head2 Tuple_Array

        Tuple_Array : (\Function : (
            is_type_definer : True,
            composes : {\$Relational, \$Positional},
            evaluates : (\$Signature::Article_Match : (
                label : \Tuple_Array,
                attrs : [
                    (
                        heading : \Heading::(),
                        body : [\Array::(), \all::( 1: \Tuple::() )],
                    ),
                    \(args:.\0:.\body all \($args:.\0 = args:.\1) <-- (1: args:.\0:.\heading,)),
                ],
            )),
            default : \~%(),
        )),

I<TODO.>

I<Note:  The in_order(TA,TA) inherited via Positional, which Tuple_Array
implements just for convenience and consistency with Array but doesn't
expect to be meaningful any more than say the Boolean version...
It needs to inline the in_order(Tuple,Tuple) used for its heading and for
its first nonmatching member, rather than Tuple implementing an in_order(),
since we don't want to infect the generic Tuple with all the Orderable ops.>
I<Note: This type structurally resembles a spreadsheet or a .NET DataTable,
or a subtype of it does.>

=head2 Tuple_Array_D0C0

        Tuple_Array_D0C0 : (\Function : (
            is_type_definer : True,
            constant : \~%(),
        )),

The singleton type definer C<Tuple_Array_D0C0> represents the only zero-attribute,
zero-tuple C<Tuple_Array> value.

=head2 Tuple_Array_D0C1

        Tuple_Array_D0C1 : (\Function : (
            is_type_definer : True,
            constant : \~%[()],
        )),

The singleton type definer C<Tuple_Array_D0C1> represents the only zero-attribute,
single-tuple C<Tuple_Array> value.

=head2 heading (Tuple_Array)

        heading::Tuple_Array : (\Function : (
            returns : \$Heading,
            matches : (\$Tuple_Array,),
            implements : \$folder::'',
            evaluates : (args:.\0:>.\heading),
        )),

The function C<heading::Tuple_Array> results in the relational I<heading>
of its C<0> argument, that is its set of distinct attribute names.  This
function implements the C<Attributive> virtual function C<heading> aka C<$>
for the composing type C<Tuple_Array>.

=head2 body (Tuple_Array)

        body::Tuple_Array : (\Function : (
            returns : \$Array,
            matches : (\$Tuple_Array,),
            implements : \$folder::'',
            evaluates : (args:.\0:>.\body),
        )),

The function C<body::Tuple_Array> results in the relational I<body> of its
C<0> argument, that is its multiset of member tuples.  This function
implements the C<Relational> virtual function C<body> aka C<|> for the
composing type C<Tuple_Array>.

=head2 select_Relational (Tuple_Array)

        select_Relational::Tuple_Array : (\Function : (
            returns : {\$Relational, \$...},
            matches : (like : \$Tuple_Array, heading : \$Heading, body : \$Array),
            implements : \$folder::'',
            evaluates : ((\Tuple_Array : (args %= \$(heading,body)))),
        )),

The function C<select_Relational::Tuple_Array> results in the
C<Tuple_Array> value that has the same I<heading> as its C<heading>
argument and whose I<body> consists of just the member I<tuples> of its
C<body> argument.  This function implements the C<Relational> virtual
function C<select_Relational> for the composing type C<Tuple_Array>.

=head1 RELATION DATA TYPES

=head2 Relation

        Relation : (\Function : (
            is_type_definer : True,
            composes : {\$Relational, \$Discrete, \$Setty},
            provides_default_for : {\$Relational},
            evaluates : (\$Signature::Article_Match : (
                label : \Relation,
                attrs : [
                    (
                        heading : \Heading::(),
                        body : [\Set::(), \all::( 1: \Tuple::() )],
                    ),
                    \(args:.\0:.\body all \($args:.\0 = args:.\1) <-- (1: args:.\0:.\heading,)),
                ],
            )),
            default : \?%(),
        )),

I<TODO.  See also definition of Set in terms of Bag, which Relation mirrors.>

=head2 Relation_D0C0

        Relation_D0C0 : (\Function : (
            is_type_definer : True,
            constant : \?%(),
        )),

The singleton type definer C<Relation_D0C0> represents the only
zero-attribute, zero-tuple C<Relation> value.  Note that I<The Third
Manifesto> also refers to this value by the special name I<TABLE_DUM>.

=head2 Relation_D0C1

        Relation_D0C1 : (\Function : (
            is_type_definer : True,
            constant : \?%{()},
        )),

The singleton type definer C<Relation_D0C1> represents the only
zero-attribute, single-tuple C<Relation> value.  Note that I<The Third
Manifesto> also refers to this value by the special name I<TABLE_DEE>.

=head2 heading (Relation)

        heading::Relation : (\Function : (
            returns : \$Heading,
            matches : (\$Relation,),
            implements : \$folder::'',
            evaluates : (args:.\0:>.\heading),
        )),

The function C<heading::Relation> results in the relational I<heading>
of its C<0> argument, that is its set of distinct attribute names.  This
function implements the C<Attributive> virtual function C<heading> aka C<$>
for the composing type C<Relation>.

=head2 body (Relation)

        body::Relation : (\Function : (
            returns : \$Set,
            matches : (\$Relation,),
            implements : \$folder::'',
            evaluates : (args:.\0:>.\body),
        )),

The function C<body::Relation> results in the relational I<body> of its
C<0> argument, that is its set of member tuples.  This function
implements the C<Relational> virtual function C<body> aka C<|> for the
composing type C<Relation>.

=head2 select_Relational (Relation)

        select_Relational::Relation : (\Function : (
            returns : {\$Relational, \$...},
            matches : (like : \$Relation, heading : \$Heading, body : \$Set),
            implements : \$folder::'',
            evaluates : ((\Relation : (args %= \$(heading,body)))),
        )),

The function C<select_Relational::Relation> results in the
C<Relation> value that has the same I<heading> as its C<heading>
argument and whose I<body> consists of just the member I<tuples> of its
C<body> argument.  This function implements the C<Relational> virtual
function C<select_Relational> for the composing type C<Relation>.

=head1 TUPLE-BAG DATA TYPES

=head2 Tuple_Bag

        Tuple_Bag : (\Function : (
            is_type_definer : True,
            composes : {\$Relational, \$Discrete},
            evaluates : (\$Signature::Article_Match : (
                label : \Tuple_Bag,
                attrs : [
                    (
                        heading : \Heading::(),
                        body : [\Bag::(), \all::( 1: \Tuple::() )],
                    ),
                    \(args:.\0:.\body all \($args:.\0 = args:.\1) <-- (1: args:.\0:.\heading,)),
                ],
            )),
            default : \+%(),
        )),

I<TODO.>

=head2 Tuple_Bag_D0C0

        Tuple_Bag_D0C0 : (\Function : (
            is_type_definer : True,
            constant : \+%(),
        )),

The singleton type definer C<Tuple_Bag_D0C0> represents the only zero-attribute,
zero-tuple C<Tuple_Bag> value.

=head2 Tuple_Bag_D0C1

        Tuple_Bag_D0C1 : (\Function : (
            is_type_definer : True,
            constant : \+%{()},
        )),

The singleton type definer C<Tuple_Bag_D0C1> represents the only zero-attribute,
single-tuple C<Tuple_Bag> value.

=head2 heading (Tuple_Bag)

        heading::Tuple_Bag : (\Function : (
            returns : \$Heading,
            matches : (\$Tuple_Bag,),
            implements : \$folder::'',
            evaluates : (args:.\0:>.\heading),
        )),

The function C<heading::Tuple_Bag> results in the relational I<heading>
of its C<0> argument, that is its set of distinct attribute names.  This
function implements the C<Attributive> virtual function C<heading> aka C<$>
for the composing type C<Tuple_Bag>.

=head2 body (Tuple_Bag)

        body::Tuple_Bag : (\Function : (
            returns : \$Bag,
            matches : (\$Tuple_Bag,),
            implements : \$folder::'',
            evaluates : (args:.\0:>.\body),
        )),

The function C<body::Tuple_Bag> results in the relational I<body> of its
C<0> argument, that is its multiset of member tuples.  This function
implements the C<Relational> virtual function C<body> aka C<|> for the
composing type C<Tuple_Bag>.

=head2 select_Relational (Tuple_Bag)

        select_Relational::Tuple_Bag : (\Function : (
            returns : {\$Relational, \$...},
            matches : (like : \$Tuple_Bag, heading : \$Heading, body : \$Bag),
            implements : \$folder::'',
            evaluates : ((\Tuple_Bag : (args %= \$(heading,body)))),
        )),

The function C<select_Relational::Tuple_Bag> results in the
C<Tuple_Bag> value that has the same I<heading> as its C<heading>
argument and whose I<body> consists of just the member I<tuples> of its
C<body> argument.  This function implements the C<Relational> virtual
function C<select_Relational> for the composing type C<Tuple_Bag>.

=head1 INTERVALISH DATA TYPES

=head2 Intervalish

        Intervalish : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Homogeneous},
        )),

I<TODO.>

=head1 INTERVAL DATA TYPES

=head2 Interval

        Interval : (\Function : (
            is_type_definer : True,
            composes : {\$Intervalish, \$Setty},
            evaluates : (\$Signature::Article_Match : (
                label : \Interval,
                attrs : \Interval_Attrs::(),
            )),
            default : (\!Before_All_Others..\!After_All_Others),
        )),

I<TODO.>

=head2 Interval_Attrs

        Interval_Attrs : (\Function : (
            is_type_definer : True,
            evaluates : [\Tuple::(), ...],
        )),

I<TODO.>

=head1 UNIONABLE INTERVALISH DATA TYPES

=head2 Unionable_Intervalish

        Unionable_Intervalish : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Intervalish, \$Unionable},
        )),

I<TODO.>

=head1 INTERVAL-SET DATA TYPES

=head2 Interval_Set

        Interval_Set : (\Function : (
            is_type_definer : True,
            composes : {\$Unionable_Intervalish, \$Setty},
            evaluates : (\$Signature::Article_Match : (
                label : \Interval_Set,
                attrs : (
                    members : [\Interval_Bag::(), \all_unique::()],
                ),
            )),
            default : ...,
        )),

I<TODO.  See also definition of Set in terms of Bag, which Interval_Set mirrors.>

=head1 INTERVAL-BAG DATA TYPES

=head2 Interval_Bag

        Interval_Bag : (\Function : (
            is_type_definer : True,
            composes : {\$Unionable_Intervalish},
            evaluates : (\$Signature::Article_Match : (
                label : \Interval_Bag,
                attrs : (
                    members : [\Tuple_Bag::(), ...],
                ),
            )),
            default : ...,
        )),

I<TODO.>

=head1 QUANTITATIVE DATA TYPES

=head2 Quantitative

        Quantitative : (\Function : (
            is_type_definer : True,
            is_generalization : True,
            composes : {\$Numerical},
        )),

I<TODO.>

=head1 QUANTITY DATA TYPES

=head2 Quantity

        Quantity : (\Function : (
            is_type_definer : True,
            composes : {\$Quantitative},
            evaluates : (\$Signature::Article_Match : (
                label : \Quantity,
                attrs : (
                    0 : [\Relation::(), ...],
                ),
            )),
            default : ...,
        )),

I<TODO.>

=head1 ARTICLE DATA TYPES

=head2 Article

        Article : (\Function : (
            is_type_definer : True,
            evaluates : \foundation::Article(),
            default : (False : ()),
        )),

The selection type definer C<Article> represents the infinite Muldis D
Foundation type C<foundation::Article>.  An C<Article> value is ...

A C<Article> value consists of a I<label> paired with a set of 0..N
I<attributes> where that set must be represented by a C<Tuple>.  While
conceptually a C<Article> could pair a I<label> with an I<asset> of any
type, restricting the latter to a C<Tuple> helps avoid unnecessary
complication for the design and users of the language.  The idiomatic way
to have an I<attributes> I<AV> that is conceptually of some non-C<Tuple>
type is to have a single attribute whose asset is I<AV>.  The idiomatic
default attribute name for a single-attribute C<Article> is C<0> when there
isn't an actual meaningful name to give it.

Given that almost every selection type definer is a subset of C<Article>, the
latter intentionally does not compose any interface type definers, and also
does not have any of its own C<System> package operators, so to minimize
interference with other types' interfaces.

Generic means for selecting C<Article> values in terms of a
I<label>/I<attributes> pair, or for extracting the I<label> or
I<attributes> of a C<Article>, are provided by dedicated language
expression node types and concrete syntax (and Foundation functions).

=head1 HANDLE DATA TYPES

=head2 Handle

        Handle : (\Function : (
            is_type_definer : True,
            evaluates : \foundation::Handle(),
        )),

The selection type definer C<Handle> represents the infinite Muldis D
Foundation type C<foundation::Handle>.  A C<Handle> value is an opaque and
transient reference to an entity or resource that potentially exists at
arms length from the current Muldis D process or language environment or
that otherwise has the potential to mutate in a way visible to the current
Muldis D process either due to or independent of the actions of said
process.  In the general case, the current Muldis D process employs
I<message passing> to interact with the entity such that the C<Handle>
serves as its uniform resource identifier.  In notable contrast to other
type definers, except C<None>, no C<Handle> type definer has a C<default>
trait, since it doesn't make sense for a C<Handle> to be a default value.

C<Handle> has multiple specialized subtypes including: C<Variable>,
C<Process>, C<Stream>, C<External>.  Given that C<Handle> is opaque, its
subtypes can only be distinguised using Foundation type definers.  It is
possible that these subtypes may overlap if the same entity is capable of
multiple relevant behaviours / can process applicable messages.

Note that C<Handle> are allowed to participate in reference cycles, for
example that the current value of a C<Variable> is itself, however the
intentional use of cycles is strongly discouraged.  Optional hints may be
associated with C<Handle> by users to help the system manage such cycles.

=head2 Variable

        Variable : (\Function : (
            is_type_definer : True,
            evaluates : \foundation::Variable(),
        )),

The selection type definer C<Variable> represents the infinite Muldis D
Foundation type C<foundation::Variable>.  A C<Variable> value is an opaque and
transient reference to a Muldis D I<variable> (the latter being a container
for an appearance of a value).  The identity of a C<Variable> is just the
identity of the (anonymous) variable that it points to, which is the
latter's address; when C<same> is given two C<Variable> arguments it will
result in C<True> iff they both point to the same variable.  Selection of a
C<Variable> value is formally a non-deterministic niladic operation, and
can only be performed in a I<procedure>, not in a I<function>.  Selecting a
C<Variable> value will create a new variable which it points to, and the
variable will be destroyed automatically when that C<Variable> value is no
longer in use by the program.  Only a procedure may use a C<Variable> to
access (read or mutate) the latter's variable; a function may only pass
around a C<Variable> it is given or compare two (that they point to the
same variable or not), and can not access the variable behind it.

As a function only can perceive a C<Variable> as a generic identifier, it
doesn't make sense to subtype or constrain a C<Variable> as if it were the
type of the underlying variable's current value.  However, a C<Variable>
can be wrapped by another value on the basis of whose other components
there can be subtyping or constraints.

I<TODO: Consider whether it is logically necessary for functions to be able
to read the variable behind a Variable, with a special expression node,
albeit with the guarantee that the variable is immutable / repeatable-read
during the life of the function call.  But if so, then that causes problems
in theory for higher-order functions, because then captures do or don't
have to be worried about should the function referring to the variable be
passed between statements.  Restricting to procedure expressions may help,
or not. Perhaps a solution to that is requiring functions to unwrap any
Variable into regular values if it expects to be non-deterministic.  Or
perhaps a more generic thing to do is split functions into 2 variants where
one is allowed to be non-deterministic (and read variables behind
Variables) while the other isn't.  The former can then also do certain
other things like read random numbers or fetch random elements or whatever.
If we cross this line, then variable reads don't need special syntax,
they're just a function of the appropriate kind.  We should probably use a
keyword other than "function" to name the variant, then, but their
structures are mostly alike.  Maybe call it a "reader"?  Note that this
"reader" would exclude most functional traits, particularly any that give
hints or imply commutativity etc.  Note that there are different kinds of
nondeterminism, such as select implementation-dependent-random something,
or scope over which a variable can be read repeatedly with same results, or
environment-based stuff, and each could perhaps be allowed or disallowed
individually.  MIND ALSO THE YAGNI PRINCIPLE, DON'T MOVE MORE OUT OF
EXCLUSIVE DOMAIN OF PROCEDURES THAN WE REALLY NEED TO. So something we
could do is NOT add the function variant, but supply a set of special
purpose foundational procedures for the common reasons we might want them.
FOR EXAMPLE, JUST CREATE A FOUNDATION 'SNAPSHOT' PROCEDURE WHICH WILL
RECURSIVELY FOLLOW A STRUCTURE AND REPLACE ALL HANDLES WITH THE CURRENT VALUES OF
THEIR VARIABLES AT THAT TIME (THIS OP SHOULD BE CHEAP).  Regular "current"/":&" deref
keyword would work for the typical single level fine, give value of a
variable to a function say.  Likewise, a proc to fetch a set of randoms.>

I<<TODO, other notes.  A C<Variable> value also carries meta-data such as
whether it is a strong or a weak reference.  The actual current value of
the referenced variable is itself readonly, but if it or any of its
elements is a Variable, then via that indirection one can have an
arbitrarily complex graph of memory that is subject-to-update, and hence
any programming language or hardware having the concept of memory pointers
can be emulated or interfaced without too much indirection.
A I<variable> itself also has meta-data such as its declared type (a
type definer) or registered stimulus-response rules etc.  Typically,
externally-interfacing resources such as file or network streams are
represented by C<Variable> values.  The generic assignment procedure C<:=>
takes a C<Variable> as its left-hand argument and updates the variable
that this points to to hold the value of its right-hand argument.>>

=head2 Process

        Process : (\Function : (
            is_type_definer : True,
            evaluates : \foundation::Process(),
        )),

The selection type definer C<Process> represents the infinite Muldis D
Foundation type C<foundation::Process>.  A C<Process> value is an opaque
and transient reference to a I<process> running inside the Muldis D
language environment / DBMS / virtual machine, which typically has its own
autonomous transactional context.  Each Muldis D I<process>
might be either self-contained or be a proxy for some entity outside the
Muldis D language environment.

A typical and idiomatic example use for multiple processes is to coordinate
usage of a shared resource such as I<the database>.  In this scenario, one
process would I<own the database> and do the actual work of reading it from
or persisting it to storage and serializing transactions, while other
processes would represent users of I<the database> that send the owner
messages defining data fetching queries or data manipulation requests, etc.

An idiomatic I<request message> includes all of the self-contained logic
necessary to perform a complete database transaction, which would
effectively be performed using I<serializable transaction isolation>, and
commit as a whole or else have no lasting effect.  An idiomatic I<response
message> indicates whether the request was granted/succeeded or
denied/failed and returns any relevant fetched data.

Any translation of a message into physical actions against a file system,
or any translation of a message into some other query language such as SQL
for a remote execution, is done by the I<database owner> Muldis D process.

=head2 Stream

        Stream : (\Function : (
            is_type_definer : True,
            evaluates : \foundation::Stream(),
        )),

The selection type definer C<Stream> represents the infinite Muldis D
Foundation type C<foundation::Stream>.  A C<Stream> value is an opaque and
transient reference to a Muldis D I<stream>, which represents streaming
data such as from/to user I/O or the filesystem or network services etc.

=head2 External

        External::'' : (\Function : (
            is_type_definer : True,
            evaluates : \foundation::External(),
        )),

The selection type definer C<External> represents the infinite Muldis D
Foundation type C<foundation::External>.  An C<External> value is an opaque and
transient reference to an entity that is defined and managed externally to
the Muldis D language environment, either internally to the Muldis D host
implementation or in some peer language that it mediates.

As C<External> values are black boxes to Muldis D, it is the responsibility
of their external manager to at the very least implement the C<same>
function for them in a fully deterministic manner, as well as externally
define any other operators for them that users may wish to invoke for those
values from Muldis D code, including any to map with or marshal with Muldis
D values.

Other programming languages may name their corresponding types I<extern>
(asmjs).

=head2 External::call_function

        External::call_function : (\Function : (
            returns : \$Any,
            matches : (\$Any,),
            evaluates : (evaluates \foundation::External_call_function() <-- args),
        )),

The function C<External::call_function> is a proxy for invoking a function
that is defined and managed externally to the Muldis D language
environment.  Muldis D will assume said function is completely
deterministic, and there would likely be problems if it isn't.  Using
C<External::call_function> as a foundation, it is possible to define an
arbitrarily complex type graph involving C<External> values.

=head1 EXCUSE DATA TYPES

=head2 Excuse

        Excuse : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \Excuse,
                attrs : (
                    0 : \Any(),
                ),
            )),
            default : \!No_Reason,
        )),

The selection type definer C<Excuse> is infinite.  An C<Excuse> value is an
explicitly stated reason for why, given some particular problem domain, a
value is not being used that is ordinary for that domain.  For example, the
typical integer division operation is not defined to give an integer result
when the divisor is zero, and so a Muldis D function for integer division
could be defined to result in an C<Excuse> value rather than throw an
exception in that case.  For another example, an C<Excuse> value could be
used to declare that the information we are storing about a person is
missing certain details and why those are missing, such as because the
person left the birthdate field blank on their application form.  Its
default value is C<\!No_Reason>.  An C<Excuse> is isomorphic to an
C<Exception> but that use of the former is not meant to terminate execution
of code early unlike the latter which is.  Other programming languages that
have typed exceptions are analogous.

I<TODO:  To be more specific, an Excuse value only should denote a
categorical reason for a normal value not being provided, and all possible
values of Excuse should be considered hard-coded.  They should not be
possible to vary based on user input; for example, a key out of range error
should not specify the attempted key.  Instead, other types that include an
Excuse as a component, such as an Exception, can include details that vary
on the specific user input, or include stack traces and such.>

I<TODO:  Generally speaking, criteria for when it is better to use an
Excuse result value rather than exception is like this.  In a function, any
result which is non-determinisitic must always be as an exception; in a
function it should generally not be possible to cause non-determinisim on
purpose (except perhaps memory exhaustion), so such a failure would
originate in the runtime environment and truly be exceptional.  Otherwise,
in a function, Excuses should be used by default in most circumstances,
typically where it is reasonable to expect a fraction of inputs may be bad
and we are purposefully letting the function determine which is which and
tell us (such as parsers say), or situations where it is reasonable to
propagate errors while continuing to process good parts; in contrast,
exceptions should be reserved for situations where the caller should easily
have known better to either invoke it with correct inputs and doing
otherwise is obviously wrong code the caller should fix.  In a procedure,
the non-determinism factor categorically doesn't require an exception.  In
general, exceptions are for exceptional circumstances that we shouldn't
have to expect to account for within the normal code paths, where
cancelling whole blocks of code is ok, while returning Excuses is for when
we do expect to handle these things within the normal code flow and/or not
automatically halt blocks of code.>

=head2 No_Reason

        No_Reason : (\Function : (
            is_type_definer : True,
            constant : \!No_Reason,
        )),

The singleton type definer C<No_Reason> represents the C<Excuse> value which
simply says that an ordinary value for any given domain is missing and that
there is simply no excuse that has been given for this; in other words,
something has gone wrong without the slightest hint of an explanation.
This is conceptually the most generic C<Excuse> value there is and it can
be used by lazy programmers as a fallback for when they don't have even a
semblance of a better explanation for why an ordinary value is missing.
Other programming languages may name their corresponding values or
quasi-values I<null> or I<nil> or I<none> or I<nothing> or I<undef> or
I<unknown>; but unlike some of those languages, C<No_Reason> equals itself.

=head2 Before_All_Others

        Before_All_Others : (\Function : (
            is_type_definer : True,
            composes : {\$Orderable},
            constant : \!Before_All_Others,
        )),

The singleton type definer C<Before_All_Others> represents a type-agnostic
analogy of negative infinity, an C<Orderable> value that sorts I<before>
all other values in the Muldis D type system, and that is its only meaning.
This value is expressly I<not> meant to represent any specific mathematical
or physical concept of I<infinity> or I<∞> (of which there are many),
including those of the IEEE floating-point standards; such things should be
defined in other, not-C<System>, Muldis D packages for the relevant domains.

=head2 After_All_Others

        After_All_Others : (\Function : (
            is_type_definer : True,
            composes : {\$Orderable},
            constant : \!After_All_Others,
        )),

The singleton type definer C<After_All_Others> represents a type-agnostic
analogy of positive infinity, an C<Orderable> value that sorts I<after>
all other values in the Muldis D type system, and that is its only meaning.
This value is expressly I<not> meant to represent any specific mathematical
or physical concept of I<infinity> or I<∞> (of which there are many),
including those of the IEEE floating-point standards; such things should be
defined in other, not-C<System>, Muldis D packages for the relevant domains.

=head2 Div_By_Zero

        Div_By_Zero : (\Function : (
            is_type_definer : True,
            constant : \!Div_By_Zero,
        )),

The singleton type definer C<Div_By_Zero> represents the I<undefined> result of
attempting to divide a simple number by a simple, unsigned, number zero.
Note that IEEE floating-point standards define a negative or positive
infinity result value when dividing by an explicitly signed (negative or
positive) zero, but the Muldis D C<System> package lacks those concepts.

=head2 Zero_To_The_Zero

        Zero_To_The_Zero : (\Function : (
            is_type_definer : True,
            constant : \!Zero_To_The_Zero,
        )),

The singleton type definer C<Zero_To_The_Zero> represents the I<undefined> result
of attempting to exponentiate a number zero to the power of a number zero.

=head2 No_Empty_Value

        No_Empty_Value : (\Function : (
            is_type_definer : True,
            constant : \!No_Empty_Value,
        )),

The singleton type definer C<No_Empty_Value> represents the I<undefined> result
of attempting to request the value with zero members of some collection
type that doesn't have a value with zero members.

=head2 No_Such_Ord_Pos

        No_Such_Ord_Pos : (\Function : (
            is_type_definer : True,
            constant : \!No_Such_Ord_Pos,
        )),

The singleton type definer C<No_Such_Ord_Pos> represents the I<undefined> result of
attempting to use a member ordinal position I<P> of C<Positional> value I<V> while
assuming incorrectly that I<V> already has a member whose ordinal position is I<P>.

=head2 No_Such_Attr_Name

        No_Such_Attr_Name : (\Function : (
            is_type_definer : True,
            constant : \!No_Such_Attr_Name,
        )),

The singleton type definer C<No_Such_Attr_Name> represents the I<undefined>
result of attempting to use an attribute named I<N> of C<Attributive> value
I<V> while assuming incorrectly that I<V> already has an attribute whose
name is I<N>.

=head2 Not_Same_Heading

        Not_Same_Heading : (\Function : (
            is_type_definer : True,
            constant : \!Not_Same_Heading,
        )),

The singleton type definer C<Not_Same_Heading> represents the I<undefined> result
of attempting to perform an operation that takes 2 C<Attributive> inputs
and requires them to have the same relational I<heading> but the actual 2
inputs have different headings.

=head2 coalesce ??

        coalesce : (\Function : (
            returns : \$Any,
            matches : (\$Any, \$Any),
            is_associative : True,
            is_idempotent : True,
            left_identity : \!No_Reason,
            evaluates : (if Excuse args:.\0 then args:.\1 else args:.\0),
        )),

        '??' : (\Alias : ( of : \$coalesce, )),

The function C<coalesce> aka C<??> results in its C<0> argument iff the
latter is not an C<Excuse>, and results in its C<1> argument otherwise.
This function is designed to be chained for any number of sequenced values
in order to pick the first non-C<Excuse> in a list.
This function has analogous stop-or-continue behaviour to the Muldis D
special syntax C<or_else> where any C<Excuse> or non-C<Excuse> stands in
for C<False> and C<True> respectively but it has the opposite associativity.
Other programming languages may name their corresponding I<null coalescing>
operators I<?:> or I<//> or I<NVL> or I<ISNULL>.

=head2 anticoalesce !!

        anticoalesce : (\Function : (
            returns : \$Any,
            matches : (\$Any, \$Any),
            is_associative : True,
            is_idempotent : True,
            left_identity : True,
            evaluates : (if Excuse args:.\0 then args:.\0 else args:.\1),
        )),

        '!!' : (\Alias : ( of : \$anticoalesce, )),

The function C<anticoalesce> aka C<!!> results in its C<0> argument iff the
latter is an C<Excuse>, and results in its C<1> argument otherwise.
This function is designed to be chained for any number of sequenced values
in order to pick the first C<Excuse> in a list.
This function has analogous stop-or-continue behaviour to the Muldis D
special syntax C<and_then> where any C<Excuse> or non-C<Excuse> stands in
for C<False> and C<True> respectively but it has the opposite associativity.

=head1 SOURCE CODE BEHAVIOURAL DATA TYPES

I<TODO.>

=head2 Package

        Package : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \Package,
                attrs : (
                    identity : \Package::Identity::(),
                    foundation : \Package::Foundation::(),
                    uses : \Package::Uses_Map::(),
                    entry : \Package::Entry_Point::(),
                    floating : \Package::Floating::(),
                    materials : \Package::Folder::(),
                ),
            )),
            default : ...,
        )),

I<TODO.>

=head2 Package::Identity

        Package::Identity : (\Function : (
            is_type_definer : True,
            evaluates : (
                package_base_name : \Package::Base_Name::(),
                authority : \Package::Canon_Authority::(),
                version_number : \Package::Canon_Version_Number::(),
            ),
        )),

=head2 Package::Foundation

        Package::Foundation : (\Function : (
            is_type_definer : True,
            evaluates : (
                authority : \Package::Canon_Authority::(),
                version_number : \Package::Canon_Version_Number::(),
            ),
        )),

=head2 Package::Base_Name

        Package::Base_Name : (\Function : (
            is_type_definer : True,
            evaluates : [\Nesting::(), \so::(), \'∌'::( 1: \'' )],
        )),

I<TODO.>

=head2 Package::Canon_Authority

        Package::Canon_Authority : (\Function : (
            is_type_definer : True,
            evaluates : [\Text::(), \so::()],
        )),

I<TODO.>

=head2 Package::Canon_Version_Number

        Package::Canon_Version_Number : (\Function : (
            is_type_definer : True,
            evaluates : [\Text::(), \so::()],
        )),

I<TODO.>

=head2 Package::Uses_Map

        Package::Uses_Map : (\Function : (
            is_type_definer : True,
            evaluates : [\Tuple::(), \'.!?'::( 1: \'' ),
                \all_attr_assets::( 1: \Package::Uses_Item() )],
        )),

I<TODO.  Each attribute name declares a single-element composing-package-local
alias for the used package.>

=head2 Package::Uses_Item

        Package::Uses_Item : (\Function : (
            is_type_definer : True,
            evaluates : (
                package_base_name : \Package::Base_Name::(),
                authority : \Package::Canon_Authority::(),
                version_number : \Package::Canon_Version_Number::(),
            ),
        )),

I<TODO.  This type should be more complicated in order to support indicating
eg multiple authorities or version numbers including in interval format and
also indicate both positive or negative assertions of compatibility.
Until then, this type just represents a single positive assertion.>

=head2 Package::Entry_Point

        Package::Entry_Point : (\Function : (
            is_type_definer : True,
            evaluates : \Absolute_Name::(),
        )),

I<TODO.  This type is subject to be expanded to some collection or have alternatives.>

=head2 Package::Floating

        Package::Floating : (\Function : (
            is_type_definer : True,
            evaluates : [\Set::(), \all::( 1: \Absolute_Name::() )],
        )),

I<TODO.>

=head2 Package::Folder

        Package::Folder : (\Function : (
            is_type_definer : True,
            evaluates : [\Tuple::(),
                \all_attr_assets::( 1: {\Package::Folder(), \Material::()} )],
        )),

I<TODO.>

=head2 Material

        Material : (\Function : (
            is_type_definer : True,
            evaluates : {\$Alias, \$Function, \$Procedure},
            default : ...,
        )),

=head2 Alias

        Alias : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \Alias,
                attrs : (
                    of : \Identity_Identifier(),
                ),
            )),
            default : ((\Alias : (of : Identity_Identifier::(),))),
        )),

I<TODO.  Also possibly use something other than Identity_Identifier as payload.>

=head2 Function

        Function : (\Function : (
            is_type_definer : True,
            evaluates : (
                function ::= args:.\0;

                returns Article function
                    and_then guard function:< = \Function
                    and_then guard when_well_formed_Article;

                traits ::= function:>;

                when_well_formed_Article ::=
                    if traits .:? (\is_type_definer, True) then
                        if traits .:? (\is_generalization, True) then
                            when_generalized_type_definer
                        else if traits.?\constant then
                            when_singleton_type_definer
                        else
                            when_regular_type_definer
                    else
                        when_regular_function;

                when_generalized_type_definer ::=
                    traits is_a (\$Signature::Tuple_Attrs_Match : (attrs : (
                        is_type_definer : (type : \True::(),),
                        is_generalization : (type : \True::(),),
                        default : (type : \Expression::(), optional : True),
                    ) %+ type_specialization_attrs_template,))
                    and_then type_specialization_attrs_constraint;

                when_singleton_type_definer ::=
                    traits is_a (\$Signature::Tuple_Attrs_Match : (attrs : (
                        is_type_definer : (type : \True::(),),
                        is_generalization : (type : \False::(), optional : True),
                        constant : (type : \Expression::(),),
                    ) %+ type_specialization_attrs_template,))
                    and_then type_specialization_attrs_constraint;

                when_regular_type_definer ::=
                    traits is_a (\$Signature::Tuple_Attrs_Match : (attrs : (
                        is_type_definer : (type : \True::(),),
                        is_generalization : (type : \False::(), optional : True),
                        evaluates : (type : {\Expression::(), \Signature::()},),
                        default : (type : \Expression::(), optional : True),
                    ) %+ type_specialization_attrs_template,))
                    and_then type_specialization_attrs_constraint;

                type_specialization_attrs_template ::=
                    (
                        composes : (type : \Set_of_Identity_Identifier::(), optional : True),
                        provides_default_for : (type : \Set_of_Identity_Identifier::(), optional : True),
                    );

                type_specialization_attrs_constraint ::=
                    if traits.?\provides_default_for then
                        traits.?\composes
                        and_then guard traits:.\provides_default_for ⊆ traits:.\composes
                    else
                        True;

                when_regular_function ::=
                    traits is_a (\$Signature::Tuple_Attrs_Match : (attrs : (
                        is_type_definer : (type : \False::(), optional : True),
                        virtual : (type : \Boolean::(), optional : True),
                        commutes : (type : \Identity_Identifier::(), optional : True),
                        negates : (type : \Identity_Identifier::(), optional : True),
                        returns : (type : \Signature::(), optional : True),
                        matches : (type : \Signature::Tuple_Attrs_Match_Simple(), optional : True),
                        implements : (type : ..., optional : True),
                        overrides : (type : ..., optional : True),
                        accepts : (type : \Expression::(), optional : True),
                        intends : (type : \Expression::(), optional : True),
                        is_associative : (type : \Boolean::(), optional : True),  `two-way associative`
                        is_commutative : (type : \Boolean::(), optional : True),
                        is_idempotent : (type : \Boolean::(), optional : True),
                        identity : (type : \Expression::(), optional : True),  `iff two-sided identity element exists`
                        left_identity : (type : \Expression::(), optional : True),  `iff only left-identity element exists`
                        right_identity : (type : \Expression::(), optional : True),  `iff only right-identity element exists`
                        repeater : (type : ..., optional : True),
                        evaluates : (type : \Expression::(), optional : True),
                    ),));
            ),
            default : ...,
        )),

I<TODO.>

I<TODO.  Add is_opaque trait, normally just (or possibly reserved for) used
in the Foundation pseudo-package, but that (possibly) may also be used in
other packages to indicate the package interfaces to some host-provided thing
without going through a Foundation-provided wrapper.>

I<TODO.  A "singleton type definer" always has a "constant" attribute and optionally has a
"composes" attribute; it is always lacking in the other attributes.
An "interface type definer" always has a True "is_generalization" attribute and
optionally has a "composes" attribute; it is always lacking in the other attributes.
A "selection type definer" always lacks an "is_generalization" attribute
(or has a False one) and always lacks a "constant" attribute; it may have any
of the other attributes.>

I<TODO.  Re-add the 'folded' of a 'singleton type definer' into an expression type or such.>

I<TODO.  A Function represents a generic expression to be evaluated at a
future date, which also has its own lexical and argument scopes.
Each Expression tree used as the value of an applicable trait of a Function has
its own isolated lexical scope and its own `args` context where generally the
`args` has the same value in every trait of a Function for a call to said.>

=head2 Procedure

        Procedure : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \Procedure,
                attrs : (\$Signature::Tuple_Attrs_Match : (attrs : (
                    virtual : (type : \Boolean::(), optional : True),
                    matches : (type : \Signature::Tuple_Attrs_Match_Simple(), optional : True),
                    implements : (type : ..., optional : True),
                    overrides : (type : ..., optional : True),
                    accepts : (type : \Expression::(), optional : True),
                    intends : (type : \Expression::(), optional : True),
                    performs : (type : \Statement::(), optional : True),
                ),)),
            )),
            default : ...,
        )),

I<TODO.>

=head2 Signature

        Signature::'' : (\Function : (
            is_type_definer : True,
            evaluates : {
                \$Function_Call_But_0,
                \$Signature::Conjunction,
                \$Signature::Disjunction,
                \$Signature::Tuple_Attrs_Match_Simple,
                \$Signature::Tuple_Attrs_Match,
                \$Signature::Article_Match,
            },
            default : \Any::(),
        )),

I<TODO.>

=head2 Signature::Conjunction

        Signature::Conjunction : (\Function : (
            is_type_definer : True,
            evaluates : [\Array::(), \all::( 1: \Signature::() )],
        )),

I<TODO.>

=head2 Signature::Disjunction

        Signature::Disjunction : (\Function : (
            is_type_definer : True,
            evaluates : [\Set::(), \all::( 1: \Signature::() )],
        )),

I<TODO.>

=head2 Signature::Tuple_Attrs_Match_Simple

        Signature::Tuple_Attrs_Match_Simple : (\Function : (
            is_type_definer : True,
            evaluates : [\Tuple::(), \all_attr_assets::( 1: \Signature::() )],
        )),

I<TODO.>

=head2 Signature::Tuple_Attrs_Match

        Signature::Tuple_Attrs_Match : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    attrs : [
                        \Tuple::(),
                        \all_attr_assets::( 1: \(
                            attr_sig ::= args:.\0;
                            returns
                                attr_sig ⊆$ \$(type,optional)
                                and
                                (attr_sig.?\type and_then guard Signature attr_sig.\type)
                                and
                                if attr_sig.?\optional
                                    then guard Boolean attr_sig.\optional
                                    else True
                            ;
                        ) )
                    ],
                ),
            )),
        )),

I<TODO.>

=head2 Signature::Article_Match

        Signature::Article_Match : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \Any::(),
                attrs : \Signature::(),
            )),
        )),

I<TODO.>

=head2 Expression

        Expression : (\Function : (
            is_type_definer : True,
            evaluates : {
                \$Literal,
                \$Args,
                \$Evaluates,
                \$Array_Selector,
                \$Set_Selector,
                \$Bag_Selector,
                \$Tuple_Selector,
                \$Article_Selector,
                \$If_Then_Else_Expr,
                \$And_Then,
                \$Or_Else,
                \$Given_When_Default_Expr,
                \$Guard,
                \$Factorization,
                \$Expansion,
                \$Vars,
                \$New,
                \$Current,
            },
            default : (\$Literal : (False,)),
        )),

I<TODO.  This represents a generic expression to be evaluated at a future date.
Its lexical scope and `args` context is the innermost Function/Procedure (trait) containing it.>

=head2 Literal

        Literal : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Any::(),
                ),
            )),
            default : (\$material : (False,)),
        )),

I<TODO.  This represents an expression that evaluates to yield the exact
same value as its payload attribute value in the source code.  Typically a
Literal node is the result of constant expression folding which in the
trivial case is just the result of a normal source code literal like `3`.>

I<However one can use the Plain_Text `literal` prefix keyword to turn
anything that might be interpreted as a non-literal expression into a
literal expression, in particular nested routine definitions with their
own `args` that one wants to execute "later" rather than "now"; for example
`add_42 ::= literal (\Function : (evaluates : (args:.\0 + 42)))`; without
the `literal` the `args` would be interpreted as the `args` of the routine
containing the `add_42` and not the `args` of the nested routine.>

=head2 Args

        Args : (\Function : (
            is_type_definer : True,
            constant : \$material,
        )),

I<TODO.  This represents an expression that evaluates to the Tuple of
input arguments given to the current call of the Function/Procedure
that this Args node is part of.
Written in Plain_Text with the token `args`.>

=head2 Evaluates

        Evaluates : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),
                ),
            )),
            default : (\$material : ...),
        )),

I<TODO.  This represents a generic function invocation expression,
where its sole sub-expression names or defines the function to invoke and
also defines the arguments to pass to said function while invoking it,
written in Plain_Text with special syntax example `evaluates X`
where X denotes a Function_Call value.>

I<Note: Visual Basic has the 'Nothing' keyword that represents the default
value of the type of the variable it is assigned to.>

=head2 Array_Selector

        Array_Selector : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : [\Array::(), \all::( 1: (
                        0 : \Expression::(),  `member value`
                        1 : \Expression::(),  `multiplicity`
                    ) )],
                ),
            )),
            default : (\$material : ([],)),
        )),

I<TODO.  This represents a selection of an Array value in terms of a list
of multiplied members; each member value and its given multiplicity comes
from a child expression.  Written in Plain_Text like `["hello":3,-5:2]`.
This is intended as a convenient shorthand for a tree of Literal+Evaluates.>

=head2 Set_Selector

        Set_Selector : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : [\Bag::(), \all::( 1: (
                        0 : \Expression::(),  `member value`
                        1 : \Expression::(),  `multiplicity`
                    ) )],
                ),
            )),
            default : (\$material : ({0:0},)),
        )),

I<TODO.  This represents a selection of an Set value in terms of a list
of multiplied members; each member value and its given multiplicity comes
from a child expression.  Written in Plain_Text like `{"hello",-5}`.
While Sets have no duplicate values, Set_Selector has common syntax with
Array_Selector and Bag_Selector in allowing one to specify a multiplicity
to support greater code reuse, including explicitly specifying one vs zero.
This is intended as a convenient shorthand for a tree of Literal+Evaluates.>

=head2 Bag_Selector

        Bag_Selector : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : [\Bag::(), \all::( 1: (
                        0 : \Expression::(),  `member value`
                        1 : \Expression::(),  `multiplicity`
                    ) )],
                ),
            )),
            default : (\$material : ({0:0},)),
        )),

I<TODO.  This represents a selection of an Bag value in terms of a list
of multiplied members; each member value and its given multiplicity comes
from a child expression.  Written in Plain_Text like `{"hello":3,-5:2}`.
This is intended as a convenient shorthand for a tree of Literal+Evaluates.>

=head2 Tuple_Selector

        Tuple_Selector : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : [\Tuple::(), \all_attr_assets::( 1: \Expression::() )],
                ),
            )),
            default : (\$material : (())),
        )),

I<TODO.  This represents a selection of a Tuple value in terms of a list
of attributes; each attribute name is specified directly, each attribute asset comes
from a child expression.  Written in Plain_Text like `(name:"Jo",age:7)`.
This is intended as a convenient shorthand for a tree of Literal+Evaluates.>

=head2 Article_Selector

        Article_Selector : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),  `label`
                    1 : \Expression::(),  `attributes`
                ),
            )),
            default : (\$material : ((\$folder::Literal : (\'',)),
                (\$folder::Literal : ()))),
        )),

I<TODO.  This represents a selection of a Article value in terms of a
label plus a list of attributes, each of which comes from a child
expression.  Written in Plain_Text like `(\Person:(name:"Jo",age:7))`.
This is intended as a convenient shorthand for a tree of Literal+Evaluates.>

=head2 If_Then_Else_Expr

        If_Then_Else_Expr : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),
                    1 : \Expression::(),
                    2 : \Expression::(),
                ),
            )),
            default : (\$material : ((\$Literal : (False,)),
                (\$Literal : (False,)), (\$Literal : (False,)))),
        )),

I<TODO.  This represents an if-then-else expression,
written in Plain_Text with special syntax example `if P then X else Y`.>

=head2 And_Then

        And_Then : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),
                    1 : \Expression::(),
                ),
            )),
            default : (\$material : ((\$Literal : (False,)),
                (\$Literal : (False,)))),
        )),

I<TODO.  This represents an and-then expression,
written in Plain_Text with special syntax example `P and_then X`.>

I<Note: Visual Basic has "AndAlso" operator that does the same thing.>

=head2 Or_Else

        Or_Else : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),
                    1 : \Expression::(),
                ),
            )),
            default : (\$material : ((\$Literal : (False,)),
                (\$Literal : (False,)))),
        )),

I<TODO.  This represents an or-else expression,
written in Plain_Text with special syntax example `P or_else X`.>

I<Note: Visual Basic has "OrElse" operator that does the same thing.>

=head2 Given_When_Default_Expr

        Given_When_Default_Expr : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),
                    1 : [\Set::(), \all::( 1: (
                        0 : \Expression::(),
                        1 : \Expression::(),
                    ) )],
                    2 : \Expression::(),
                ),
            )),
            default : (\$material : ((\$Literal : (False,)),
                {}, (\$Literal : (False,)))),
        )),

I<TODO.  This represents a given-when-default expression,
written in Plain_Text with special syntax example
`given X when A then B when C then D default Y`.>

=head2 Guard

        Guard : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),
                ),
            )),
            default : (\$material : ((\$Literal : (False,)),)),
        )),

I<TODO.  This represents a guard expression,
written in Plain_Text with special syntax example `guard X`.>

=head2 Factorization

        Factorization : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    factors : [\Tuple::(), \all_attr_assets::( 1: \Expression::() )],
                    returns : \Expression::(),
                ),
            )),
            default : (\$material : (factors : (), returns : (\$Literal : (False,)))),
        )),

I<TODO.  This represents a compound expression, which consists of a single
"returns" component plus a list of 0..N lexically labelled expression factors,
written in Plain_Text with special syntax example `(x ::= y+3; returns 2*x)`.
When one (less commonly) wants to simply name an expression factor not
conceptually part of a compound expression, the alternate Plain Text syntax
example `foo ::= 42` is shorthand for `(foo ::= 42; returns foo)`.>

I<TODO.  Consider provision for declaring standalone factors where
procedure statements are allowed, which would normally look a lot like an
assignment statement or be confused for such; in the meantime, procedure
factors may only be declared in sub-expressions of statements though they
can be re-used in other statements.>

=head2 Expansion

        Expansion : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    factor : \Attr_Name::(),
                ),
            )),
            default : (\$material : (factor : \'',)),
        )),

I<TODO.  This represents a logical expansion of an expression factor at this
spot that was previously declared within a Factorization, written in Plain_Text
as an unqualified alphanumeric or quoted identifier example `foo`.
Note that it is an error for a factor to contain any Expansion referring
to itself, either directly or indirectly (except via a routine call).>

=head2 Vars

        Vars : (\Function : (
            is_type_definer : True,
            constant : \$material,
        )),

I<TODO.  This represents a procedure expression that evaluates to a
Variable whose current value is a Tuple whose attributes are intended to be
used as procedure body lexical variables.  Written in Plain_Text with the
token `vars`.  On entry to a procedure call a new Variable is created for
"vars" and given the nullary Tuple as its initial current value; this
Variable is usually destroyed when that procedure exits, unless some other
Variable has been made to reference it directly or indirectly meanwhile.>

=head2 New

        New : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),
                )
            )),
            default : (\$material : ((\$Literal : (False,)),)),
        )),

I<TODO.  This represents a procedure expression that evaluates to a newly
created "Variable" whose initial current value is given by evaluating its
sub-expression.  Written in Plain_Text with syntax example `new foo`.>

=head2 Current

        Current : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),
                ),
            )),
            default : (\$material : ((\$New : ((\$Literal : (False,)),)),)),
        )),

I<< TODO.  This represents a procedure expression that evaluates to the
"current value" of the "Variable" that its sub-expression evaluates to.
Written in Plain_Text with syntax example `current bar` or `bar:&`.
Note that C<Current> is designed to mirror C<New>, so the identity
C<x = (new x):& = current new x> should hold for any value of C<x>. >>

=head2 Statement

        Statement : (\Function : (
            is_type_definer : True,
            evaluates : {
                \$Declare,
                \$Performs,
                \$If_Then_Else_Stmt,
                \$Given_When_Default_Stmt,
                \$Block,
                \$Leave,
                \$Iterate,
            },
            default : (\$Block : ([],)),
        )),

I<TODO.  This represents a generic statement to be performed at a future date.
Its lexical scope and `args` context is the innermost Procedure (trait) containing it.>

=head2 Declare

        Declare : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    vars : [\Tuple::(), \all_attr_assets::( 1: \Expression::() )],
                ),
            )),
            default : (\$material : (vars : (),)),
        )),

I<TODO.  This represents a procedure body statement that declares 0..N
lexically labelled expression factors, each of which is intended to be a
convenient shorthand for referring to a procedure body lexical variable.
To be specific, using Plain_Text syntax examples, the statement
`declare (foo:42, bar:"Hello", baz:{})` is strictly a shorthand for
`vars := vars:& %+ (foo ::= vars:&.\foo; bar ::= vars:&.\bar; baz ::= vars:&.\baz;
returns (foo : new 42, bar : new "Hello", baz : new {}))`.
Or, the parens may be omitted for singles, for example `declare foo:42`.
Note that in theory the fact this is a shorthand means that `declare` might
best just exist as a Plain_Text feature and be rendered in its longhand
at this core data types level; on the other hand we have types for And_Then
plus Or_Else and justified keeping them; its a similar situation.>

=head2 Performs

        Performs : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),
                ),
            )),
            default : (\$material : ...),
        )),

I<TODO.  This represents a generic procedure invocation statement,
where its sole sub-expression names or defines the procedure to invoke and
also defines the arguments to pass to said procedure while invoking it,
written in Plain_Text with special syntax example `performs X`
where X denotes a Procedure_Call value.>

=head2 If_Then_Else_Stmt

        If_Then_Else_Stmt : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),
                    1 : \Statement::(),
                    2 : \Statement::(),
                ),
            )),
            default : (\$material : ((\$Literal : (False,)),
                (\$Block : ([],)), (\$Block : ([],)))),
        )),

I<TODO.  This represents an if-then-else statement,
written in Plain_Text with special syntax example `if P then X else Y`.
Note that a plain if-then statement is shorthand for this, in which case
the 'else' is an empty compound statement.>

=head2 Given_When_Default_Stmt

        Given_When_Default_Stmt : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    0 : \Expression::(),
                    1 : [\Set::(), \all::( 1: (
                        0 : \Expression::(),
                        1 : \Statement::(),
                    ) )],
                    2 : \Statement::(),
                ),
            )),
            default : (\$material : ((\$Literal : (False,)),
                {}, (\$Block : ([],)))),
        )),

I<TODO.  This represents a given-when-default statement,
written in Plain_Text with special syntax example
`given X when A then B when C then D default Y`.>

=head2 Block

        Block : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (\$Signature::Tuple_Attrs_Match : (attrs : (
                    0 : (type : [\Array::(), \all::( 1: \Statement::() )]),
                    label : (type : \Attr_Name::(), optional : True),
                ),)),
            )),
            default : (\$material : ([],)),
        )),

I<TODO.  This represents an optionally labelled statement block, which
consists of an ordered list of 0..N statements, which optionally may be
iterated, written in Plain_Text with special syntax example
`[declare x: 42; print(x:&);]` or `do_work block [foo(); bar();]`.>

=head2 Leave

        Leave : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (\$Signature::Tuple_Attrs_Match : (attrs : (
                    label : (type : \Attr_Name::(), optional : True),
                ),)),
            )),
            default : (\$material : ()),
        )),

I<TODO.  This represents an instruction to abnormally exit the statement
block defined by a specified parent Block node (a normal exit is to simply
execute to the end of the statement block); if an explicit label is given,
the ancestor Block with that label is the one abnormally exited, and
otherwise the most immediate parent Block is the one; written in Plain_Text
with special syntax examples `leave` or `leave do_work`.>

=head2 Iterate

        Iterate : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (\$Signature::Tuple_Attrs_Match : (attrs : (
                    label : (type : \Attr_Name::(), optional : True),
                ),)),
            )),
            default : (\$material : ()),
        )),

I<TODO.  This represents an instruction to immediately exit, and then
continue execution at the start of, the statement block defined by a
specified parent Block node; if an explicit label is given, the ancestor
Block with that label is the one iterated, and otherwise the most immediate
parent Block is the one; written in Plain_Text
with special syntax examples `iterate` or `iterate do_work`.>

=head2 Heading

        Heading : (\Function : (
            is_type_definer : True,
            evaluates : [\Tuple::(), \all_attr_assets::( 1: \True::() )],
        )),

I<TODO.>

I<TODO.  For the likes of all_attrs etc consider making args:.\0 a unary tuple
instead whereupon keywords analagous to name/asset are used, if we had such
a thing for opening tuples as for creating them.>

=head2 Attr_Name

        Attr_Name : (\Function : (
            is_type_definer : True,
            evaluates : [\Heading::(), \is_unary::()],
        )),

I<TODO.>

=head2 Nesting

        Nesting : (\Function : (
            is_type_definer : True,
            evaluates : [\Array::(), \all::( 1: \Attr_Name::() )],
        )),

I<TODO.>

=head2 Local_Name

        Local_Name : (\Function : (
            is_type_definer : True,
            evaluates : [\Nesting::(), \so::(), \(
                given args:.\0.0
                    when \foundation then #args:.\0 = 2
                    when \used       then #args:.\0 ≥ 2  `elem 2 is pkg local alias`
                    when \package    then #args:.\0 ≥ 1
                    when \folder     then #args:.\0 ≥ 1
                    when \material   then #args:.\0 = 1
                    when \floating   then #args:.\0 ≥ 2
                    default False
            )],
            default : [\foundation, ...],
        )),

I<TODO.>

=head2 Absolute_Name

        Absolute_Name : (\Function : (
            is_type_definer : True,
            evaluates : (Local_Name args:.\0 and_then guard
                args:.\0.0 ⊆$ \$(foundation,used,package)),
        )),

I<TODO.>

=head2 Routine_Call

        Routine_Call : (\Function : (
            is_type_definer : True,
            evaluates : (\$Signature::Article_Match : (
                label : \$material,
                attrs : (
                    call : {\Local_Name::(), \Identity_Identifier::(), \Function::(), \Procedure::()},
                    args : \Tuple::(),
                ),
            )),
            default : \foundation::False(),
        )),

I<TODO.  This represents a specification of a function or procedure invocation,
and names or defines the function or procedure to invoke and
also defines the arguments to pass to said function or procedure while invoking it,
written in Plain_Text with special syntax example `\foo::()` or `\(...)` or `\[...]`.>

I<TODO.  See also and update the documentation or bodies of Homogeneous or
Unionable or etc other operators to more explicitly take/use Routine_Call
where they have better knowledge of its structure now that its more nailed
down than before.>

I<TODO.  Consider whether Routine_Call needs to be stricter eg requiring
Absolute_Name rather than Local_Name, or whether or not it needs any extra
attributes for caller context.  But to avoid over-engineering, don't worry
about those / assume they are not needed here, until such time we actually
are trying to make the code execute.  Users shouldn't have to be writing
such manually anyhow, the runtime environment should fill them in as needed.>

=head2 Function_Call

        Function_Call : (\Function : (
            is_type_definer : True,
            evaluates : [
                \Routine_Call::(),
                \(args:.\0:>.\asset:.\call is_a {\Local_Name::(), \Identity_Identifier::(), \Function::()}),
            ],
            default : \(False),
        )),

I<TODO.  This represents a specification of a function invocation,
and names or defines the function to invoke and
also defines the arguments to pass to said function while invoking it,
written in Plain_Text with special syntax example `\foo::()` or `\(...)`.>

=head2 Function_Call_But_0

        Function_Call_But_0 : (\Function : (
            is_type_definer : True,
            evaluates : (Function_Call args:.\0 and_then guard
                args:.\0.\args disjoint_heading \0),
        )),

I<TODO.>

=head2 Function_Call_But_0_1

        Function_Call_But_0_1 : (\Function : (
            is_type_definer : True,
            evaluates : (Function_Call args:.\0 and_then guard
                args:.\0.\args disjoint_heading \$(0..1)),
        )),

I<TODO.>

=head2 Procedure_Call

        Procedure_Call : (\Function : (
            is_type_definer : True,
            evaluates : [
                \Routine_Call::(),
                \(args:.\0:>.\asset:.\call is_a {\Local_Name::(), \Identity_Identifier::(), \Procedure::()}),
            ],
            default : \[],
        )),

I<TODO.  This represents a specification of a procedure invocation,
and names or defines the procedure to invoke and
also defines the arguments to pass to said procedure while invoking it,
written in Plain_Text with special syntax example `\foo::()` or `\[...]`.>

=head2 Key_Asset_Pair

        Key_Asset_Pair : (\Function : (
            is_type_definer : True,
            evaluates : (Tuple args:.\0 and_then guard
                $args:.\0 = \$(key,asset)),
        )),

I<TODO.  Note, this type currently isn't used anywhere.>

=head2 with_args

        with_args : (\Function : (
            returns : \$Routine_Call,
            matches : (\$Routine_Call, \$Tuple),
            evaluates : (\$Routine_Call : (
                call : args:.\0:>.\call,
                args : args:.\0:>.\args %+ args:.\1,
            )),
        )),

        '<--' : (\Alias : ( of : \$with_args, )),

I<TODO.  This adds to the list of arguments for routine call.  It is
functionally equivalent to "priming" or "partial function application".
A Raku corresponding operator has the name "assuming".>

=head2 priming

        priming : (\Function : (
            commutes : \$assuming,
        )),

        '-->' : (\Alias : ( of : \$priming, )),

I<TODO.  This also adds to the list of arguments for a routine call.>

=head2 Signature_to_Function_Call_But_0

        Signature_to_Function_Call_But_0 : (\Function : (
            returns : \$Function_Call_But_0,
            matches : (\$Signature,),
            evaluates : (
                sig ::= args:.\0;
                returns
                    if Function_Call_But_0 sig then
                        sig
                    else if Signature::Conjunction sig then guard
                        \(
                            topic    ::= args:.\0;
                            conj_sig ::= args:.\sig;
                            returns empty::(conj_sig)
                                or_else guard topic is_a first::(conj_sig)
                                and_then guard topic is_a nonfirst::(conj_sig)
                        ) <-- (:sig,)
                    else if Signature::Disjunction sig then guard
                        \(
                            topic    ::= args:.\0;
                            disj_sig ::= args:.\sig;
                            returns disj_sig any \(args:.\topic is_a args:.\0) <-- (:topic,)
                        ) <-- (:sig,)
                    else if Signature::Tuple_Attrs_Match_Simple sig then guard
                        \(
                            topic     ::= args:.\0;
                            tuple_sig ::= args:.\sig;
                            returns Tuple topic
                                and_then guard topic =$ tuple_sig
                                and_then guard
                                    tuple_sig all_attrs \(
                                        name     ::= args:.\0:.\name;
                                        attr_sig ::= args:.\0:.\asset;
                                        topic    ::= args:.\topic;
                                        returns topic.name is_a attr_sig;
                                    ) <-- (:topic,)
                        ) <-- (:sig,)
                    else if Signature::Tuple_Attrs_Match sig then guard
                        \(
                            topic     ::= args:.\0;
                            tuple_sig ::= args:.\sig;
                            attrs_sig ::= tuple_sig:>.\attrs;
                            returns Tuple topic
                                and_then guard
                                    topic ⊆$ attrs_sig
                                    and
                                    (attrs_sig all_attrs \(
                                        name     ::= args:.\0:.\name;
                                        attr_sig ::= args:.\0:.\asset;
                                        topic    ::= args:.\topic;
                                        returns if topic.?name
                                            then guard topic.name is_a attr_sig
                                            else attr_sig.?\optional
                                        ;
                                    ) <-- (:topic,))
                        ) <-- (:sig,)
                    else if Signature::Article_Match sig then guard
                        \(
                            topic       ::= args:.\0;
                            article_sig ::= args:.\sig:>;
                            returns Article topic
                                and_then guard topic:< = article_sig:.\label
                                and_then guard topic:> is_a article_sig:.\attrs
                        ) <-- (:sig,)
                    else
                        fail  `We should never get here.`
                    ;
            ),
        )),

I<TODO.  Also double-check that all_attrs etc are not infinite-recursing
by way of this function.  Also make sure Foundation implementations can
recognize and short-circuit the simple boolean signatures.>

I<WARNING/TODO.  Actually, now that regular types are largely defined in
terms of Signature, it is very likely we would have infinite recursion at
runtime due to for example any code asking "is this a Signature::foo" which
includes Signature_to_Function_Call_But_0 would be calling
Signature_to_Function_Call_But_0 in order to answer that question, at least
until something is changed that the compiler folds the type definitions to
no longer use Signature_to_Function_Call_But_0.>

=head1 SOURCE CODE ANNOTATION DATA TYPES

I<TODO.>

=head2 Annotation

        Annotation::'' : (\Function : (
            is_type_definer : True,
            is_generalization : True,
        )),

I<TODO.>

I<Annotations can include what otherwise would be a separate special table
for higher level DBMS mappers such as Entity Framework or DBIx::Class to
track their own metadata within a database, such as an easy way to tell if
the database schema version matches what the application expects.>

=head1 SOURCE CODE DECORATION DATA TYPES

I<TODO.  Generally speaking, all Decoraction types are declared by a
package that is not System, such as in some System::Plain_Text/etc package.>

I<TODO:  The decoration data types may loosely resemble template defs so
that decorations don't necessarily have to be attached to all
behavioural/annotation nodes they are conceptually adjacent / apply to but
rather can be in some nearby parent context and refer to behavioural/etc
nodes by name.>

I<When representing extra info used by eg EF/DBIC, that should be split
between Annotation and Decoration depending on whether it affects behaviour
in any way / would cause a problem if missing, or if it is purely cosmetic
and would not cause a problem if missing.>

=head2 Decoration

        Decoration::'' : (\Function : (
            is_type_definer : True,
            is_generalization : True,
        )),

I<TODO.>

=head1 AUTHOR

Darren Duncan (C<darren@DarrenDuncan.net>)

=head1 LICENSE AND COPYRIGHT

This file is part of the formal specification of the B<Muldis D Standard
Library> (B<MDSL>) primary component of the B<Muldis D> language
specification.  MDSL substantially comprises executable code as well.

MDSL is Copyright © 2002-2018, Muldis Data Systems, Inc.

See the LICENSE AND COPYRIGHT of L<Muldis::D::Standard_Library> for details.

=cut
